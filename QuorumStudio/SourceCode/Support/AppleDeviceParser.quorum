package Libraries.Development.Environment.Studio.Mobile
use Libraries.Containers.Array
use Libraries.System.ProcessListener
use Libraries.System.Process
use Libraries.System.ProcessEvent

class AppleDeviceParser is ProcessListener
    boolean isDevice = false
    boolean isSimulator = false

    Array<AppleDevice> devices

    action ProcessStarted(ProcessEvent event)

    end

    action ProcessStopped(ProcessEvent event)
    end

    action Error(ProcessEvent event)
        ProcessLine(event:GetOutput())
    end

    action Output(ProcessEvent event)
        ProcessLine(event:GetOutput())
    end

    action ProcessLine(text line)
        text value = line
        if line = undefined or line:IsEmpty()
            return now
        end

        if value = "== Devices =="
            isDevice = true
            isSimulator = false
        elseif value = "== Simulators =="
            isDevice = false
            isSimulator = true
        else
            AppleDevice device
            if isDevice
                device:ParseDevice(line)
            elseif isSimulator
                device:ParseSimulator(line)
            end
            
            if device not= undefined and device:IsParsed()
                devices:Add(device)
            end
        end
    end

    action GetDevices returns Array
        return devices
    end
end