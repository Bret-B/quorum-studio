package Libraries.Development.Environment.Studio.Interface

use Libraries.Interface.Controls.Dialog
use Libraries.Interface.Controls.TextField
use Libraries.Interface.Controls.Checkbox
use Libraries.Interface.Controls.Button
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Color
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Game.Graphics.Label
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Game.GameStateManager
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Containers.Array
use Libraries.Containers.Support.Pair
use Libraries.Development.Environment.Studio.Behaviors.WholeWordMatchCaseBehavior
use Libraries.Development.Environment.Studio.Behaviors.PreviousBehavior
use Libraries.Development.Environment.Studio.Behaviors.NextBehavior
use Libraries.Interface.Events.TextChangeListener
use Libraries.Interface.Controls.TextStyles.ColoredHighlight
use Libraries.Interface.Controls.TextStyles.TextStyle
use Libraries.Interface.Events.TextChangeEvent
use Libraries.Development.Environment.Studio.Behaviors.ReplaceAllBehavior
use Libraries.Development.Environment.Studio.Behaviors.ReplaceBehavior
use Libraries.Interface.Events.KeyboardListener
use Libraries.Development.Environment.Studio.Behaviors.FindBehavior
use Libraries.Interface.Events.KeyboardEvent
use Libraries.Game.InputTable
use Libraries.Game.InputSet
use Libraries.Interface.Selections.TabPaneSelection
use Libraries.Interface.Events.SelectionEvent
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.TextBox
use Libraries.Development.Environment.Studio.CodeEditorTabPane


class ReplaceDialog is FindDialog, TextChangeListener, SelectionListener
    boolean showing = false
    boolean initialized = false
    boolean textChangeListenerOn = true     //flag for TextChangeListener for when Replace and ReplaceAll are invoked

    GameStateManager gameManager
    QuorumStudio studio = cast(QuorumStudio, gameManager:GetGame())
    CodeEditorTabPane tabPane = studio:GetTabPane()

    // The TextBox that we're searching through.
    CodeTextBox textBox = undefined

    //ReplaceDialog Controls
    TextField findField
    TextField replaceField
    Label matches = undefined
    Checkbox wholeWord
    Checkbox matchCase
    Button previous
    Button next
    Button replace
    Button replaceAll
    Button findButton

    //ReplaceDialog Control Behaviors
    WholeWordMatchCaseBehavior wholeWordMatchCaseBehavior
    PreviousBehavior previousBehavior 
    NextBehavior nextBehavior 
    ReplaceAllBehavior replaceAllBehavior
    ReplaceBehavior replaceBehavior
    FindBehavior findBehavior

    on create
        findField:AddTextChangeListener(me)
        wholeWordMatchCaseBehavior:SetDialog(cast(Dialog, me))
        wholeWordMatchCaseBehavior:SetDialogType(wholeWordMatchCaseBehavior:REPLACE)
        replaceAllBehavior:SetDialog(me)
        replaceBehavior:SetDialog(me) 
        findBehavior:SetDialog(me)
        tabPane:AddSelectionListener(me)
        SetMatchesLabel(matches)
        SetModal(false)
        //SetBehavior(nextBehavior)
    end

    action OnShow()
        showing = true
        
        findField:Focus()
        wholeWord:SetToggleState(textBox:GetIsWholeWord())
        matchCase:SetToggleState(textBox:GetIsMatchCase())
        findField:SetText(textBox:GetFindWord())
        replaceField:SetText(textBox:GetReplaceWord())
        textBox:SetIsDialogOpen(true)
        if findField:GetText() not= ""
            findButton:Activate()
        end
        
        //Should be "ALT + N" (until ALT key works correctly using the same hotkey as the CodeTextBox)
        KeyboardEvent keys
        text inputGroup = GetInputGroup()
        InputTable dialogInputs = studio:GetInputTable(inputGroup)
        InputSet nextSet 
        nextSet:SetKeyboardInput(keys:F3)
        dialogInputs:Add(nextSet, nextBehavior)

        //Should be "ALT + P" (until ALT key works correctly using the same hotkey as the CodeTextBox)
        InputSet previousSet
        previousSet:SetKeyboardInput(keys:F3)
        previousSet:AddModifier(previousSet:SHIFT)
        dialogInputs:Add(previousSet, previousBehavior)

        //Should be "ALT + R" (until ALT key works correctly using CMD+SHIFT+R)
        InputSet replaceSet
        replaceSet:SetKeyboardInput(keys:R)
        replaceSet:AddModifier(replaceSet:SHIFT)
        dialogInputs:Add(replaceSet, replaceBehavior)

        //Should be "ALT + A" (until ALT key works correctly using CMD+SHIFT+A)
        InputSet replaceAllSet
        replaceAllSet:SetKeyboardInput(keys:A)
        replaceAllSet:AddModifier(replaceAllSet:SHIFT)
        dialogInputs:Add(replaceAllSet, replaceAllBehavior)
        
    end

    action Initialize

        initialized = true

        SetName("Find and Replace")
        SetPixelWidth(300)
        SetVerticalLayoutMode(parent:Libraries.Interface.Controls.Control:FIT_CONTENTS)

        LabelBoxView view
        Color color
        view:SetBorderThickness(1)
        view:SetBorderStyle(view:ALL - view:TOP)
        view:Initialize(color:CustomColor(0.9, 0.9, 0.9, 1), color:Black())
        
        Control panel
        FlowLayout replaceLayout
        panel:SetLayout(replaceLayout)
        panel:SetPercentageWidth(1.0)
        panel:SetPixelHeight(170)
        panel:SetBorderColor(color:Black())
        panel:SetBorderThickness(1)
        panel:Add(CreateReplaceControl())
        panel:Add(CreateMatchesControl())
        panel:SetView2D(view)
        Add(panel)
    end

    private action CreateReplaceControl returns Control
        Color color
        Control createReplaceContainer
        FlowLayout createReplaceLayout
        createReplaceContainer:SetLayout(createReplaceLayout)
        createReplaceContainer:SetBorderThickness(1)
        createReplaceContainer:SetBorderColor(color:Black())
        createReplaceContainer:SetPercentageWidth(1.0)
        createReplaceContainer:SetVerticalLayoutMode(createReplaceContainer:FIT_CONTENTS)

        FlowLayout findLayout
        Control findContainer
        findContainer:SetLayout(findLayout)
        findContainer:SetPercentageWidth(1.0)
        findContainer:SetVerticalLayoutMode(findContainer:FIT_CONTENTS)
        findContainer:SetLeftPadding(20)
        findContainer:SetTopPadding(20)
        findContainer:SetRightPadding(30)

        Label findLabel
        findLabel:SetText("Find:")
        findLabel:SetRightPadding(52)
        findContainer:Add(findLabel)
        
        findField:SetHorizontalLayoutMode(findField:parent:Libraries.Interface.Controls.Control:FILL)
        findField:SetName("Find")
        findField:SetText("")
        findField:SetFocusable(true)
        findField:SetNextFocus(replaceField)
        findField:SetPreviousFocus(replaceAll)
        findButton:SetBehavior(findBehavior)
        findContainer:Add(findField)

        createReplaceContainer:Add(findContainer)

        FlowLayout replaceLayout
        Control replaceContainer
        replaceContainer:SetLayout(replaceLayout)
        replaceContainer:SetPercentageWidth(1.0)
        replaceContainer:SetVerticalLayoutMode(replaceContainer:FIT_CONTENTS)
        replaceContainer:SetLeftPadding(20)
        replaceContainer:SetTopPadding(20)
        replaceContainer:SetRightPadding(30)

        Label replaceLabel
        replaceLabel:SetText("Replace:")
        replaceLabel:SetRightPadding(6)
        replaceContainer:Add(replaceLabel)

        replaceField:SetHorizontalLayoutMode(replaceField:parent:Libraries.Interface.Controls.Control:FILL)
        replaceField:SetName("Replace")
        replaceField:SetText("")
        replaceField:SetFocusable(true)
        replaceField:SetNextFocus(wholeWord)
        replaceField:SetPreviousFocus(findField)
        replaceContainer:Add(replaceField)

        createReplaceContainer:Add(replaceContainer)

        FlowLayout optionsLayout
        Control optionsContainer
        optionsContainer:SetLayout(optionsLayout)
        optionsContainer:SetPercentageWidth(1.0)
        optionsContainer:SetHorizontalLayoutMode(optionsContainer:STANDARD)
        optionsContainer:SetVerticalLayoutMode(optionsContainer:FIT_CONTENTS)
        optionsContainer:SetLeftPadding(10)
        optionsContainer:SetRightPadding(10)
        optionsContainer:SetTopPadding(30)
        
        FlowLayout checkBoxLayout
        Control checkBoxContainer
        checkBoxContainer:SetLayout(checkBoxLayout)
        checkBoxContainer:SetPercentageWidth(0.35)
        checkBoxContainer:SetLeftPadding(6)
        checkBoxContainer:SetRightPadding(20)
        checkBoxContainer:SetHorizontalLayoutMode(checkBoxContainer:STANDARD)
        checkBoxContainer:SetVerticalLayoutMode(checkBoxContainer:FIT_CONTENTS)
        
        wholeWord:SetName("Whole Word")
        wholeWord:SetFontSize(14)
        wholeWord:SetPercentageWidth(1.0)
        wholeWord:SetFocusable(true)
        wholeWord:SetNextFocus(matchCase)
        wholeWord:SetPreviousFocus(replaceField)
        wholeWord:SetBehavior(wholeWordMatchCaseBehavior)
        checkBoxContainer:Add(wholeWord)

        matchCase:SetName("Match Case")
        matchCase:SetFontSize(14)
        matchCase:SetTopPadding(20)
        matchCase:SetPercentageWidth(1.0)
        matchCase:SetFocusable(true)
        matchCase:SetNextFocus(previous)
        matchCase:SetPreviousFocus(wholeWord)
        matchCase:SetBehavior(wholeWordMatchCaseBehavior)
        checkBoxContainer:Add(matchCase)

        optionsContainer:Add(checkBoxContainer)

        FlowLayout buttons1Layout
        Control buttons1Container
        buttons1Container:SetLayout(buttons1Layout)
        buttons1Container:SetPercentageWidth(0.24)
        buttons1Container:SetLeftPadding(20)
        buttons1Container:SetHorizontalLayoutMode(buttons1Container:STANDARD)
        buttons1Container:SetVerticalLayoutMode(buttons1Container:FIT_CONTENTS)

        previous:SetName("Previous")
        previous:SetFontSize(14)
        previous:SetBottomPadding(10)
        previous:SetHorizontalLayoutMode(previous:parent:Libraries.Interface.Controls.Control:STANDARD)
        previous:SetPercentageWidth(1.0)
        previous:SetFocusable(true)
        previous:SetNextFocus(next)
        previous:SetPreviousFocus(matchCase)
        
        replace:SetName("Replace")
        replace:SetFontSize(14)
        replace:SetTopPadding(10)
        replace:SetHorizontalLayoutMode(replace:parent:Libraries.Interface.Controls.Control:STANDARD)
        replace:SetPercentageWidth(1.0)
        replace:SetFocusable(true)
        replace:SetNextFocus(replaceAll)
        replace:SetPreviousFocus(next)
        replace:SetBehavior(replaceBehavior)
        buttons1Container:Add(previous)
        buttons1Container:Add(replace)
        
        optionsContainer:Add(buttons1Container)

        FlowLayout buttons2Layout
        Control buttons2Container
        buttons2Container:SetLayout(buttons2Layout)
        buttons2Container:SetPercentageWidth(0.32)
        buttons2Container:SetLeftPadding(10)
        buttons2Container:SetHorizontalLayoutMode(buttons2Container:STANDARD)
        buttons2Container:SetVerticalLayoutMode(buttons2Container:FIT_CONTENTS)

        next:SetName("Next")
        next:SetFontSize(14)
        next:SetBottomPadding(10)
        next:SetHorizontalLayoutMode(next:parent:Libraries.Interface.Controls.Control:STANDARD)
        next:SetPercentageWidth(1.0)
        next:SetFocusable(true)
        next:SetNextFocus(replace)
        next:SetPreviousFocus(previous)
        
        replaceAll:SetName("Replace All")
        replaceAll:SetFontSize(14)
        replaceAll:SetTopPadding(10)
        replaceAll:SetPercentageWidth(1.0)
        replaceAll:SetHorizontalLayoutMode(replaceAll:parent:Libraries.Interface.Controls.Control:STANDARD)
        replaceAll:SetFocusable(true)
        replaceAll:SetNextFocus(findField)
        replaceAll:SetPreviousFocus(replace)
        replaceAll:SetBehavior(replaceAllBehavior)
        buttons2Container:Add(next)
        buttons2Container:Add(replaceAll)       
        optionsContainer:Add(buttons2Container)       
        createReplaceContainer:Add(optionsContainer)

        return createReplaceContainer
    end

    action IsInitialized returns boolean
        return initialized
    end

    action Show
        if textBox not= undefined
            LayoutProperties properties = GetDefaultLayoutProperties()
            properties:SetPercentageX(0)
            properties:SetPercentageY(0)
            properties:SetPercentageOriginX(1)
            properties:SetPercentageOriginY(1)
            properties:SetPixelX(textBox:GetGlobalX() + textBox:GetWidth() - 20)
            properties:SetPixelY(textBox:GetGlobalY() + textBox:GetHeight() - 39)
        end

        parent:Dialog:Show()
    end

    action SetTextBox(CodeTextBox code)
        textBox = code
        textBox:AddTextChangeListener(me)
        findBehavior:SetTextBox(textBox)
        wholeWordMatchCaseBehavior:SetTextBox(textBox)
        textBox:SetDialog(me)
        replaceBehavior:SetTextBox(textBox)
        replaceAllBehavior:SetTextBox(textBox)
        previous:SetBehavior(previousBehavior)
        next:SetBehavior(nextBehavior)
    end

    action GetTextBox returns CodeTextBox
        return textBox
    end

    action GetFindWord() returns text
        return findField:GetText()
    end

    action GetIsMatchCase returns boolean
        return matchCase:GetToggleState()
    end

    action GetIsWholeWord returns boolean
        return wholeWord:GetToggleState()
    end

    action SetTextChangeListenerOn(boolean state)
        textChangeListenerOn = state
    end

    action GetTextChangeListenerOn returns boolean
        return textChangeListenerOn
    end

    action GetReplaceWord returns text
        return replaceField:GetText()
    end


    //called after replace and replaceAll to update the textValue to include the textBox modifications
    //repopulates the highlighting for any remaining instances that were not replaced by calling FindText (not currently working)
    action UpdateTextBox()
        findButton:Activate()
    end

    //Listens for text change within the findField and text change events in the 
    //current textBox that is being searched. Calls to update the selections if
    //there is a text change event that occurs in either field.
    //Does not listen for textChanges in the textBox until after replaceAll is executed
    action TextChanged(TextChangeEvent event)
        text find = ""
        if event:GetControl():Equals(cast(Object, findField)) 
            find = findField:GetText()
            if event:GetEventType() = event:ADDED
                if find not= ""
                    findButton:Activate()
                end
            elseif event:GetEventType() = event:DELETED
                if (find = "") and (textBox not= undefined)
                    findButton:Activate()
                elseif find not= ""
                    findButton:Activate()
                end
            elseif event:GetEventType() = event:MODIFIED
                if find not= ""
                    findButton:Activate()
                end
            end
        end

        if textBox not= undefined
            if event:GetControl():Equals(cast(Object, textBox))                
                if textChangeListenerOn
                    if event:GetEventType() = event:ADDED
                        UpdateTextBox()
                    elseif event:GetEventType() = event:DELETED
                        UpdateTextBox()
                    elseif event:GetEventType() = event:MODIFIED   
                        UpdateTextBox()
                    end
                end
            end
        end
    end

    action SelectionChanged(SelectionEvent event) 
        if showing
            TabPaneSelection selection = tabPane:GetSelection()
            Tab tab = selection:GetTab()
            if tab not= undefined and tab is EditorTab
                EditorTab t = cast(EditorTab, tab)
                TextBox box = t:GetTextBox()
                if box not= undefined
                    textBox:RemoveFindReplaceStyles()
                    SetTextBox(cast(CodeTextBox, box))
                    UpdateTextBox()
                end
            end
        end
    end

    //Removes any highlights in the textBox, empties the arrays and textFields and resets the selection index.
    action OnHide
        showing = false
        //textBox:RemoveFindReplaceStyles()
        textBox:SetFindWord(findField:GetText())
        textBox:SetReplaceWord(replaceField:GetText())
        textBox:SetIsDialogOpen(false)
    end   
end