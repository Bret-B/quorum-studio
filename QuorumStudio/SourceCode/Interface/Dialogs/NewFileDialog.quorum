package Libraries.Development.Environment.Studio.Interface

use Libraries.Interface.Controls.Dialog
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Color
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Game.Graphics.Label
use Libraries.Interface.Controls.TextField
use Libraries.Interface.Controls.Button
use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.ScrollPane
use Libraries.Development.Environment.Projects.Quorum.QuorumProject
use Libraries.Interface.Controls.ButtonGroup
use Libraries.System.File
use Libraries.System.FileWriter
use Libraries.Interface.Controls.List
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Development.Environment.Studio.Behaviors.DialogCancelBehavior
use Libraries.Game.Graphics.Gradient
use Libraries.Development.Environment.Studio.Behaviors.NewFileOkBehavior
use Libraries.Development.Environment.Projects.ProjectManager
use Libraries.Development.Environment.Studio.Behaviors.OpenBrowseFoldersBehavior




class NewFileDialog is Dialog

    boolean initialized = false

    // The current Quorum project that we're modifying the properties of.
    // Set during the SetProperties(QuorumProject) action.
    QuorumProject project = undefined   
    ProjectManager manager = undefined

    //Variables for the NewFile Dialog
    TextField fileNameField
    TextField folderNameField
    Button folderBrowseButton
    Button okayButton
    Button cancelButton
    
    //Behaviors used by the Browse, Ok, and Cancel Buttons
    DialogCancelBehavior cancelBehavior
    NewFileOkBehavior okayBehavior
    OpenBrowseFoldersBehavior browseBehavior
    
    File file
    Color color 

    on create
        cancelBehavior:SetDialog(me)
        okayBehavior:SetDialog(me)
    end

    action Initialize
        if initialized
            output "WARNING: Re-initializing the NewFileDialog."
        end

        initialized = true
       
        SetName("New File")
        SetPixelWidth(400)
        SetVerticalLayoutMode(parent:Libraries.Interface.Controls.Control:FIT_CONTENTS)    

        LabelBoxView view
        Color color
        view:SetBorderThickness(1)
        view:SetBorderStyle(view:ALL - view:TOP)
        view:Initialize(color:CustomColor(0.9, 0.9, 0.9, 1), color:Black())
        
        Control panel
        FlowLayout newFileLayout
        panel:SetLayout(newFileLayout)
        panel:SetPercentageWidth(1.0)
        panel:SetPixelHeight(200)
        panel:SetBorderColor(color:Black())
        panel:SetBorderThickness(1)
        panel:Add(CreateNewFileControl())
        panel:SetView2D(view)
        Add(panel)          
    end

    action SetProperties(QuorumProject project)
        me:project = project
        //sets the project for the OpenBrowseFoldersBehavior
        browseBehavior:SetProject(me:project)

        //sets the folder name text field to the current QuorumProject in the 
        //NewFileDialog
        text executableName = project:GetExecutableName()
        if executableName not= undefined
            folderNameField:SetText(executableName)
        end        
    end

    action IsInitialized returns boolean
        return initialized
    end

    action CreateNewFileControl() returns Control
        Control createNewFileContainer
        FlowLayout createNewFileLayout
        createNewFileContainer:SetLayout(createNewFileLayout)
        createNewFileContainer:SetBorderThickness(1)
        createNewFileContainer:SetBorderColor(color:Black())
        createNewFileContainer:SetPercentageWidth(1.0)   
        createNewFileContainer:SetVerticalLayoutMode(createNewFileContainer:FIT_CONTENTS)

        FlowLayout fileLayout
        Control fileContainer
        fileContainer:SetLayout(fileLayout)
        fileContainer:SetPercentageWidth(1.0)
        fileContainer:SetVerticalLayoutMode(fileContainer:FIT_CONTENTS)
        fileContainer:SetLeftPadding(10)
        fileContainer:SetTopPadding(6)
        
        Label fileName
        fileName:SetText("File Name: ")
        fileName:SetPercentageWidth(0.3)
        fileContainer:Add(fileName)
        
        fileNameField:SetPercentageWidth(0.7)
        fileContainer:Add(fileNameField)

        createNewFileContainer:Add(fileContainer)

        FlowLayout folderLayout
        Control folderContainer
        folderContainer:SetLayout(folderLayout)
        folderContainer:SetPercentageWidth(1.0)
        folderContainer:SetPixelWidth(-10)
        folderContainer:SetVerticalLayoutMode(folderContainer:FIT_CONTENTS)
        folderContainer:SetLeftPadding(10)
        folderContainer:SetTopPadding(10)

        Label folderLabel
        folderLabel:SetText("Folder: ")
        folderLabel:SetPercentageWidth(0.3)
        folderContainer:Add(folderLabel)
     
        folderNameField:SetPercentageWidth(0.5)
        folderContainer:Add(folderNameField)
       
        folderBrowseButton:SetName("Browse...")
        folderBrowseButton:SetBehavior(browseBehavior)
        folderBrowseButton:SetPercentageWidth(0.15)
        folderBrowseButton:SetLeftPadding(6)
        folderBrowseButton:SetBottomPadding(6)
        //folderBrowseButton:SetBehavior(browseBehavior)
        folderContainer:Add(folderBrowseButton)
        
        createNewFileContainer:Add(folderContainer)

        FlowLayout closingButtonsLayout
        Control closingButtonsContainer
        closingButtonsContainer:SetLayout(closingButtonsLayout)
        closingButtonsContainer:SetPercentageWidth(1.0)
        closingButtonsContainer:SetVerticalLayoutMode(closingButtonsContainer:FIT_CONTENTS)
        closingButtonsContainer:SetTopPadding(20)
        
        Control closingPadding
        closingPadding:SetPercentageWidth(0.5)
        closingPadding:SetPixelHeight(1)
        closingButtonsContainer:Add(closingPadding)

        cancelButton:SetName("Cancel")
        cancelButton:SetHorizontalLayoutMode(cancelButton:parent:Libraries.Interface.Controls.Control:STANDARD)
        cancelButton:SetPercentageWidth(0.2)
        cancelButton:SetLeftPadding(0)
        cancelButton:SetTopPadding(0)
        cancelButton:SetBehavior(cancelBehavior)
        closingButtonsContainer:Add(cancelButton)

        Gradient okayGradient
        Color gradientTop = color:CustomColor(0.85, 0.85, 1, 1)
        Color gradientBottom = color:CustomColor(0.75, 0.75, 0.9, 1)
        okayGradient:Set(gradientBottom, gradientBottom, gradientTop, gradientTop)

        okayButton:SetName("OK")
        okayButton:SetHorizontalLayoutMode(okayButton:parent:Libraries.Interface.Controls.Control:STANDARD)
        okayButton:SetBackgroundColor(okayGradient)
        okayButton:SetPercentageWidth(0.2)
        okayButton:SetLeftPadding(10)
        okayButton:SetTopPadding(0)
        okayButton:SetBehavior(okayBehavior)
        closingButtonsContainer:Add(okayButton)

        createNewFileContainer:Add(closingButtonsContainer)

        return createNewFileContainer
    end
    
    //returns the current QuorumProject that the NewFile is being added to
    action GetProject returns QuorumProject
        return project
    end

    action CreateNewFile()
        project:SetExecutableName(folderNameField:GetText())
//        FileWriter writer
        file:SetWorkingDirectory(project:GetExecutableName())
        file:SetPath(fileNameField:GetText())
        file:Write("")
        file:Close()
//        writer:OpenForWrite(file)
//        writer:Close()

    end
    

end