package Libraries.Development.Environment.Studio.Interface

use Libraries.Interface.Controls.Dialog
use Libraries.Game.Graphics.Label
use Libraries.Game.GameStateManager
use Libraries.Game.Game
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Color
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.ProgressBar
use Libraries.Language.Compile.Library

use Libraries.Language.Compile.CompilerListener
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Compute.Math
use Libraries.Containers.Array
use Libraries.Compute.Random
use Libraries.Interface.Accessibility
use Libraries.Interface.Views.ControlShaderView
use Libraries.Interface.Layouts.ManualLayout

class SplashScreen is Dialog, CompilerListener
    boolean initialized = false
    Icon logo
    Label studio
    Label loading
    ProgressBar progress
    Library library = undefined
    number currentPercent = 0
    GameStateManager manager
    Math math
    Random random
    Array<text> scanLabels
    Array<text> parsingLabels
    Array<text> typeCheckingLabels
    Array<text> semanticsLabels
    Array<text> doneLabels
    boolean playMessage = false
    text notification = ""

    on create
        SetModal(true)
        SetTopBarVisible(false)

        scanLabels:Add("Taking the Upside Down to Prom")
        scanLabels:Add("Defeating Calamity Ganon")
        scanLabels:Add("return 42 = life:CalculateMeaningOf()")

        parsingLabels:Add("Trolling Internet Trolls")
        parsingLabels:Add("Britney is Free!")
        parsingLabels:Add("Generating Free Tacos")
        parsingLabels:Add("Waiting for Zelda: Breath of the Wild 2")

        typeCheckingLabels:Add("Watching Robots do Standup")
        typeCheckingLabels:Add("Lowering human defenses")
        typeCheckingLabels:Add("Starting robot apocalypse")
        typeCheckingLabels:Add("Throwing shade while rendering")

        semanticsLabels:Add("Harvesting Garden Gnomes")
        semanticsLabels:Add("Social distancing from Cthulu")
        semanticsLabels:Add("Bonferroni correcting")

        doneLabels:Add("Ironing Baby Yoda")
        doneLabels:Add("Resurrecting Palpatine for no reason")
        doneLabels:Add("Vaccinating Chewbacca")
    end

    action UpdatePercentageComplete(number percent)
        currentPercent = percent
    end

    action FindRandomPhrase(Array<text> phrases) returns text
        text result = ""
        if phrases not= undefined and not phrases:IsEmpty()
            integer i = random:RandomIntegerBetween(0, phrases:GetSize() - 1)
            result = phrases:Get(i)
        end
        return result
    end

    action UpdateLabel(text label)
        if label = "Started Scanning"
            notification = FindRandomPhrase(scanLabels)
        elseif label = "Parsing"
            notification = FindRandomPhrase(parsingLabels)
        elseif label = "Type Checking"
            notification = FindRandomPhrase(typeCheckingLabels)
        elseif label = "Semantic Analysis"
            notification = FindRandomPhrase(semanticsLabels)
        elseif label = "All done!"
            notification = FindRandomPhrase(doneLabels)
        end
        playMessage = true
    end

    action Initialize
        if initialized
            output "WARNING: Re-initializing the FindDialog."
        end

        initialized = true

        QuorumStudio qs = cast(QuorumStudio, manager:GetGame())
        SetName("Loading")
        SetPercentageWidth(0.40)
        
        logo:Load("Resources/Graphics/Interface/QuorumLighter.png")

        LayoutProperties properties
        Control panel
        FlowLayout panelLayout
        panel:SetLayout(panelLayout)
        panel:SetInterfaceOptionsKey("Dialog")
        panel:SetPercentageWidth(1.0)
//        panel:SetPixelHeight(140)
        panel:SetVerticalLayoutMode(properties:FIT_CONTENTS)
        panel:SetBorderStyle(properties:ALL)
        panel:SetBorderThickness(1)

        // Make a region to contain the logo. It'll use a ManualLayout so we can center the logo within it.
        Control logoRegion
        logoRegion:SetPercentageWidth(0.25)
        logoRegion:SetPercentageHeight(0.8)
        logoRegion:SetVerticalLayoutMode(properties:MAINTAIN_ASPECT_RATIO)
        ManualLayout logoLayout
        logoRegion:SetLayout(logoLayout)
        panel:Add(logoRegion)

        logo:SetPercentageWidth(0.75)
        logo:SetPercentageHeight(0.75)
        logo:SetHorizontalLayoutMode(properties:MAINTAIN_ASPECT_RATIO)
        LayoutProperties logoProperties = logo:GetDefaultLayoutProperties()

        // Position the logo from its center (set origin), and put the origin at the center of the region
        logoProperties:SetPercentageOriginX(0.5)
        logoProperties:SetPercentageOriginY(0.5)
        logo:SetPercentageX(0.5)
        logo:SetPercentageY(0.5)
        logoRegion:Add(logo)

        Control textRegion
        FlowLayout regionLayout
        textRegion:SetLayout(regionLayout)
        textRegion:SetHorizontalLayoutMode(properties:FILL)
        textRegion:SetPercentageHeight(1)
        panel:Add(textRegion)

        studio:SetText(qs:GetDisplayName())
        studio:SetFontSize(20)
        studio:SetHorizontalLayoutMode(properties:STANDARD)
        studio:SetPercentageWidth(1.0)
        studio:SetTopPadding(8)
//        studio:SetTopPadding(85)
//        studio:SetLeftPadding(40)
        textRegion:Add(studio)
        //progress:SetSize(studio:GetWidth(), progress:GetHeight())
        //progress:SetSize(1000, 1000)
        
        loading:SetText("Loading ...")
        loading:SetHorizontalLayoutMode(properties:STANDARD)
        loading:SetPercentageWidth(1.0)
        loading:SetTopPadding(12)
//        loading:SetLeftPadding(270)
//        loading:SetTopPadding(-70)
//        loading:SetX(studio:GetX())
//        loading:SetY(studio:GetY() - studio:GetHeight())
        
        progress:SetPercentageWidth(0.4)
        progress:SetPixelWidth(0)
        progress:SetPercentageHeight(0.15)
        progress:SetTopPadding(5)
        progress:SetPixelHeight(0)
        
        textRegion:Add(loading)
        textRegion:Add(progress)

        ControlShaderView view
        view:Initialize()
        panel:SetView2D(view)

        Add(panel)
        progress:SetFocusable(true)
        
    end

    action OnShow
//        progress:SetWidth(studio:GetWidth())
        //progress:SetHeight(2000)
        progress:Focus()
    end

    action Update(number seconds)
        //logo:Rotate(seconds * 90)
        number value = math:RoundToNearestInteger(currentPercent)
        progress:SetValue(value)
//        loading:SetX(studio:GetX())
//        loading:SetY(studio:GetY() - studio:GetHeight())
//
//        progress:SetX(studio:GetX())
//        progress:SetY(studio:GetY() - studio:GetHeight() - loading:GetHeight())

        if playMessage
            Accessibility access = manager:GetAccessibility()
            if access not= undefined
                access:Notify(manager:GetFocus(), notification)
            end
            loading:SetText(notification)
            playMessage = false
        end
    end

    action GetLibrary returns Library
        return library
    end

    action SetLibrary(Library library)
        me:library = library
        library:AddListener(me)
    end
end
