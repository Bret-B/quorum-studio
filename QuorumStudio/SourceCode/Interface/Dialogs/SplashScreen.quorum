package Libraries.Development.Environment.Studio.Interface

use Libraries.Interface.Controls.Dialog
use Libraries.Game.Graphics.Label
use Libraries.Game.GameStateManager
use Libraries.Game.Game
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Color
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.ProgressBar
use Libraries.Language.Compile.Library

use Libraries.Language.Compile.CompilerListener
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Compute.Math
use Libraries.Containers.Array
use Libraries.Compute.Random
use Libraries.Interface.Accessibility

class SplashScreen is Dialog, CompilerListener
    boolean initialized = false
    Icon logo
    Label studio
    Label loading
    ProgressBar progress
    Library library = undefined
    number currentPercent = 0
    GameStateManager manager
    Math math
    Random random
    Array<text> scanLabels
    Array<text> parsingLabels
    Array<text> typeCheckingLabels
    Array<text> semanticsLabels
    Array<text> doneLabels
    boolean playMessage = false
    text notification = ""

    on create
        SetModal(true)
        SetTopBarVisible(false)

        scanLabels:Add("Taking the Upside Down to Prom")
        scanLabels:Add("Defeating Calamity Ganon")
        scanLabels:Add("Social distancing from Cthulu")

        parsingLabels:Add("Trolling Internet Trolls")
        parsingLabels:Add("Free Britney")
        parsingLabels:Add("Generating Free Tacos")
        parsingLabels:Add("Waiting for Zelda: Breath of the Wild 2")

        typeCheckingLabels:Add("Watching Robots do Standup")
        typeCheckingLabels:Add("Lowering human defenses")
        typeCheckingLabels:Add("Starting robot apocalypse")
        typeCheckingLabels:Add("Contemplating in 3-dimensions")

        semanticsLabels:Add("Harvesting Garden Gnomes")
        semanticsLabels:Add("return 42 = life:CalculateMeaningOf()")
        semanticsLabels:Add("Bonferroni correcting")

        doneLabels:Add("Ironing Baby Yoda")
        doneLabels:Add("Resurrecting Palpatine for no reason")
        doneLabels:Add("Vaccinating Chewbacca")
    end

    action UpdatePercentageComplete(number percent)
        currentPercent = percent
    end

    action FindRandomPhrase(Array<text> phrases) returns text
        text result = ""
        if phrases not= undefined and not phrases:IsEmpty()
            integer i = random:RandomIntegerBetween(0, phrases:GetSize() - 1)
            result = phrases:Get(i)
        end
        return result
    end

    action UpdateLabel(text label)
        if label = "Started Scanning"
            notification = FindRandomPhrase(scanLabels)
        elseif label = "Parsing"
            notification = FindRandomPhrase(parsingLabels)
        elseif label = "Type Checking"
            notification = FindRandomPhrase(typeCheckingLabels)
        elseif label = "Semantic Analysis"
            notification = FindRandomPhrase(semanticsLabels)
        elseif label = "All done!"
            notification = FindRandomPhrase(doneLabels)
        end
        playMessage = true
    end

    action Initialize
        if initialized
            output "WARNING: Re-initializing the FindDialog."
        end

        initialized = true

        QuorumStudio qs = cast(QuorumStudio, manager:GetGame())
        SetName("Loading")
        SetPercentageWidth(0.40)
        SetVerticalLayoutMode(parent:Libraries.Interface.Controls.Control:FIT_CONTENTS)

        LabelBoxView view
        Color color
        view:SetBorderThickness(1)
        view:SetBorderStyle(view:ALL)
        view:Initialize(color:CustomColor(0.9, 0.9, 0.9, 1), color:Black())
        
        logo:Load("Resources/Graphics/Interface/Quorum.png")

        LayoutProperties properties
        Control panel
        FlowLayout findLayout
        logo:SetPercentageHeight(0.75)
        logo:SetPercentageWidth(logo:GetWidth()/ logo:GetHeight())
        logo:SetHorizontalLayoutMode (properties:MAINTAIN_ASPECT_RATIO)
        logo:SetLeftPadding(40)
        logo:SetTopPadding(50)

        studio:SetText(qs:GetDisplayName())
        studio:SetFontSize(20)
        studio:SetTopPadding(85)
        studio:SetLeftPadding(40)
        loading:SetText("Loading ...")
        loading:SetLeftPadding(270)
        loading:SetTopPadding(-70)

        loading:SetX(studio:GetX())
        loading:SetY(studio:GetY() - studio:GetHeight())
        panel:SetLayout(findLayout)
        panel:SetPercentageWidth(1.0)
        panel:SetPixelHeight(140)
        panel:SetBorderColor(color:Black())
        panel:SetBorderThickness(1)
        panel:Add(logo)

        panel:Add(progress)
        panel:Add(studio)
        panel:Add(loading)
        panel:SetView2D(view)
        Add(panel)
        progress:SetFocusable(true)
       
        
    end

    action OnShow
        progress:Focus()
    end

    action Update(number seconds)
        logo:Rotate(seconds * 90)
        number value = math:RoundToNearestInteger(currentPercent)
        progress:SetValue(value)
        loading:SetX(studio:GetX())
        loading:SetY(studio:GetY() - studio:GetHeight())

        if playMessage
            Accessibility access = manager:GetAccessibility()
            if access not= undefined
                access:Notify(manager:GetFocus(), notification)
            end
            loading:SetText(notification)
            playMessage = false
        end
    end

    action GetLibrary returns Library
        return library
    end

    action SetLibrary(Library library)
        me:library = library
        library:AddListener(me)
    end
end
