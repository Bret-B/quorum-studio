package Libraries.Development.Environment.Studio.Interface

use Libraries.Interface.Controls.Dialog
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.ScrollPane
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.Label
use Libraries.Interface.Controls.TextField
use Libraries.Interface.Controls.ButtonGroup
use Libraries.Interface.Controls.List
use Libraries.Interface.Controls.Button
use Libraries.Interface.Controls.Checkbox
use Libraries.Interface.Controls.RadioButton
use Libraries.Development.Environment.Projects.Quorum.QuorumProject
use Libraries.Development.Environment.Projects.ProjectProperty
use Libraries.Containers.Iterator
use Libraries.Containers.Array
use Libraries.Game.Graphics.Gradient
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesOkBehavior
use Libraries.Development.Environment.Studio.Behaviors.DialogCancelBehavior
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesAddJarBehavior
use Libraries.Development.Environment.Studio.Behaviors.RemoveListItemBehavior
use Libraries.Development.Environment.Studio.Behaviors.EmptyListBehavior
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesAddJavaClassFolderBehavior
use Libraries.Interface.Events.MouseWheelListener
use Libraries.Interface.Events.MouseEvent
use Libraries.Development.Environment.Projects.Quorum.ImageSheets
use Libraries.System.File
use Libraries.Development.Environment.Projects.Quorum.ImageSheet
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesAddImagesBehavior
use Libraries.Containers.HashTable
use Libraries.Interface.Selections.ListSelection
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesImageSheetListBehavior
use Libraries.Interface.Controls.ListItem
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesAddImageSheetsBehavior
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesAddSourceBehavior
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesEditImageSheetsBehavior
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesRemoveImageSheetBehavior
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesRemoveImageBehavior
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesAddImageFolderBehavior
use Libraries.Development.Environment.Studio.Behaviors.ProjectPropertiesMobileResourcesBrowseBehavior
use Libraries.Development.Environment.Studio.Behaviors.EmptyTextFieldBehavior
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Selections.TabPaneSelection
use Libraries.Game.GameStateManager
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Development.Environment.Configuration.Options
use Libraries.Development.Environment.Projects.Credentials
use Libraries.Development.Environment.Studio.Mobile.AppleDevicesReader
use Libraries.Development.Environment.Studio.Mobile.AppleDevice
use Libraries.Interface.Views.ControlShaderView
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Forms.Page
use Libraries.Interface.Forms.Grouping
use Libraries.Interface.Forms.Banner
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Interface.Pages.StackedRowPage

class ProjectPropertiesDialog is Dialog, MouseWheelListener

    boolean initialized = false

    // The current Quorum project that we're modifying the properties of.
    // Set during the SetProperties(QuorumProject) action.
    QuorumProject project = undefined
    File sourceLocation = undefined

    // Variables for the Project Information tab.
    TextField programLocationField = undefined
    TextField programNameField = undefined
    RadioButton standardProjectButton = undefined
    RadioButton legoProjectButton = undefined
    RadioButton webBrowserProjectButton = undefined

    List javaClassList = undefined
    List jarList = undefined
    List quorumSourcesList = undefined

    ProjectPropertiesAddJavaClassFolderBehavior addJavaClassBehavior
    RemoveListItemBehavior removeJavaClassBehavior
    EmptyListBehavior clearJavaClassesBehavior

    ProjectPropertiesAddJarBehavior addJarBehavior
    RemoveListItemBehavior removeJarBehavior
    EmptyListBehavior clearJarsBehavior

    ProjectPropertiesAddSourceBehavior addSourceBehavior
    RemoveListItemBehavior removeSourceBehavior
    EmptyListBehavior clearSourcesBehavior
    
    ProjectPropertiesAddImageSheetsBehavior addImageSheetsBehavior
    ProjectPropertiesEditImageSheetsBehavior editImageSheetsBehavior
    ProjectPropertiesRemoveImageSheetBehavior   removeImageSheetBehavior
    ProjectPropertiesImageSheetListBehavior imageSheetListBehavior

    ProjectPropertiesAddImagesBehavior addImagesBehavior
    ProjectPropertiesAddImageFolderBehavior addImageFolderBehavior
    ProjectPropertiesRemoveImageBehavior removeImageBehavior

    ProjectPropertiesMobileResourcesBrowseBehavior mobileResourcesBrowseBehavior
    ProjectPropertiesMobileResourcesBrowseBehavior iosResourcesBrowseBehavior
    EmptyTextFieldBehavior emptyTextFieldBehavior

    // Variables for the Games tab.
    Checkbox enableImageSheets
    Checkbox buildImageSheetsOnCompile
    TextField imageSheetsBuildPathField
    List imageSheetsList
    List imagesList

    Array<List> imagesListArray         //stores each associated imageSheetName with the correct list of images to be displayed in the imagesList
    integer imageSheetCount = -1   

    // Variables for the Mobile tab.
    TextField androidResourcesField = undefined
    TextField androidLibraryField = undefined
    TextField keyStoreField = undefined
    TextField keyStorePasswordField = undefined
    TextField keyAliasField = undefined
    TextField keyPasswordField = undefined
    TextField jdkField = undefined
    TextField iosResourcesField = undefined
    TextField iosBundlingField = undefined
    TextField iosProvisioningField = undefined
    TextField iosSignatureField = undefined
    TabPane tabPane
    List availableDevicesList

    //variables for the Teams tab
    TextField usernameTeams
    TextField passwordTeams
    TextField nameTeams
    TextField emailTeams

    // Behaviors used by the OK and Cancel buttons.
    ProjectPropertiesOkBehavior okBehavior
    DialogCancelBehavior cancelBehavior
    Options options = undefined

    action OnShow
        tabPane:Focus()
        Tab tab = tabPane:GetSelectedTab()
        if not tab:Equals(tabPane:Get(0))
            tab:Activate()
        end
    end

    on create
        addJarBehavior:SetDialog(me)
        addJavaClassBehavior:SetDialog(me)
        addSourceBehavior:SetDialog(me)
        addImagesBehavior:SetDialog(me)
        addImageFolderBehavior:SetDialog(me)
        removeImageBehavior:SetDialog(me)
        addImageSheetsBehavior:SetDialog(me)
        editImageSheetsBehavior:SetDialog(me)
        removeImageSheetBehavior:SetDialog(me)
        imageSheetListBehavior:SetDialog(me)
        mobileResourcesBrowseBehavior:SetDialog(me)
        iosResourcesBrowseBehavior:SetDialog(me)
        iosResourcesBrowseBehavior:SetAndroid(false)
        okBehavior:SetDialog(me)
        cancelBehavior:SetDialog(me)
        SetBehavior(okBehavior)

        GameStateManager manager
        QuorumStudio studio = cast(QuorumStudio, manager:GetGame())
        options = studio:GetOptions()
    end

    action Initialize
        if initialized
            output "WARNING: Re-initializing the ProjectPropertiesDialog."
        end

        initialized = true

        SetName("Properties")
        SetPercentageWidth(0.5)
        GetDefaultLayoutProperties():SetPercentageOriginY(1.0)
        SetPercentageY(0.875)

        LayoutProperties viewProperties = tabPane:GetViewAreaLayoutProperties()
        viewProperties:SetVerticalLayoutMode(viewProperties:FIT_CONTENTS)
        
        FlowLayout tabLayout
        tabPane:SetViewAreaLayout(tabLayout)
        tabPane:SetPercentageWidth(1.0)
        tabPane:SetVerticalLayoutMode(viewProperties:FIT_CONTENTS)

        tabPane:Add(CreateProjectInfoTab())
        tabPane:Add(CreateGamesTab())
        tabPane:Add(CreateAndroidTab())
        tabPane:Add(CreateiOSTab())
        tabPane:Add(CreateTeamsTab())
        Add(tabPane)
    end

    action SetProperties(QuorumProject project)
        imageSheetCount = -1
        me:project = project
        sourceLocation = me:project:GetSourceLocation()

        SetName(project:GetName() + " Properties")

        programLocationField:SetText(project:GetLocation():GetAbsolutePath())
        text executableName = project:GetProperty(project:EXECUTABLE_NAME):GetFirstValue()

        if executableName not= undefined
            programNameField:SetText(executableName)
        end

        text projectType = project:GetProperty(project:PROJECT_TYPE):GetFirstValue()

        if projectType = undefined or projectType = project:JAVA_COMPILE_TYPE
            standardProjectButton:SetToggleState(true)
        elseif projectType = project:JAVASCRIPT_COMPILE_TYPE
            webBrowserProjectButton:SetToggleState(true)
        elseif projectType = project:LEGO_COMPILE_TYPE
            legoProjectButton:SetToggleState(true)
        end

        javaClassList:Empty()

        Iterator<text> javaClasses = project:GetJavaClassDependencies()
        repeat while javaClasses:HasNext()
            javaClassList:Add(javaClasses:Next())
        end

        jarList:Empty()

        ProjectProperty jarProperty = project:GetProperty(project:JAR_DEPENDENCIES)

        if jarProperty not= undefined
            Iterator<text> jarDependencies = jarProperty:GetValueIterator()

            repeat while jarDependencies:HasNext()
                jarList:Add(jarDependencies:Next())
            end
        end

        quorumSourcesList:Empty()

        ProjectProperty quorumDependenciesProperty = project:GetProperty(project:QUORUM_DEPENDENCIES)

        if quorumDependenciesProperty not= undefined
            Iterator<text> quorumDependencies = quorumDependenciesProperty:GetValueIterator()

            repeat while quorumDependencies:HasNext()
                quorumSourcesList:Add(quorumDependencies:Next())
            end
        end

        ProjectProperty enableImageSheetsProperty = project:GetProperty(project:IMAGE_SHEET_ENABLED)
        
        if enableImageSheetsProperty not= undefined and enableImageSheetsProperty:GetFirstValue() = "true"
            enableImageSheets:SetToggleState(true)
        else
            enableImageSheets:SetToggleState(false)
        end

        ProjectProperty buildImageSheetsProperty = project:GetProperty(project:IMAGE_SHEET_REBUILD_ON_COMPILE)

        if buildImageSheetsProperty not= undefined and buildImageSheetsProperty:GetFirstValue() = "true"
            buildImageSheetsOnCompile:SetToggleState(true)
        else
            buildImageSheetsOnCompile:SetToggleState(false)
        end

        ProjectProperty imageSheetsPathProperty = project:GetProperty(project:IMAGE_SHEET_BUILD_PATH)

        if imageSheetsPathProperty not= undefined
            imageSheetsBuildPathField:SetText(imageSheetsPathProperty:GetFirstValue())
        else
            imageSheetsBuildPathField:SetText("resources")
        end

        imageSheetsList:Empty()
        imagesList:Empty()
        imagesListArray:Empty()     
        
        ProjectProperty imageSheetsProperty = project:GetProperty(project:IMAGE_SHEETS)
        if imageSheetsProperty not= undefined
            
            ImageSheets sheets = project:GetImageSheets()
            Array<ImageSheet> imageSheets = sheets:GetSheets(true)
            Iterator<ImageSheet> sheetsIterator = imageSheets:GetIterator()
            output imageSheetCount
            repeat while sheetsIterator:HasNext()
                imageSheetCount = imageSheetCount + 1
                ImageSheet sheet = sheetsIterator:Next()
                imageSheetsList:Add(imageSheetCount, sheet:GetName())
                Iterator<text> sheetIterator = sheet:GetValues()
                List list
                repeat while sheetIterator:HasNext()
                    list:Add(sheetIterator:Next())
                end
                imagesListArray:Add(list)
            end
        end

        ProjectProperty androidResourcesProperty = project:GetProperty(project:ANDROID_ASSETS)
        ProjectProperty androidLibraryProperty = project:GetProperty(project:ANDROID_PATH)
        ProjectProperty androidKeyStoreProperty = project:GetProperty(project:ANDROID_KEYSTORE_PATH)
        ProjectProperty androidKeyStorePasswordProperty = project:GetProperty(project:ANDROID_KEYSTORE_PASSWORD)
        ProjectProperty androidKeyAliasProperty = project:GetProperty(project:ANDROID_KEY_ALIAS)
        ProjectProperty androidKeyPasswordProperty = project:GetProperty(project:ANDROID_KEY_PASSWORD)
        ProjectProperty androidJDKProperty = project:GetProperty(project:ANDROID_ALTERNATE_JDK_PATH)
        ProjectProperty iosVersion = project:GetProperty(project:IOS_VERSION)
        ProjectProperty iosDeviceProperty = project:GetProperty(project:IOS_DEVICE)
        ProjectProperty iosBundleProperty = project:GetProperty(project:IOS_BUNDLE)
        ProjectProperty iosProvisioningProperty = project:GetProperty(project:IOS_PROVISIONING)
        ProjectProperty iosSignatureProperty = project:GetProperty(project:IOS_SIGNATURE)
        ProjectProperty iosResourcesProperty = project:GetProperty(project:IOS_ASSETS)

        if androidResourcesProperty not= undefined and androidResourcesField not= undefined
            androidResourcesField:SetText(androidResourcesProperty:GetFirstValue())
        else
            androidResourcesField:SetText("media")
        end

        if androidLibraryProperty not= undefined and androidLibraryField not= undefined
            androidLibraryField:SetText(project:GetAndroidPath())
        else
            androidLibraryField:Empty()
        end

        androidLibraryField:HideCaret()
        
        if androidKeyStoreProperty not= undefined and keyStoreField not= undefined
            keyStoreField:SetText(project:GetAndroidKeystorePath())
        else
            keyStoreField:Empty()
        end
        keyStoreField:HideCaret()

        if androidKeyStorePasswordProperty not= undefined and keyStorePasswordField not= undefined
            keyStorePasswordField:SetText(project:GetAndroidKeystorePassword())
        else
            keyStorePasswordField:Empty()
        end
        keyStorePasswordField:HideCaret()

        if androidKeyAliasProperty not= undefined and keyAliasField not= undefined
            keyAliasField:SetText(project:GetAndroidKeyAlias())
        else
            keyAliasField:Empty()
        end
        keyAliasField:HideCaret()

        if androidKeyPasswordProperty not= undefined and keyPasswordField not= undefined
            keyPasswordField:SetText(project:GetAndroidKeyPassword())
        else
            keyPasswordField:Empty()
        end
        keyPasswordField:HideCaret()
        
        if androidJDKProperty not= undefined and jdkField not= undefined
            jdkField:SetText(project:GetAndroidAlternateJDKPath())
        else
            jdkField:Empty()
        end
        jdkField:HideCaret()

        if iosDeviceProperty not= undefined
            text value = iosDeviceProperty:GetFirstValue()
            boolean done = false
            i = 0
            repeat while i < availableDevicesList:GetSize() and not done
                text item = availableDevicesList:Get(i)
                if value = item
                    availableDevicesList:Select(i)
                    done = true
                end
                i = i + 1
            end
        end

        if iosBundleProperty not= undefined and iosBundlingField not= undefined
            text value = iosBundleProperty:GetFirstValue()
            iosBundlingField:SetText(value)
        end

        if iosProvisioningProperty not= undefined and iosProvisioningField not= undefined
            text value = iosProvisioningProperty:GetFirstValue()
            iosProvisioningField:SetText(value)
        end

        if iosSignatureProperty not= undefined and iosSignatureField not= undefined
            text value = iosSignatureProperty:GetFirstValue()
            iosSignatureField:SetText(value)
        end

        if iosResourcesProperty not= undefined
            iosResourcesField:SetText(iosResourcesProperty:GetFirstValue())
        else
            iosResourcesField:SetText("media")
        end

        if project:HasGitSupport() 
            Credentials credentials = options:GetPassword(project:GetLocation():GetAbsolutePath())
            if credentials not= undefined
                me:usernameTeams:SetText(credentials:GetUsername())
                me:passwordTeams:SetText(credentials:GetPassword())
                me:nameTeams:SetText(credentials:GetName())
                me:emailTeams:SetText(credentials:GetEmail())
            end
        end
    end
    
    /* Called by the ProjectPropertiesImageSheetListBehavior, when the image sheet 
    selection changes this action sets the correct images list, dependent on the 
    imageSheet selected in the imageSheets list*/
    action SetCurrentImageList(integer sheetIndex)
        imagesList:Empty()
        List list = imagesListArray:Get(sheetIndex)

        if list not= undefined
            integer i = 0
            repeat while i < list:GetSize()
                output list:Get(i)
                imagesList:Add(list:Get(i))
                i = i + 1
            end
        end

        imagesList:Select(0)
        imagesList:Activate()
    end

    /*returns the list of images associated with the given index */
    action GetCurrentImageList(integer index) returns List
        return imagesListArray:Get(index)
    end

    action GetCurrentImageSheet returns ListItem
        ListSelection listSelection = imageSheetsList:GetSelection()
        return listSelection:GetListItem()
    end

    action GetCurrentImageSheetIndex returns integer
        ListSelection listSelection = imageSheetsList:GetSelection()
        return listSelection:GetIndex()
    end

    action AddToImagesListArray(integer sheetIndex, List list)
        imagesListArray:RemoveAt(sheetIndex)        
        imagesListArray:Add(sheetIndex, list)
    end

    /* called by the AddImageSheetsOkBehavior, adds the new image sheet name
    to the list of image sheets for the project */
    action AddNewImageSheet(text imageSheetName)
        imageSheetCount = imageSheetCount + 1
        imageSheetsList:Add(imageSheetCount, imageSheetName)
        List list
        list:Empty()
        imagesListArray:Add(imageSheetCount, list)
    end

    action EditImageSheetName(text imageSheetName, integer index)
        imageSheetsList:Add(index, imageSheetName)
    end

    action RemoveImageSheet(integer index)
        imageSheetCount = imageSheetCount - 1
        imageSheetsList:Remove(index)
        imagesListArray:RemoveAt(index)
    end
   
    action RemoveImage(integer imageIndex, integer sheetIndex)
        ListItem listItem = cast(ListItem, imagesList:GetItem(imageIndex))
        imagesList:Remove(listItem)
        List list 
        integer i = 0
        repeat while i < imagesList:GetSize()
            list:Add(imagesList:GetListItem(i))
            i = i + 1
        end
        imagesListArray:RemoveAt(sheetIndex)
        imagesListArray:Add(sheetIndex, list)
        SetCurrentImageList(sheetIndex)
    end

    action ToggleEnableImageSheets()
        if not enableImageSheets:GetToggleState()
            enableImageSheets:SetToggleState(true)
        end
    end

    /*
    This action returns the current project that's associated with this dialog.
    Because QuorumStudio reuses the same properties dialog for multiple projects
    when possible, this usually refers to the last project whose properties were
    opened. This value is set from the SetProperties action.
    */
    action GetProject returns QuorumProject
        return project
    end


    action SetAndroidResourcesLocation(text location)
        androidResourcesField:SetText(location)
    end

    action SetIOSResourcesLocation(text location)
        iosResourcesField:SetText(location)
    end

    /*
    This action saves all of the changes made in this dialog within the project,
    and asks the project to rewrite its property file.
    */
    action SavePropertyChanges
        project:SetExecutableName(programNameField:GetText())
        
        if standardProjectButton:GetToggleState() = true
            project:SetProjectCompileType(project:JAVA_COMPILE_TYPE)
        elseif webBrowserProjectButton:GetToggleState() = true
            project:SetProjectCompileType(project:JAVASCRIPT_COMPILE_TYPE)
        elseif legoProjectButton:GetToggleState() = true
            project:SetProjectCompileType(project:LEGO_COMPILE_TYPE)
        end

        project:SetJavaClassDependencies(ListToArray(javaClassList))
        project:SetJarDependencies(ListToArray(jarList))
        project:SetQuorumDependencies(ListToArray(quorumSourcesList))

        if not imageSheetsList:IsEmpty()
            enableImageSheets:SetToggleState(true)
            buildImageSheetsOnCompile:SetToggleState(true)
        end

        project:EnableImageSheets(enableImageSheets:GetToggleState())
        project:EnableRecompileImageSheetsOnBuild(buildImageSheetsOnCompile:GetToggleState())
        text imageSheetBuildPath = imageSheetsBuildPathField:GetText():Trim()
        if enableImageSheets:GetToggleState()
            if imageSheetBuildPath:StartsWith("/")
                project:SetImageSheetBuildPath(imageSheetBuildPath)
            else
                project:SetImageSheetBuildPath("/" + imageSheetBuildPath)
            end
        end
        if(enableImageSheets:GetToggleState())
            ImageSheets imageSheets
            Array<text> imageSheetName = ListToArray(imageSheetsList)
            integer i = 0
            repeat while i < imageSheetName:GetSize()
                Array<text> images = ListToArray(imagesListArray:Get(i))
                ImageSheet imageSheet
                imageSheet:SetName(imageSheetName:Get(i))
                imageSheet:SetValues(images)
                imageSheets:Add(imageSheet)
                i = i + 1
            end

            project:SetImageSheets(imageSheets)
            ProjectProperty projectImageSheets = project:GenerateImageSheetProperty()
            projectImageSheets:SetKey(project:IMAGE_SHEETS)
            project:AddProperty(projectImageSheets)
        end
        
        boolean mobileFlag = false

        if not androidLibraryField:GetText():Equals("")
            project:SetAndroidPath(androidLibraryField:GetText())
            mobileFlag = true
        end
        if not keyStoreField:GetText():Equals("")
            project:SetAndroidKeystorePath(keyStoreField:GetText())
            mobileFlag = true
        end
        if not keyStorePasswordField:GetText():Equals("")
            project:SetAndroidKeystorePassword(keyStorePasswordField:GetText())
            mobileFlag = true
        end
        if not keyAliasField:GetText():Equals("")
            project:SetAndroidKeyAlias(keyAliasField:GetText())
            mobileFlag = true
        end
        if not keyPasswordField:GetText():Equals("")
            project:SetAndroidKeyPassword(keyPasswordField:GetText())
            mobileFlag = true
        end
        if not jdkField:GetText():Equals("")
            project:SetAndroidAlternateJDKPath(jdkField:GetText())
            mobileFlag = true
        end

        text androidAssetsPath = androidResourcesField:GetText():Trim()
        if androidAssetsPath:StartsWith("/")
            project:SetAndroidAssetsPath(androidAssetsPath)
        else
            project:SetAndroidAssetsPath("/" + androidAssetsPath)
        end
    
        //Save IOS Properties
        ListSelection selection = availableDevicesList:GetSelection()
        if selection not= undefined
            text name = selection:GetDisplayName()
            integer index = selection:GetIndex()
            if index >= 0
                AppleDevicesReader reader
                Array<AppleDevice> devices = reader:GetAvailableDevices()
                AppleDevice device = devices:Get(index)
                //tell the project to try and load a particular device.
                project:SetIOSPhysicalDevice(device:IsDevice())
                project:SetIOSDevice(name)
            end
        end

        text bundle = iosBundlingField:GetText()
        project:SetIOSBundle(bundle)

        text provisioning = iosProvisioningField:GetText()
        project:SetIOSProvisioning(provisioning)

        text signature = iosSignatureField:GetText()
        project:SetIOSSignature(signature)

        text iosAssetsPath = iosResourcesField:GetText():Trim()
        if iosAssetsPath:StartsWith("/")
            project:SetIOSAssetsPath(iosAssetsPath)
        else
            project:SetIOSAssetsPath("/" + iosAssetsPath)
        end

        project:Save()

        if project:HasGitSupport() 
            text user = me:usernameTeams:GetText()
            text pass = me:passwordTeams:GetText()
            text name = me:nameTeams:GetText()
            text email = me:emailTeams:GetText()
            Credentials credentials
            credentials:SetUsername(user)
            credentials:SetPassword(pass)
            credentials:SetName(name)
            credentials:SetEmail(email)
            options:AddPassword(project:GetLocation():GetAbsolutePath(), credentials)
        end
    end

    action IsInitialized returns boolean
        return initialized
    end

    private action CreateProjectInfoTab returns Tab
        Tab projectInfoTab
        projectInfoTab:SetFocusTarget(programLocationField)
        projectInfoTab:SetName("Project Information")
        projectInfoTab:DisplayCloseButton(false)

        StackedRowPage page 
        page:SetPixelHeight(500) //for testing, remove after fixes
        Banner banner = page:AddBanner("Projects", "General Project Properties for locations and dependencies")
        banner:PositionFromTop()
        
        page:AddLabel("Project Location")
        Grouping resourceGroup = page:AddGrouping("Location")
        programLocationField = resourceGroup:AddTextField("Location")
        
        page:AddLabel("Project Name")
        programNameField = page:AddTextField("Name")
        
        //add radio buttons
        page:AddLabel("Project Type")
        ButtonGroup projectRadios
        projectRadios:SetName("Project Type")
        Grouping projectTypeGroup = page:AddGrouping("Project Type")
        standardProjectButton = projectTypeGroup:AddRadioButton("Standard")
        webBrowserProjectButton = projectTypeGroup:AddRadioButton("Browser")
        legoProjectButton = projectTypeGroup:AddRadioButton("LEGO")
        
        //Now group them
        projectRadios:Add(standardProjectButton)
        projectRadios:Add(webBrowserProjectButton)
        projectRadios:Add(legoProjectButton)
        
        //Create all of the plugins lists and buttons for controlling them
        page:AddLabel("Plugin Folders")
        javaClassList = page:AddList("Plugins")
        javaClassList:Add("Option 1")
        javaClassList:Add("Option 2")
        
        Grouping pluginGroup = page:AddGrouping("Plugins")
        Button addPlugin = pluginGroup:AddButton("Add")
        Button removePlugin = pluginGroup:AddButton("Remove")
        Button clearPlugin = pluginGroup:AddButton("Clear")

        //set the behaviors on the plugin buttons
        addPlugin:SetBehavior(addJavaClassBehavior)
        removePlugin:SetBehavior(removeJavaClassBehavior)
        clearPlugin:SetBehavior(clearJavaClassesBehavior)

        addJavaClassBehavior:SetList(javaClassList)
        removeJavaClassBehavior:SetList(javaClassList)
        clearJavaClassesBehavior:SetList(javaClassList)
        
        //Create the list for all of the jar files
        page:AddLabel("Jar Folders")
        jarList = page:AddList("Jars")
        jarList:Add("Option 1")
        jarList:Add("Option 2")
        
        Grouping jarGroup = page:AddGrouping("Jars")
        Button addJar = jarGroup:AddButton("Add")
        Button removeJar = jarGroup:AddButton("Remove")
        Button clearJars = jarGroup:AddButton("Clear")

        //set the behaviors on the Jar buttons
        addJarBehavior:SetList(jarList)
        removeJarBehavior:SetList(jarList)
        clearJarsBehavior:SetList(jarList)

        addJar:SetBehavior(addJarBehavior)
        removeJar:SetBehavior(removeJarBehavior)
        clearJars:SetBehavior(clearJarsBehavior)

        page:AddLabel("Source Folders")
        quorumSourcesList = page:AddList("Sources")
        quorumSourcesList:Add("Option 1")
        quorumSourcesList:Add("Option 2")
        
        Grouping sourceGroup = page:AddGrouping("Sources")
        Button addSource = sourceGroup:AddButton("Add")
        Button removeSource = sourceGroup:AddButton("Remove")
        Button clearSources = sourceGroup:AddButton("Clear")
        
        //set the behaviors on the Source buttons
        addSourceBehavior:SetList(quorumSourcesList)
        removeSourceBehavior:SetList(quorumSourcesList)
        clearSourcesBehavior:SetList(quorumSourcesList)

        addSource:SetBehavior(addSourceBehavior)
        removeSource:SetBehavior(removeSourceBehavior)
        clearSources:SetBehavior(clearSourcesBehavior)
        
        Grouping buttonGroup = page:AddGrouping("Confirm")
        Button cancelButton = buttonGroup:AddButton("Cancel")
        Button okayButton = buttonGroup:AddButton("Ok")
        buttonGroup:PositionFromBottom() 

        cancelButton:SetBehavior(cancelBehavior)
        okayButton:SetBehavior(okBehavior)

        projectInfoTab:SetRelatedItem(page)
        return projectInfoTab
    end

    private action CreateGamesTab returns Tab
        Tab tab
        tab:SetFocusTarget(enableImageSheets)
        tab:SetName("Games")
        tab:DisplayCloseButton(false)

        StackedRowPage page 
        page:SetPixelHeight(500) //for testing, remove after fixes
        Banner banner = page:AddBanner("Image Sheets", "Properties for 2D Image Sheets")
        banner:PositionFromTop()
        
        page:AddLabel("Image Sheet Properties")
        enableImageSheets = page:AddCheckbox("Enable Image Sheets")
        buildImageSheetsOnCompile = page:AddCheckbox("Rebuild Image Sheets every compile")
        
        page:AddLabel("Build Path")
        imageSheetsBuildPathField = page:AddTextField("Build Path Field")

        page:AddLabel("Image Sheets")
        imageSheetsList = page:AddList("Image SheetList")
        imageSheetsList:SetItemActivationBehavior(imageSheetListBehavior)

        Grouping imageSheetGroup = page:AddGrouping("Image Sheet Buttons")
        Button addSheet = imageSheetGroup:AddButton("Add")
        Button editSheet = imageSheetGroup:AddButton("Remove")
        Button removeSheet = imageSheetGroup:AddButton("Clear")

        addImageSheetsBehavior:SetImageSheetsList(imageSheetsList)
        editImageSheetsBehavior:SetImageSheetsList(imageSheetsList)
        removeImageSheetBehavior:SetImageSheetsList(imageSheetsList)

        addSheet:SetBehavior(addImageSheetsBehavior)
        editSheet:SetBehavior(editImageSheetsBehavior)
        removeSheet:SetBehavior(removeImageSheetBehavior)
        
        page:AddLabel("Images")
        imagesList = page:AddList("Image List")
        imagesList:Add("Option 1")
        imagesList:Add("Option 2")
        
        Grouping imageGroup = page:AddGrouping("Image Buttons")
        Button addImage = imageGroup:AddButton("Add")
        Button addImageFolder = imageGroup:AddButton("Add Folder")
        Button removeImage = imageGroup:AddButton("Clear")
        
        addImage:SetBehavior(addImagesBehavior)
        addImageFolder:SetBehavior(addImageFolderBehavior)
        removeImage:SetBehavior(removeImageBehavior)

        addImagesBehavior:SetImageSheetsList(imageSheetsList)
        addImageFolderBehavior:SetImageSheetsList(imageSheetsList)
        removeImageBehavior:SetImageSheetsList(imageSheetsList)
        removeImageBehavior:SetImagesList(imagesList)
        
        Grouping buttonGroup = page:AddGrouping("Confirm")
        Button cancelButton = buttonGroup:AddButton("Cancel")
        Button okButton = buttonGroup:AddButton("Ok")
        buttonGroup:PositionFromBottom() 

        cancelButton:SetBehavior(okBehavior)
        okButton:SetBehavior(okBehavior)

        tab:SetRelatedItem(page)
        return tab
    end

    private action CreateiOSTab returns Tab
        Tab tab
        tab:SetName("Apple")
        tab:DisplayCloseButton(false)

        StackedRowPage page 
        page:SetPixelHeight(500) //for testing, remove after fixes
        Banner banner = page:AddBanner("Apple Device Settings", "Pick a simulator or enter security credentials for physical devices")
        banner:PositionFromTop()
        
        page:AddLabel("Resource Folder")
        Grouping resourceGroup = page:AddGrouping("Resources")
        iosResourcesField = resourceGroup:AddTextField("Apple Resource Folder")
        Button browse = resourceGroup:AddButton("Browse ...")
        browse:SetBehavior(iosResourcesBrowseBehavior)
        tab:SetFocusTarget(iosResourcesField)
        
        page:AddLabel("Bundle Identifier")
        iosBundlingField = page:AddTextField("Bundling")

        page:AddLabel("Signing Identity")
        iosSignatureField = page:AddTextField("Signing")
        
        page:AddLabel("Provisioning profile")
        iosProvisioningField = page:AddTextField("Provisioning")
        
        
        page:AddLabel("Device List")
        availableDevicesList = page:AddList("Device List")

        AppleDevicesReader reader
        Array<AppleDevice> devices = reader:GetAvailableDevices()
        i = 0
        repeat while i < devices:GetSize()
            AppleDevice device = devices:Get(i)
            availableDevicesList:Add(device:GetName())
            i = i + 1
        end
        
        Grouping buttonGroup = page:AddGrouping("Confirm")
        Button cancel = buttonGroup:AddButton("Cancel")
        Button ok = buttonGroup:AddButton("Ok")
        buttonGroup:PositionFromBottom() 
        tab:SetRelatedItem(page)

        ok:SetBehavior(okBehavior)
        cancel:SetBehavior(cancelBehavior)
        return tab
    end

    private action CreateAndroidTab returns Tab
        Tab tab
        tab:SetFocusTarget(androidResourcesField)
        tab:SetName("Android")
        tab:DisplayCloseButton(false)

        StackedRowPage page 
        page:SetPixelHeight(500) //for testing, remove after fixes
        Banner banner = page:AddBanner("Android Device Settings", "Enter credentials for physical devices")
        banner:PositionFromTop()
        
        page:AddLabel("Resource Folder")
        Grouping resourceGroup = page:AddGrouping("Resources")
        androidResourcesField = resourceGroup:AddTextField("Android Resource Folder")
        Button browse = resourceGroup:AddButton("Browse ...")
        browse:SetBehavior(mobileResourcesBrowseBehavior)
        tab:SetFocusTarget(androidResourcesField)
        
        page:AddLabel("Library Path")
        androidLibraryField = page:AddTextField("Android Library Path")

        page:AddLabel("Key Store Path")
        keyStoreField = page:AddTextField("Keystore Path")
        
        page:AddLabel("Key Store password")
        keyStorePasswordField = page:AddTextField("Key Store password")
        
        page:AddLabel("Key Alias")
        keyAliasField = page:AddTextField("Key Alias")

        page:AddLabel("Key password")
        keyPasswordField = page:AddTextField("Key password")

        page:AddLabel("Path to Alternative Java Development Kit")
        jdkField = page:AddTextField("Path to Alternate Java Development Kit")

        keyStorePasswordField:SetPassword(true)
        keyPasswordField:SetPassword(true)

        Grouping buttonGroup = page:AddGrouping("Confirm")
        Button cancel = buttonGroup:AddButton("Cancel")
        Button ok = buttonGroup:AddButton("Ok")
        buttonGroup:PositionFromBottom() 
        tab:SetRelatedItem(page)

        ok:SetBehavior(okBehavior)
        cancel:SetBehavior(cancelBehavior)
        return tab
    end

    private action CreateTeamsTab returns Tab
        Tab tab
        tab:SetFocusTarget(usernameTeams)
        tab:SetName("Teams")
        tab:DisplayCloseButton(false)

        StackedRowPage page 
        page:SetPixelHeight(500) //for testing, remove after fixes
        Banner banner = page:AddBanner("Version Control Settings", 
            "Enter credentials for Github or Bitbucket. Passwords should be credentials keys, not 
            account passwords, for these services")
        banner:PositionFromTop()
        
        page:AddLabel("Username")
        usernameTeams = page:AddTextField("Username")

        page:AddLabel("Password")
        passwordTeams = page:AddTextField("Password")
        passwordTeams:SetPassword(true)

        page:AddLabel("Name")
        nameTeams = page:AddTextField("Name")
        
        page:AddLabel("Email")
        emailTeams = page:AddTextField("Email")

        Grouping buttonGroup = page:AddGrouping("Confirm")
        Button cancel = buttonGroup:AddButton("Cancel")
        Button ok = buttonGroup:AddButton("Ok")
        buttonGroup:PositionFromBottom() 
        tab:SetRelatedItem(page)

        ok:SetBehavior(okBehavior)
        cancel:SetBehavior(cancelBehavior)
        return tab
    end

    private action ListToArray(List list) returns Array<text>
        Array<text> array

        integer counter = 0
        repeat while counter < list:GetSize()
            array:Add(list:Get(counter))
            counter = counter + 1
        end

        return array
    end

    action OnHide()
        imageSheetCount = -1
        imagesListArray:Empty()
    end
end