package Libraries.Development.Environment.Studio.Interface

use Libraries.Interface.Controls.Dialog
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Color
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Game.Graphics.Label
use Libraries.Interface.Controls.Button
use Libraries.Development.Environment.Studio.Behaviors.SaveChangesBehavior
use Libraries.Development.Environment.Studio.Behaviors.DiscardChangesBehavior
use Libraries.Development.Environment.Studio.Behaviors.CancelExitBehavior
use Libraries.Game.GameStateManager
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Interface.Views.ControlShaderView
use Libraries.Interface.Layouts.LayoutProperties

class SaveBeforeClosingDialog is Dialog
    GameStateManager manager
    QuorumStudio studio = cast(QuorumStudio, manager:GetGame())

    boolean initialized = false

    Button saveButton
    Button discardButton
    Button cancelButton

    SaveChangesBehavior saveBehavior
    DiscardChangesBehavior discardBehavior
    CancelExitBehavior cancelBehavior
    
    boolean singleItemWording = false

    on create
        saveBehavior:SetDialog(me)
        discardBehavior:SetDialog(me)
        cancelBehavior:SetDialog(me)
        SetBehavior(saveBehavior)
    end

    action OnShow()
        saveButton:Focus()
    end

    action Initialize
        initialized = true

        SetName("Save Before Closing")
        SetPixelWidth(450)
        SetVerticalLayoutMode(parent:Libraries.Interface.Controls.Control:FIT_CONTENTS)    

        ControlShaderView view
        view:Initialize()
        
        Control panel
        LayoutProperties properties = GetDefaultLayoutProperties()
        FlowLayout newFileLayout
        panel:SetLayout(newFileLayout)
        panel:SetPercentageWidth(1.0)
        panel:SetPixelHeight(110)
        panel:SetInterfaceOptionsKey("Dialog")
        panel:SetBorderThickness(1)
        panel:Add(CreateSaveChangesControl())
        panel:SetView2D(view)
        Add(panel)
    end

    private action CreateSaveChangesControl returns Control
        Color color
        Control control
        FlowLayout layout
        control:SetLayout(layout)
        control:SetBorderThickness(1)
        control:SetBorderColor(color:Black())
        control:SetPercentageWidth(1.0)
        control:SetVerticalLayoutMode(control:FIT_CONTENTS)
        
        Label warningLabel
        warningLabel:SetText("You have unsaved changes , would you like to save before closing?")
        warningLabel:SetTopMargin(20)
        warningLabel:SetLeftMargin(10)
        warningLabel:SetRightMargin(10)
        warningLabel:SetPercentageWidth(1.0)
        control:Add(warningLabel)

        Control buttonContainer
        FlowLayout buttonLayout
        buttonContainer:SetLayout(buttonLayout)
        buttonContainer:SetPercentageWidth(1.0)
        buttonContainer:SetVerticalLayoutMode(buttonContainer:FIT_CONTENTS)
        buttonContainer:SetTopMargin(50)
        buttonContainer:SetBottomMargin(20)

        Control buttonMargin
        buttonMargin:SetPercentageWidth(0.2)
        buttonMargin:SetPixelHeight(1)
        buttonContainer:Add(buttonMargin)

        if singleItemWording
            saveButton:SetName("Save")
        else
                saveButton:SetName("Save All")
end
        saveButton:SetBehavior(saveBehavior) //sets the selection to save all integer value
        saveButton:SetNextFocus(discardButton)
        saveButton:SetPreviousFocus(cancelButton)
        saveButton:SetRightMargin(50)
        buttonContainer:Add(saveButton) 

        if singleItemWording
            discardButton:SetName("Don't Save")
        else
            discardButton:SetName("Discard All")
        end
        discardButton:SetBehavior(discardBehavior)   //sets the selection to discard changes integer value
        discardButton:SetNextFocus(cancelButton)
        discardButton:SetPreviousFocus(saveButton)
        discardButton:SetRightMargin(50)
        buttonContainer:Add(discardButton)

        cancelButton:SetName("Cancel")
        cancelButton:SetBehavior(cancelBehavior) //sets the selection to cancel exit integer value
        cancelButton:SetNextFocus(saveButton)
        cancelButton:SetPreviousFocus(cancelButton)
        buttonContainer:Add(cancelButton)

        buttonContainer:Add(buttonMargin)
        control:Add(buttonContainer)
        
        return control
    end

    action OnHide()
        studio:ResetExitFlag()   
    end

    action GetSaveBehavior returns SaveChangesBehavior
        return saveBehavior
    end

    action SetSaveBehavior(SaveChangesBehavior saveBehavior)
        me:saveBehavior = saveBehavior
        saveBehavior:SetDialog(me)
        saveButton:SetBehavior(saveBehavior)
    end

    action GetDiscardBehavior returns DiscardChangesBehavior
        return discardBehavior
    end

    action SetDiscardBehavior(DiscardChangesBehavior discardBehavior)
        me:discardBehavior = discardBehavior
        discardBehavior:SetDialog(me)
        discardButton:SetBehavior(discardBehavior)
    end
    
    action SetSingleItemWording(boolean singleItemWording)
        me:singleItemWording = singleItemWording
    end

end