package Libraries.Development.Environment.Studio.Interface.VisualEditor

use Libraries.Interface.Selections.Selection
use Libraries.Interface.Item

class VisualEditorSelection is Selection
    
    InteractableItem selectedItem = undefined
    EditorLayer selectedLayer = undefined

    boolean itemChanged = false
    boolean layerChanged = false

    action Initialize(VisualSceneController controller)
        Initialize(controller, controller:GetName())
    end

    action SetSelection(InteractableItem item)
        if selectedItem not= undefined
            selectedItem:LostSelection()
        end

        selectedItem = item
        itemChanged = true
        UpdateDisplayName()

        if selectedItem not= undefined
            selectedItem:GainedSelection()
        end
    end

    action SetSelection(EditorLayer layer)
        if selectedLayer not= undefined
            selectedLayer:LostSelection()
        end

        selectedLayer = layer
        layerChanged = true
        UpdateDisplayName()

        if selectedLayer not= undefined
            selectedLayer:GainedSelection()
        end
    end

    action SetSelection(EditorLayer layer, InteractableItem item)
        if layer = selectedLayer
            if item not= selectedItem
                SetSelection(item)
            end
            return now
        elseif item = selectedItem
            SetSelection(layer)
        end

        if selectedItem not= undefined
            selectedItem:LostSelection()
        end

        if selectedLayer not= undefined
            selectedLayer:LostSelection()
        end

        selectedLayer = layer
        selectedItem = item
        layerChanged = true
        itemChanged = true
        UpdateDisplayName()

        if selectedLayer not= undefined
            selectedLayer:GainedSelection()
        end

        if selectedItem not= undefined
            selectedItem:GainedSelection()
        end
    end

    private action UpdateDisplayName
        if selectedLayer = undefined or selectedItem = undefined
            VisualSceneController scene = GetSceneController()
            if scene not= undefined
                SetDisplayName(scene:GetName())
            else
                SetDisplayName("")
            end
        else
            SetDisplayName(selectedItem:GetName() + " - " + selectedLayer:GetName())
        end

        itemChanged = false
        layerChanged = false

        GetSceneController():UpdateCursorVisibility()
    end

    action GetSelectedItem returns InteractableItem
        return selectedItem
    end

    action GetSelectedLayer returns EditorLayer
        return selectedLayer
    end

    /*
    Returns true only when a SelectionEvent is being processed where
    the item changed. After the event has been processed, this returns
    false.
    */
    action ItemChanged returns boolean
        return itemChanged
    end

    /*
    Returns true only when a SelectionEvent is being processed where
    the layer changed. After the event has been processed, this returns
    false.
    */
    action LayerChanged returns boolean
        return layerChanged
    end

    action IsItemSelected returns boolean
        return selectedItem not= undefined
    end

    action IsLayerSelected returns boolean
        return selectedLayer not= undefined
    end

    action IsEmpty returns boolean
        return selectedLayer = undefined
    end

    action Empty
        SetSelection(undefined, undefined)
    end

    action GetSceneController returns VisualSceneController
        Item item = GetItem()
        if item is VisualSceneController
            return cast(VisualSceneController, item)
        else
            return undefined
        end
    end

end