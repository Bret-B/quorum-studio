package Libraries.Development.Environment.Studio.Interface.VisualEditor

use Libraries.Game.Graphics.Model
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.Material
use Libraries.Game.Graphics.ColorAttribute
use Libraries.Compute.Matrix4

class NewAudio3DPreview is NewItemPreview3D, EditorAudio3D

    action Load(VisualSceneController controller, Audio3DBlueprint schematic)
        SetController(controller)
        SetItemBlueprint(schematic)

        SetName(schematic:GetName() + controller:GetNextNamePostfix(schematic:GetName()))
        SetDescription(schematic:GetDescription())

        LoadBox(0.5, 0.5, 0.5, schematic:GetModelTexture())

        Color color
        color:SetColor(1, 1, 1, 0.75)
    
        // We need to add a diffuse color attribute to the model so that the textured box also supports color.
        SetColor(color)

        source = schematic:GetSource()
        maxVolumeDistance = schematic:GetMaxVolumeDistance()
        rolloffRate = schematic:GetRolloffRate()
        volume = schematic:GetVolume()
        pitch = schematic:GetPitch()
        playOnLoad = schematic:GetPlayOnLoad()
        looping = schematic:GetLooping()
        doppler = schematic:GetDoppler()

        EditorCursor cursor = controller:GetCursor()
        if cursor is EditorCursor3D
            EditorCursor3D cursor3D = cast(EditorCursor3D, cursor)
            SetPosition(cursor3D:GetX(), cursor3D:GetY(), cursor:GetZ())
        end
    end

    action ConvertToModel returns Model
        EditorAudio3D audio
        audio:LoadPlaceholderBox()

        audio:SetName(GetName())
        audio:SetDescription(GetDescription())

        audio:SetSource(GetSource())
        audio:SetMaximumVolumeDistance(GetMaximumVolumeDistance())
        audio:SetRolloffRate(GetRolloffRate())
        audio:SetVolume(GetVolume())
        audio:SetPitch(GetPitch())
        audio:SetPlayOnLoad(GetPlayOnLoad())
        audio:SetLooping(GetLooping())
        audio:SetDoppler(GetDoppler())

        audio:SetPosition(GetPosition())

        return audio
    end

end