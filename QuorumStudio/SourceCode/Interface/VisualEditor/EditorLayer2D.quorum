package Libraries.Development.Environment.Studio.Interface.VisualEditor

use Libraries.Game.Layer2D
use Libraries.Interface.Events.ResizeEvent
use Libraries.Game.Graphics.Camera
use Libraries.Interface.Events.MouseEvent
use Libraries.Interface.Item

class EditorLayer2D is Layer2D, EditorLayer

    /*
    The camera preview describes the basic position and dimensions of the camera in the scene.
    It does NOT represent the editor camera! This represents where the camera will be if
    you loaded the scene into a game and ran it outside of the editor.
    */
    CameraPreview2D preview

    Grid2D grid
    boolean gridSnapEnabled = true

    on create
        VisualEditorResources resources
        preview:SetSize(resources:CAMERA_PREVIEW_2D_DEFAULT_WIDTH, resources:CAMERA_PREVIEW_2D_DEFAULT_HEIGHT)
        preview:SetPosition(preview:GetWidth() / 2.0, preview:GetHeight() / 2.0)
        Add(preview)
        preview:SetPosition(0, 0)

        grid:SetGridSize(GetCamera():GetWidth() + grid:GetGridUnitSize(), GetCamera():GetHeight() + grid:GetGridUnitSize())
        Add(grid)
        UpdateGridPosition()

        ShowEditorComponents(false)
    end

    action ProcessMouseEvent(MouseEvent event) returns Item
        VisualSceneController controller = GetSceneController()
        if controller = undefined or (not controller:GetSelectedLayer():Equals(me))
            return undefined
        end

        return parent:Layer2D:ProcessMouseEvent(event)
    end

    action GetCameraPreview returns CameraPreview2D
        return preview
    end

    action SetCameraPreviewX(number x)
        preview:SetX(x)
    end

    action SetCameraPreviewY(number y)
        preview:SetY(y)
    end

    action SetCameraPreviewPosition(number x, number y)
        preview:SetPosition(x, y)
    end

    action SetCameraPreviewWidth(number width)
        preview:SetWidth(width)
    end

    action SetCameraPreviewHeight(number height)
        preview:SetHeight(height)
    end

    action SetCameraPreviewSize(number width, number height)
        preview:SetSize(width, height)
    end

    action GetCameraPreviewX returns number
        return preview:GetX()
    end

    action GetCameraPreviewY returns number
        return preview:GetY()
    end

    action GetCameraPreviewWidth returns number
        return preview:GetWidth()
    end

    action GetCameraPreviewHeight returns number
        return preview:GetHeight()
    end

    action Resize(ResizeEvent event)
        // Don't respond to resizing here -- allow the VisualSceneController to handle it for us.
    end

    action SetGridSnapping(boolean enabled)
        if enabled
            grid:Show()
        else
            grid:Hide()
        end

        gridSnapEnabled = enabled
    end

    action SetGridUnitSize(number units)
        grid:SetGridUnitSize(cast(integer, units))
    end

    action GetGridUnitSize returns number
        return grid:GetGridUnitSize()
    end

    action IsGridSnapping returns boolean
        return gridSnapEnabled
    end

    action GetNearestGridCoordinate(number coordinate) returns number
        return grid:GetNearestGridCoordinate(coordinate)
    end

    action GetNextGridCoordinate(number coordinate) returns number
        return grid:GetNextGridCoordinate(coordinate)
    end

    action GetPreviousGridCoordinate(number coordinate) returns number
        return grid:GetPreviousGridCoordinate(coordinate)
    end

    action UpdateGridPosition
        Camera camera = GetCamera()
        number x = camera:GetPosition():GetX() - camera:GetWidth() / 2.0
        number y = camera:GetPosition():GetY() - camera:GetHeight() / 2.0

        if not grid:IsGridCoordinate(x)
            x = grid:GetPreviousGridCoordinate(x)
        end
        if not grid:IsGridCoordinate(y)
            y = grid:GetPreviousGridCoordinate(y)
        end

        grid:SetPosition(x, y)
    end

    action ShowEditorComponents(boolean shouldShow)
        if shouldShow
            preview:Show()
            grid:Show()
            UpdateGridPosition()
        else
            preview:Hide()
            grid:Hide()
        end
    end
end