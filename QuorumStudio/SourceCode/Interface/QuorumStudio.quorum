package Libraries.Development.Environment.Studio
use Libraries.Game.Game
use Libraries.Interface.Controls.ScrollPane
use Libraries.Development.Environment.Projects.ProjectTree
use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Item2D
use Libraries.Game.Graphics.Color
use Libraries.Development.Environment.Projects.ProjectManager
use Libraries.Development.Environment.Studio.Updates.Updater
use Libraries.Development.Environment.Configuration.Options
use Libraries.Development.Environment.Resources
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Game.Layer2D
use Libraries.Containers.Array
use Libraries.System.File
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.Tab
use Libraries.Game.Graphics.Texture
use Libraries.Game.GameStateManager
use Libraries.Game.Graphics.GraphicsManager
use Libraries.Game.DesktopConfiguration
use Libraries.Interface.Item
use Libraries.Interface.Events.FocusListener
use Libraries.Interface.Events.FocusEvent
use Libraries.Game.InputTable
use Libraries.Development.Environment.Studio.Interface.KeyMap
use Libraries.Containers.Iterator
use Libraries.Development.Environment.Studio.Behaviors.ScanLibraryBehavior
use Libraries.Development.Environment.Studio.Build.BuildManager
use Libraries.Concurrency.ThreadRunner
use Libraries.System.SystemHelper
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.Control
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Game.InputMonitor
use Libraries.Interface.Events.KeyboardEvent
use Libraries.Development.Environment.Studio.Behaviors.BuildLoop
use Libraries.Game.Graphics.FramesPerSecondLabel
use Libraries.Interface.Controls.Dialog
use Libraries.Game.Graphics.Label
use Libraries.Development.Environment.Studio.Interface.SplashScreen
use Libraries.Development.Environment.Studio.Build.ScanListener
use Libraries.Development.Environment.Studio.StudioFocusManager
use Libraries.Sound.Audio
use Libraries.Interface.Events.ResizeListener
use Libraries.Interface.Events.ResizeEvent
use Libraries.Containers.HashTable
use Libraries.System.Console
use Libraries.Development.Environment.Studio.Behaviors.CodeCompletionInputTable
use Libraries.Interface.Behaviors.Controls.MenuRootDownBehavior
use Libraries.Interface.Behaviors.Controls.MenuRootUpBehavior
use Libraries.Interface.Behaviors.Controls.MenuRootActivateBehavior
use Libraries.Interface.Behaviors.Controls.MenuRootCloseBehavior
use Libraries.Game.InputSet
use Libraries.Development.Environment.Studio.Behaviors.OpenSaveBeforeClosingDialog
use Libraries.Development.Environment.Studio.Interface.SaveBeforeClosingDialog
use Libraries.Development.Environment.Studio.Behaviors.CloseCodeCompletionBehavior
use Libraries.Interface.Behaviors.Behavior
use Libraries.Development.Environment.Studio.Behaviors.QuorumStudioBehavior
use Libraries.Development.Environment.Studio.Behaviors.CameraYawRotationBehavior
use Libraries.Development.Environment.Studio.Behaviors.CameraPitchRotationBehavior
use Libraries.Development.Environment.Studio.Behaviors.CameraMoveDirectionBehavior
use Libraries.Development.Environment.Studio.Behaviors.CameraStrafeDirectionBehavior
use Libraries.Development.Environment.Studio.Behaviors.CameraVerticalDirectionBehavior
use Libraries.Development.Environment.Studio.Behaviors.CameraItemsInViewBehavior
use Libraries.Development.Environment.Studio.Behaviors.SelectionXDirectionBehavior
use Libraries.Development.Environment.Studio.Behaviors.SelectionYDirectionBehavior
use Libraries.Development.Environment.Studio.Behaviors.SelectionZDirectionBehavior
use Libraries.Development.Environment.Studio.Behaviors.SelectionScaleBehavior
use Libraries.Development.Environment.Studio.Behaviors.SelectionDeleteBehavior
use Libraries.Development.Environment.Studio.Behaviors.NewItemCycleModelBehavior
use Libraries.Development.Environment.Studio.Behaviors.NewItemCycleColorBehavior
use Libraries.Development.Environment.Studio.Behaviors.NewItemCreatePreviewBehavior
use Libraries.Development.Environment.Studio.Behaviors.NewItemCancelBehavior
use Libraries.Development.Environment.Studio.Behaviors.NewItemConfirmBehavior
use Libraries.Development.Environment.Studio.Behaviors.SelectionCancelBehavior
use Libraries.Development.Environment.Studio.Behaviors.ReportLocationBehavior
use Libraries.Development.Environment.Studio.Behaviors.ReportItemsInViewBehavior
use Libraries.Development.Environment.Studio.Behaviors.ReportCameraDirectionBehavior
use Libraries.Development.Environment.Studio.Behaviors.ToggleGridBehavior

class QuorumStudio is Game, FocusListener, ScanListener, ResizeListener
    public constant number VERSION = 1.0
    constant text NAME = "Quorum Studio " + VERSION
    public integer SAVE_CHANGES = 0
    public integer DISCARD_CHANGES = 1
    public integer CANCEL_EXIT = 2
    integer DEFAULT_WINDOW_WIDTH = 2500
    integer DEFAULT_WINDOW_HEIGHT = 1500
    ScrollPane projectPane
    ProjectTree projectTree
    CodeEditorTabPane codeTabs
    OutputEditorTabPane outputWindow
    Control mainContentPane
    // inputOutputPane contains both the code editor and the output area.
    Control inputOutputPane
    Color color
    
    //Color background
    ProjectManager manager
    Updater updater
    Toolbar toolbar
    Menu menu
    number tempXOffset = 0

    Options options
    File projectsFolder
    Resources resources
    Behaviors behaviors
    OpenSaveBeforeClosingDialog openSaveBeforeClosingBehavior
    Array<Item> focusList
    HashTable<text, integer> windowsList
    integer focus = -1

    Item currentFocus = undefined
    Item previousFocus = undefined
    SystemHelper helper
    BuildLoop buildLoop
    SplashScreen splashScreen = undefined
    boolean splashScreenHide = false
    integer exitStatus = SAVE_CHANGES

    action Start
        DesktopConfiguration config = GetDesktopConfiguration()
        config:title = NAME
        StartGame()
    end

    action SetExitStatus (integer status)
        exitStatus = status
    end

    action GetExitStatus returns integer
        return exitStatus
    end

    action ResetExitFlag
        openSaveBeforeClosingBehavior:SetExitFlag(true)
    end

    action GetExitFlag returns boolean
        return openSaveBeforeClosingBehavior:GetExitFlag()
    end

    action OnExit returns boolean
        /* Checks to see if there are unsaved changes and if this is the first prompt for the unsaved
        changes. If yes, then the Save Changes Before Closing Dialog will pop up and the user can choose: 
        SAVE ALL changes in which QuorumStudio calls exit again (exitFlag = false, so the dialog 
        does not prompt the user again.
        DISCARD ALL changes calling exit again and saving the state of quorum studio without saving any 
        changes to the files themselves.
        or CANCEL EXIT in which the dialog is closed and the exitFlag is reset to true from the cancel exit behavior
        so that the next time the user goes to exit QuroumStudio they will be prompted with the dialog again.*/
        if codeTabs:GetDirtyTabsCount() > 0 and (openSaveBeforeClosingBehavior:GetExitFlag()) 
            openSaveBeforeClosingBehavior:Run()
            SaveBeforeClosingDialog dialog = openSaveBeforeClosingBehavior:GetDialog()
            return false
        end
        if exitStatus = CANCEL_EXIT
            return false
        end
        
        ThreadRunner buildThreadRunner = behaviors:GetBuildThreadRunner()
        ThreadRunner indexThread = behaviors:GetBuildIndexingThread()
        buildLoop:SetFinished(true)
        buildThreadRunner:ShutDown()
        indexThread:ShutDown()
        BuildManager value = GetBuildManager()
        value:StopAll()
        if exitStatus = SAVE_CHANGES
            Save()
        end
        return true
    end

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action GetBuildManager returns BuildManager
        return behaviors:GetBuildManager()
    end     

    action GetCurrentFocus returns Item
        return currentFocus
    end

    action GetPreviousFocus returns Item
        return previousFocus
    end

    action GainedFocus(FocusEvent event)
        currentFocus = event:GetNewFocus()
        previousFocus = event:GetLastFocus()

        //Bubble up the parents until you find which window you're in
        //then set the window focus cycle to move there
        if currentFocus is Item2D
            Item2D newFocus = cast(Item2D, currentFocus)
            if newFocus:GetParent() not= undefined
                boolean notFound = true
                repeat while notFound and newFocus:GetParent() not= undefined
                    if windowsList:HasKey(newFocus:GetParent():GetName())
                        focus = windowsList:GetValue(newFocus:GetParent():GetName())
                        notFound = false
                    end
                    newFocus = newFocus:GetParent()
                end
            end
        else
            // If the focus wasn't an Item2D, it must be from the 3D scene editor.
            focus = windowsList:GetValue(codeTabs:GetName())
        end
    end

    action LostFocus(FocusEvent event)

    end

    action GetOutputPane returns OutputEditorTabPane
        return outputWindow
    end

    action GetProjectManager returns ProjectManager
        return manager
    end

    action GetProjectTree returns ProjectTree
        return projectTree
    end

    action GetToolbar returns Toolbar
        return toolbar
    end

    action GetTabPane returns CodeEditorTabPane
        return codeTabs
    end

    action GetProjectsFolder returns File
        return projectsFolder
    end

    action NextFocus returns Item
        focus = focus + 1
        if focus >= focusList:GetSize()
            focus = 0
        end

        return focusList:Get(focus)
    end

    action PreviousFocus returns Item
        focus = focus - 1
        if focus < 0
            focus = focusList:GetSize() - 1
        end

        return focusList:Get(focus)
    end

    action GetFocusSelection returns integer
        return focus
    end

    action Started
    end

    action Stopped
        splashScreenHide = true
    end

    action SizeChanged(ResizeEvent event)
        integer height = event:GetHeight()
        integer width = event:GetWidth()
        options:SetWidth(GetScreenWidth())
        options:SetHeight(GetScreenHeight())
        options:SetInterfaceScale(GetInterfaceScale())
    end

    action CreateGame
        resources:SetOptions(options)

        AddResizeListener(me)
        StudioFocusManager focusManager
        SetFocusManager(focusManager)
        FlowLayout flowLayout
        SetLayout(flowLayout)

        options:SetMainApplication(me)
        GameStateManager gameStateManager
        GraphicsManager graphics = gameStateManager:GetGameGraphics()
        graphics:ClearScreenColor(101.0/255.0, 101.0/255.0, 101.0/255.0, 1.0)
        SetupResources()

        Layer2D layer = me:GetCurrentLayer2D()
        layer:SetAutomaticResizing(true)
        
        Load()
        SetupInterface()
        integer width = options:GetWidth()
        integer height = options:GetHeight()
        number size = options:GetInterfaceScale()
        SetInterfaceScale(size)
        SetScreenSize(width, height)
        mainContentPane:Resize()

        SetApplicationIcon(resources:GetApplicationIcon())

        SplashScreen splash
        splashScreen = splash
        splashScreen:Initialize()
        splashScreen:SetLibrary(GetBuildManager():GetLibrary())
        splashScreen:Show()

        SetupFocus()
        SetupInterfaceScaling()
        SetupInputTables()
        
        BuildManager builds = behaviors:GetBuildManager()
        builds:SetOptions(options)
        builds:SetCodeEditorTabPane(codeTabs)
        builds:SetOutputEditorTabPane(outputWindow)
        builds:SetProjectManager(manager)

        //now that we're all setup, tell the library scanner to run
        ScanLibraryBehavior scanner = behaviors:GetScanLibraryBehavior()
        scanner:Add(me)
        BuildManager builder = behaviors:GetBuildManager()
        ThreadRunner buildThreadRunner = behaviors:GetBuildThreadRunner()
        ThreadRunner indexThread = behaviors:GetBuildIndexingThread()
        buildThreadRunner:Add(scanner)
        buildLoop:SetBuildManager(builder)

        indexThread:Add(buildLoop)
        FocusWindow()

        if options:IsShowingFramesPerSecondCounter()
            FramesPerSecondLabel fps
            fps:SetSize(50)

            //get the current Layer2D and turn off depth buffer sorting for Quorum Studio
            Layer2D currentLayer2 = GetCurrentLayer2D()
            SetColliding(false)

            Layer2D fpsLayerOfAwesome
            fpsLayerOfAwesome:Add(fps)
            AddLayer(fpsLayerOfAwesome)
            fpsLayerOfAwesome:SetDepthBufferSorting(false)
            fpsLayerOfAwesome:SetColliding(false)
        end 
    end

    action SetupVisualEditorInput
        InputTable table
        table:SetIdentifier("VisualSceneController")
        KeyMap map = resources:GetKeyMap()

        InputSet rotateLeftStart = map:GetCameraRotateLeftKey()
        InputSet rotateLeftStop = map:GetCameraRotateLeftKey()
        rotateLeftStop:SetInputTrigger(rotateLeftStop:FINISH)
        InputSet rotateRightStart = map:GetCameraRotateRightKey()
        InputSet rotateRightStop = map:GetCameraRotateRightKey()
        rotateRightStop:SetInputTrigger(rotateRightStop:FINISH)

        CameraYawRotationBehavior rotateLeftStartBehavior = behaviors:GetCameraStartRotatingLeftBehavior()
        CameraYawRotationBehavior rotateRightStartBehavior = behaviors:GetCameraStartRotatingRightBehavior()
        CameraYawRotationBehavior rotateYawStopBehavior = behaviors:GetCameraStopRotatingYawBehavior()

        table:Add(rotateLeftStart, rotateLeftStartBehavior)
        table:Add(rotateLeftStop, rotateYawStopBehavior)
        table:Add(rotateRightStart, rotateRightStartBehavior)
        table:Add(rotateRightStop, rotateYawStopBehavior)

        InputSet rotateUpStart = map:GetCameraRotateUpKey()
        InputSet rotateUpStop = map:GetCameraRotateUpKey()
        rotateUpStop:SetInputTrigger(rotateUpStop:FINISH)
        InputSet rotateDownStart = map:GetCameraRotateDownKey()
        InputSet rotateDownStop = map:GetCameraRotateDownKey()
        rotateDownStop:SetInputTrigger(rotateDownStop:FINISH)
        
        CameraPitchRotationBehavior rotateUpStartBehavior = behaviors:GetCameraStartRotatingUpBehavior()
        CameraPitchRotationBehavior rotateDownStartBehavior = behaviors:GetCameraStartRotatingDownBehavior()
        CameraPitchRotationBehavior rotatePitchStopBehavior = behaviors:GetCameraStopRotatingPitchBehavior()

        table:Add(rotateUpStart, rotateUpStartBehavior)
        table:Add(rotateUpStop, rotatePitchStopBehavior)
        table:Add(rotateDownStart, rotateDownStartBehavior)
        table:Add(rotateDownStop, rotatePitchStopBehavior)

        InputSet moveForwardStart = map:GetCameraMoveForwardKey()
        InputSet moveForwardStop = map:GetCameraMoveForwardKey()
        moveForwardStop:SetInputTrigger(moveForwardStop:FINISH)
        InputSet moveBackwardStart = map:GetCameraMoveBackwardKey()
        InputSet moveBackwardStop = map:GetCameraMoveBackwardKey()
        moveBackwardStop:SetInputTrigger(moveBackwardStop:FINISH)
        
        CameraMoveDirectionBehavior moveForwardStartBehavior = behaviors:GetCameraStartMovingForwardBehavior()
        CameraMoveDirectionBehavior moveBackwardStartBehavior = behaviors:GetCameraStartMovingBackwardBehavior()
        CameraMoveDirectionBehavior moveDirectionStopBehavior = behaviors:GetCameraStopMovingDirectionBehavior()

        table:Add(moveForwardStart, moveForwardStartBehavior)
        table:Add(moveForwardStop, moveDirectionStopBehavior)
        table:Add(moveBackwardStart, moveBackwardStartBehavior)
        table:Add(moveBackwardStop, moveDirectionStopBehavior)

        InputSet strafeLeftStart = map:GetCameraStrafeLeftKey()
        InputSet strafeLeftStop = map:GetCameraStrafeLeftKey()
        strafeLeftStop:SetInputTrigger(strafeLeftStop:FINISH)
        InputSet strafeRightStart = map:GetCameraStrafeRightKey()
        InputSet strafeRightStop = map:GetCameraStrafeRightKey()
        strafeRightStop:SetInputTrigger(strafeRightStop:FINISH)
        
        CameraStrafeDirectionBehavior strafeLeftStartBehavior = behaviors:GetCameraStartStrafingLeftBehavior()
        CameraStrafeDirectionBehavior strafeRightStartBehavior = behaviors:GetCameraStartStrafingRightBehavior()
        CameraStrafeDirectionBehavior strafeStopBehavior = behaviors:GetCameraStopStrafingBehavior()

        table:Add(strafeLeftStart, strafeLeftStartBehavior)
        table:Add(strafeLeftStop, strafeStopBehavior)
        table:Add(strafeRightStart, strafeRightStartBehavior)
        table:Add(strafeRightStop, strafeStopBehavior)

        InputSet moveUpStart = map:GetCameraMoveUpKey()
        InputSet moveUpStop = map:GetCameraMoveUpKey()
        moveUpStop:SetInputTrigger(moveUpStop:FINISH)
        InputSet moveDownStart = map:GetCameraMoveDownKey()
        InputSet moveDownStop = map:GetCameraMoveDownKey()
        moveDownStop:SetInputTrigger(moveDownStop:FINISH)
        
        CameraVerticalDirectionBehavior moveUpStartBehavior = behaviors:GetCameraStartMovingUpBehavior()
        CameraVerticalDirectionBehavior moveDownStartBehavior = behaviors:GetCameraStartMovingDownBehavior()
        CameraVerticalDirectionBehavior moveVerticalStopBehavior = behaviors:GetCameraStopMovingVerticallyBehavior()

        table:Add(moveUpStart, moveUpStartBehavior)
        table:Add(moveUpStop, moveVerticalStopBehavior)
        table:Add(moveDownStart, moveDownStartBehavior)
        table:Add(moveDownStop, moveVerticalStopBehavior)

        InputSet itemsInView = map:GetCameraItemsInViewKey()
        CameraItemsInViewBehavior itemsInViewBehavior = behaviors:GetCameraItemsInViewBehavior()
        table:Add(itemsInView, itemsInViewBehavior)

        InputSet createNewItemPreview = map:GetNewItemCreatePreviewKey()
        NewItemCreatePreviewBehavior createNewItemPreviewBehavior
        table:Add(createNewItemPreview, createNewItemPreviewBehavior)

        InputSet reportLocation = map:GetReportLocationKey()
        InputSet reportCameraDirection = map:GetReportCameraDirectionKey()
        InputSet reportItemsInView = map:GetReportItemsInViewKey()

        ReportLocationBehavior reportLocationBehavior = behaviors:GetReportLocationBehavior()
        ReportCameraDirectionBehavior reportCameraDirectionBehavior = behaviors:GetReportCameraDirectionBehavior()
        ReportItemsInViewBehavior reportItemsInViewBehavior = behaviors:GetReportItemsInViewBehavior()

        table:Add(reportLocation, reportLocationBehavior)
        table:Add(reportCameraDirection, reportCameraDirectionBehavior)
        table:Add(reportItemsInView, reportItemsInViewBehavior)

        InputSet toggleGridInput = map:GetToggleGridKey()
        ToggleGridBehavior toggleGridBehavior = behaviors:GetToggleGridBehavior()
        table:Add(toggleGridInput, toggleGridBehavior)

        AddInputTable(table)

        InputTable selectionTable
        selectionTable:SetIdentifier("SelectionHighlight")
        
        selectionTable:Add(reportLocation, reportLocationBehavior)

        InputSet selectionMoveLeftStart = map:GetSelectionMoveLeftKey()
        InputSet selectionMoveLeftStop = map:GetSelectionMoveLeftKey()
        selectionMoveLeftStop:SetInputTrigger(selectionMoveLeftStop:FINISH)
        InputSet selectionMoveRightStart = map:GetSelectionMoveRightKey()
        InputSet selectionMoveRightStop = map:GetSelectionMoveRightKey()
        selectionMoveRightStop:SetInputTrigger(selectionMoveRightStop:FINISH)

        SelectionXDirectionBehavior selectionMoveLeftStartBehavior = behaviors:GetSelectionStartMovingLeftBehavior()
        SelectionXDirectionBehavior selectionMoveRightStartBehavior = behaviors:GetSelectionStartMovingRightBehavior()
        SelectionXDirectionBehavior selectionMoveXStopBehavior = behaviors:GetSelectionStopMovingXBehavior()

        selectionTable:Add(selectionMoveLeftStart, selectionMoveLeftStartBehavior)
        selectionTable:Add(selectionMoveLeftStop, selectionMoveXStopBehavior)
        selectionTable:Add(selectionMoveRightStart, selectionMoveRightStartBehavior)
        selectionTable:Add(selectionMoveRightStop, selectionMoveXStopBehavior)

        InputSet selectionMoveUpStart = map:GetSelectionMoveUpKey()
        InputSet selectionMoveUpStop = map:GetSelectionMoveUpKey()
        selectionMoveUpStop:SetInputTrigger(selectionMoveUpStop:FINISH)
        InputSet selectionMoveDownStart = map:GetSelectionMoveDownKey()
        InputSet selectionMoveDownStop = map:GetSelectionMoveDownKey()
        selectionMoveDownStop:SetInputTrigger(selectionMoveDownStop:FINISH)

        SelectionYDirectionBehavior selectionMoveUpStartBehavior = behaviors:GetSelectionStartMovingUpBehavior()
        SelectionYDirectionBehavior selectionMoveDownStartBehavior = behaviors:GetSelectionStartMovingDownBehavior()
        SelectionYDirectionBehavior selectionMoveYStopBehavior = behaviors:GetSelectionStopMovingYBehavior()

        selectionTable:Add(selectionMoveUpStart, selectionMoveUpStartBehavior)
        selectionTable:Add(selectionMoveUpStop, selectionMoveYStopBehavior)
        selectionTable:Add(selectionMoveDownStart, selectionMoveDownStartBehavior)
        selectionTable:Add(selectionMoveDownStop, selectionMoveYStopBehavior)

        InputSet selectionMoveForwardStart = map:GetSelectionMoveForwardKey()
        InputSet selectionMoveForwardStop = map:GetSelectionMoveForwardKey()
        selectionMoveForwardStop:SetInputTrigger(selectionMoveForwardStop:FINISH)
        InputSet selectionMoveBackwardStart = map:GetSelectionMoveBackwardKey()
        InputSet selectionMoveBackwardStop = map:GetSelectionMoveBackwardKey()
        selectionMoveBackwardStop:SetInputTrigger(selectionMoveBackwardStop:FINISH)

        SelectionZDirectionBehavior selectionMoveForwardStartBehavior = behaviors:GetSelectionStartMovingForwardBehavior()
        SelectionZDirectionBehavior selectionMoveBackwardStartBehavior = behaviors:GetSelectionStartMovingBackwardBehavior()
        SelectionZDirectionBehavior selectionMoveZStopBehavior = behaviors:GetSelectionStopMovingZBehavior()

        selectionTable:Add(selectionMoveForwardStart, selectionMoveForwardStartBehavior)
        selectionTable:Add(selectionMoveForwardStop, selectionMoveZStopBehavior)
        selectionTable:Add(selectionMoveBackwardStart, selectionMoveBackwardStartBehavior)
        selectionTable:Add(selectionMoveBackwardStop, selectionMoveZStopBehavior)

        InputSet selectionScaleUpSet = map:GetSelectionScaleUpKey()
        InputSet selectionScaleDownSet = map:GetSelectionScaleDownKey()

        SelectionScaleBehavior selectionScaleUpBehavior = behaviors:GetSelectionScaleUpBehavior()
        SelectionScaleBehavior selectionScaleDownBehavior = behaviors:GetSelectionScaleDownBehavior()

        selectionTable:Add(selectionScaleUpSet, selectionScaleUpBehavior)
        selectionTable:Add(selectionScaleDownSet, selectionScaleDownBehavior)

        InputSet selectionScaleXUpSet = map:GetSelectionScaleXUpKey()
        InputSet selectionScaleXDownSet = map:GetSelectionScaleXDownKey()

        SelectionScaleBehavior selectionScaleXUpBehavior = behaviors:GetSelectionScaleXUpBehavior()
        SelectionScaleBehavior selectionScaleXDownBehavior = behaviors:GetSelectionScaleXDownBehavior()

        selectionTable:Add(selectionScaleXUpSet, selectionScaleXUpBehavior)
        selectionTable:Add(selectionScaleXDownSet, selectionScaleXDownBehavior)

        InputSet selectionScaleYUpSet = map:GetSelectionScaleYUpKey()
        InputSet selectionScaleYDownSet = map:GetSelectionScaleYDownKey()

        SelectionScaleBehavior selectionScaleYUpBehavior = behaviors:GetSelectionScaleYUpBehavior()
        SelectionScaleBehavior selectionScaleYDownBehavior = behaviors:GetSelectionScaleYDownBehavior()

        selectionTable:Add(selectionScaleYUpSet, selectionScaleYUpBehavior)
        selectionTable:Add(selectionScaleYDownSet, selectionScaleYDownBehavior)

        InputSet selectionScaleZUpSet = map:GetSelectionScaleZUpKey()
        InputSet selectionScaleZDownSet = map:GetSelectionScaleZDownKey()

        SelectionScaleBehavior selectionScaleZUpBehavior = behaviors:GetSelectionScaleZUpBehavior()
        SelectionScaleBehavior selectionScaleZDownBehavior = behaviors:GetSelectionScaleZDownBehavior()

        selectionTable:Add(selectionScaleZUpSet, selectionScaleZUpBehavior)
        selectionTable:Add(selectionScaleZDownSet, selectionScaleZDownBehavior)

        InputSet selectionDeleteSet = map:GetSelectionDeleteKey()
        InputSet selectionCancelSet = map:GetSelectionCancelKey()

        SelectionDeleteBehavior selectionDeleteBehavior = behaviors:GetSelectionDeleteBehavior()
        SelectionCancelBehavior selectionCancelBehavior = behaviors:GetSelectionCancelBehavior()

        selectionTable:Add(selectionDeleteSet, selectionDeleteBehavior)
        selectionTable:Add(selectionCancelSet, selectionCancelBehavior)

        selectionTable:Add(toggleGridInput, toggleGridBehavior)

        AddInputTable(selectionTable)

        InputTable itemPreviewTable
        itemPreviewTable:SetIdentifier("NewItemPreview")

        itemPreviewTable:Add(reportLocation, reportLocationBehavior)

        // The NewItemPreview shares the same movement/scaling behavior as SelectionHighlights.
        itemPreviewTable:Add(selectionMoveLeftStart, selectionMoveLeftStartBehavior)
        itemPreviewTable:Add(selectionMoveLeftStop, selectionMoveXStopBehavior)
        itemPreviewTable:Add(selectionMoveRightStart, selectionMoveRightStartBehavior)
        itemPreviewTable:Add(selectionMoveRightStop, selectionMoveXStopBehavior)
        itemPreviewTable:Add(selectionMoveUpStart, selectionMoveUpStartBehavior)
        itemPreviewTable:Add(selectionMoveUpStop, selectionMoveYStopBehavior)
        itemPreviewTable:Add(selectionMoveDownStart, selectionMoveDownStartBehavior)
        itemPreviewTable:Add(selectionMoveDownStop, selectionMoveYStopBehavior)
        itemPreviewTable:Add(selectionMoveForwardStart, selectionMoveForwardStartBehavior)
        itemPreviewTable:Add(selectionMoveForwardStop, selectionMoveZStopBehavior)
        itemPreviewTable:Add(selectionMoveBackwardStart, selectionMoveBackwardStartBehavior)
        itemPreviewTable:Add(selectionMoveBackwardStop, selectionMoveZStopBehavior)

        itemPreviewTable:Add(selectionScaleUpSet, selectionScaleUpBehavior)
        itemPreviewTable:Add(selectionScaleDownSet, selectionScaleDownBehavior)
        itemPreviewTable:Add(selectionScaleXUpSet, selectionScaleXUpBehavior)
        itemPreviewTable:Add(selectionScaleXDownSet, selectionScaleXDownBehavior)
        itemPreviewTable:Add(selectionScaleYUpSet, selectionScaleYUpBehavior)
        itemPreviewTable:Add(selectionScaleYDownSet, selectionScaleYDownBehavior)
        itemPreviewTable:Add(selectionScaleZUpSet, selectionScaleZUpBehavior)
        itemPreviewTable:Add(selectionScaleZDownSet, selectionScaleZDownBehavior)

        InputSet newItemCycleModel = map:GetNewItemCycleModelKey()
        InputSet newItemCycleColor = map:GetNewItemCycleColorKey()

        NewItemCycleModelBehavior newItemCycleModelBehavior = behaviors:GetNewItemCycleModelBehavior()
        NewItemCycleColorBehavior newItemCycleColorBehavior = behaviors:GetNewItemCycleColorBehavior()

        itemPreviewTable:Add(newItemCycleModel, newItemCycleModelBehavior)
        itemPreviewTable:Add(newItemCycleColor, newItemCycleColorBehavior)

        InputSet newItemCancel = map:GetNewItemCancelKey()
        InputSet newItemConfirm = map:GetNewItemConfirmKey()

        NewItemCancelBehavior newItemCancelBehavior = behaviors:GetNewItemCancelBehavior()
        NewItemConfirmBehavior newItemConfirmBehavior = behaviors:GetNewItemConfirmBehavior()

        itemPreviewTable:Add(newItemCancel, newItemCancelBehavior)
        itemPreviewTable:Add(newItemConfirm, newItemConfirmBehavior)

        itemPreviewTable:Add(toggleGridInput, toggleGridBehavior)

        AddInputTable(itemPreviewTable)
    end

    action SetupFocus
    /*
        The order the items are added to the focusList and the value
        associated with them in the hashTable affect the way in which
        focus cycles between the main windows
    */
        focusList:Add(toolbar)
        focusList:Add(projectTree)
        focusList:Add(codeTabs)
        focusList:Add(outputWindow)
        windowsList:Add(toolbar:GetName(), 0)
        windowsList:Add(projectTree:GetName(), 1)
        windowsList:Add(codeTabs:GetName(), 2)
        windowsList:Add(outputWindow:GetName(), 3)

        KeyMap keyMap = resources:GetKeyMap()
        
        AddFocusListener(me)
    end

    action SetupInterfaceScaling
        InputTable table = GetDefaultInputTable()
        KeyMap keyMap = resources:GetKeyMap()

        //table:Add(keyMap:GetScaleInterfaceUpKey(), behaviors:GetScaleInterfaceUpBehavior())
        //table:Add(keyMap:GetScaleInterfaceDownKey(), behaviors:GetScaleInterfaceDownBehavior())
    end

    action GetResources returns Resources
        return resources
    end

    action SetupResources
        manager:Add(projectTree)
        manager:Add(options)

        //load shared resources for all GUI components
        resources:Load()
        projectTree:SetResources(resources)
        projectTree:SetMainApplication(me)
        toolbar:SetResources(resources)
        menu:SetResources(resources)
        codeTabs:SetResources(resources)
        outputWindow:SetResources(resources)

        projectTree:SetProjectManager(manager)
        AddSelectionListener(projectTree)
        
        toolbar:SetBehaviors(behaviors)
        menu:SetBehaviors(behaviors)
        outputWindow:SetBehaviors(behaviors)
        codeTabs:SetBehaviors(behaviors)
        behaviors:SetMainApplication(me)
    end

    action SetupInterface
        menu:Setup()
        toolbar:Setup()

        CreateMainContentPane()
        Add(menu)
        Add(toolbar)
        Add(mainContentPane)
    end

    action SetupInputTables
        KeyboardEvent keys

        CodeCompletionInputTable codeCompletionTable

        MenuRootDownBehavior menuDownBehavior
        MenuRootUpBehavior menuUpBehavior
        MenuRootActivateBehavior menuRunBehavior
        CloseCodeCompletionBehavior menuCloseBehavior

        // Used only to prevent certain inputs from propagating downward
        Behavior emptyBehavior

        QuorumStudioBehavior codeCompletionBehavior = behaviors:GetCodeCompletionBehavior()

        InputSet downArrow
        InputSet upArrow
        InputSet enterSet
        InputSet escapeSet
        InputSet spaceSet
        downArrow:SetKeyboardInput(keys:DOWN)
        upArrow:SetKeyboardInput(keys:UP)
        enterSet:SetKeyboardInput(keys:ENTER)
        escapeSet:SetKeyboardInput(keys:ESCAPE)
        spaceSet:SetKeyboardInput(keys:SPACE)

        InputSet releaseDownArrow
        InputSet releaseUpArrow
        releaseDownArrow:SetKeyboardInput(keys:DOWN)
        releaseDownArrow:SetInputTrigger(releaseDownArrow:FINISH)
        releaseUpArrow:SetKeyboardInput(keys:UP)
        releaseUpArrow:SetInputTrigger(releaseUpArrow:FINISH)

        codeCompletionTable:Add(downArrow, menuDownBehavior)
        codeCompletionTable:Add(upArrow, menuUpBehavior)
        codeCompletionTable:Add(enterSet, menuRunBehavior)
        codeCompletionTable:Add(escapeSet, menuCloseBehavior)
        codeCompletionTable:Add(spaceSet, menuCloseBehavior)
        codeCompletionTable:Add(releaseDownArrow, emptyBehavior)
        codeCompletionTable:Add(releaseUpArrow, emptyBehavior)

        AddInputTable(codeCompletionTable)
        SetupVisualEditorInput()
    end

    action Load
        //load the user's settings
        options:Read()

        me:projectsFolder = options:GetProjectsFolder()
        if not projectsFolder:Exists()
            projectsFolder:CreateDirectories()
        end

        Array<text> projects = options:GetProjects()

        i = 0
        repeat while i < projects:GetSize()
            text value = projects:Get(i)
            File file
            file:SetAbsolutePath(value)
            manager:Open(file)
            i = i + 1
        end

        Iterator<File> iterator = options:GetFilesOpenOnStartup()
        repeat while iterator:HasNext()
            File file = iterator:Next()
            codeTabs:OpenNewTab(file)
        end
        codeTabs:Display(0)

        Console console
        Array<text> flags = console:GetConsoleArguments()
        if flags not= undefined and flags:GetSize() > 0
            i = 0
            repeat while i < flags:GetSize()
                text value = flags:Get(i)
                check
                File file
                file:SetAbsolutePath(value)
                if file:Exists()
                    manager:Open(file)
                end
                detect e //if the user passed junk, just ignore it
                end
                i = i + 1
            end
        end
    end

    action Save
        options:SetWidth(GetScreenWidth())
        options:SetHeight(GetScreenHeight())
        options:SetInterfaceScale(GetInterfaceScale())
        manager:SaveOpenProjects()
        options:Write()
        File mapFile = options:GetKeyMapFile()
        KeyMap map = resources:GetKeyMap()
        if map not= undefined
            map:Save(mapFile)
        end

        codeTabs:SaveDirtyTabs()
    end

    action GetMenu returns Menu
        return menu
    end

    private action CreateMainContentPane
        LayoutProperties layoutProperties = mainContentPane:GetDefaultLayoutProperties()
        layoutProperties:SetVerticalLayoutMode(layoutProperties:FILL)
        layoutProperties:SetPercentageWidth(1)
        layoutProperties:SetTopPadding(5)

        FlowLayout layout
        mainContentPane:SetLayout(layout)
        mainContentPane:SetName("Main Content")

        CreateProjectsWindow()
        CreateCodeEditor()
        CreateOutputWindow()

        LayoutProperties inputOutputProperties = inputOutputPane:GetDefaultLayoutProperties()
        inputOutputProperties:SetHorizontalLayoutMode(inputOutputProperties:FILL)
        inputOutputProperties:SetVerticalLayoutMode(inputOutputProperties:FILL)
        FlowLayout inputOutputLayout
        inputOutputPane:SetLayout(inputOutputLayout)
        inputOutputPane:SetName("Input Output")

        mainContentPane:Add(projectPane)
        inputOutputPane:Add(codeTabs)
        inputOutputPane:Add(outputWindow)
        mainContentPane:Add(inputOutputPane)
    end

    action CreateCodeEditor
    end

    action CreateProjectsWindow()
        LayoutProperties scrollLayout = projectPane:GetDefaultLayoutProperties()
        scrollLayout:SetVerticalLayoutMode(scrollLayout:FILL)
        scrollLayout:SetHorizontalLayoutMode(scrollLayout:STANDARD)
        scrollLayout:SetPixelWidth(300)

        projectPane:SetBackgroundColor(resources:GetBackgroundColor())
        projectPane:SetName("Project Pane")
        projectPane:Add(projectTree)
    end

    private action RemoveHiddenFiles(Array<File> directory)
        integer i = 0
        repeat while i < directory:GetSize()
            if(directory:Get(i):IsHidden())
                directory:Remove(directory:Get(i))
            end
            i = i + 1
        end
    end

    action CreateOutputWindow()
        outputWindow:Setup()
    end

    action LoadUpdates
        File defaultUpdateFile
        defaultUpdateFile:SetPath("Configuration/Updates.json")
        updater:Load(defaultUpdateFile)

        Downloader download
        download:SetPath("https://quorumlanguage.com/studio/live/Configuration/Updates.json")
        boolean value = download:Exists()

        File downloadHere
        downloadHere:SetPath("Updates/Configuration/Updates.json")
        File mom = downloadHere:GetParentDirectory()
        if not mom:Exists()
            mom:CreateDirectories()
        end

        download:SetFile(downloadHere)
        download:Download()
        
        Updater newUpdate
        newUpdate:Load(downloadHere)
        updater:CheckForNewerVersion(newUpdate)
        if updater:HasUpdates()
            updater:DownloadModules()
        end
    end

    action CheckForUpdates
        //first ping the server and see if the files are there.
        
        //get the file --- for now let's get one from testing
        File new
        new:SetPath("Tests/UpdatesTests/UpdateNewVersion.json")

        Updater up
        up:Load(new)

        updater:CheckForNewerVersion(up)
    end

    action IsSupportedFile(File file) returns boolean
        text extension = file:GetFileExtension()
        if extension = "quorum"
            return true
        elseif extension = "qs"
            return true
        else
            //other supported extensions would be here
        end
        return false
    end

    action GetStudioFocusManager returns StudioFocusManager
        return cast(StudioFocusManager, GetFocusManager())
    end

    action Update(number seconds)
        if splashScreenHide 
            splashScreen:Hide()
            splashScreenHide = false
            //The projects have to loaded to correctly open all the subtrees
            //so this is happening here
            projectTree:LoadExpandState(options:GetTreeState())
            projectTree:LoadSelection(options:GetTreeSelect())
            SetFocus(projectTree)
        end
    end
end
