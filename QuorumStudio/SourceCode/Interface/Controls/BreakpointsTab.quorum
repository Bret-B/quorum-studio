package Libraries.Development.Environment.Studio.Interface

use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.Tree
use Libraries.Game.Graphics.Color
use Libraries.Development.Environmen.Resources
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Development.Environment.Studio.Build.BuildManager
use Libraries.Language.Debug.Debugger
use Libraries.Interface.Controls.TreeItem
use Libraries.Interface.Layouts.Layout
use Libraries.Language.Debug.BreakpointListener
use Libraries.Language.Debug.BreakpointEvent
use Libraries.Language.Debug.Breakpoint
use Libraries.System.File
use Libraries.Containers.Iterator

class BreakpointsTab is Tab, BreakpointListener
    BreakpointScrollPane pane
    Resources resources = undefined
    Behaviors behaviors = undefined
    BuildManager manager = undefined
    Debugger debugger = undefined
    Tree breakpointTree

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action SetBehaviors(Behaviors behaviors)
        me:behaviors = behaviors
        manager = behaviors:GetBuildManager()
        debugger = manager:GetDebugger()

        manager:Add(me)
    end

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
    end

    action GetPane returns BreakpointScrollPane
        return pane
    end

    action ToggledBreakpoint(BreakpointEvent event)
        Breakpoint point = event:GetBreakpoint()
        File file = point:GetFile()
        integer line = point:GetLine()

        if event:IsAdded()
            boolean found = false
            Iterator<TreeItem> items = breakpointTree:GetTreeItems()
            repeat while items:HasNext() and not found
                TreeItem item = items:Next()
                if item:GetName() = file:GetFileName()
                    found = true
                    TreeItem newOne
                    newOne:Initialize("" + line, resources:GetBreakpoint())
                    item:Add(newOne)
                end
            end

            if not found
                TreeItem top
                top:Initialize("" + file:GetFileName(), resources:GetBreakpoint())
                
                TreeItem newOne
                newOne:Initialize("" + line, resources:GetBreakpoint())
                top:Add(newOne)

                breakpointTree:Add(top)
            end
            breakpointTree:Resize()
        else 
            boolean found = false
            Iterator<TreeItem> items = breakpointTree:GetTreeItems()
            repeat while items:HasNext() and not found
                TreeItem item = items:Next()
                if item:GetName() = file:GetFileName()
                    found = true
                    if item:GetSize() = 1
                        breakpointTree:Remove(item)
                        found = true
                    else 
                        Iterator<TreeItem> lines = item:GetTreeItems()
                        repeat while lines:HasNext()
                            TreeItem l = lines:Next()
                            if l:GetName() = "" + line
                                item:Remove(l)
                                found = true
                            end
                        end
                    end
                end
            end
        end
    end

    action Setup
        Layout treeLayout
        treeLayout:SetPercentageOriginY(1)
        treeLayout:SetPercentageY(1)
        breakpointTree:AddLayout(treeLayout)
        
        pane:Setup()
        pane:Initialize(1,1, resources:GetBackgroundColor())
        pane:Add(breakpointTree)
        pane:Resize()

        Initialize("Breakpoints", GetPane(), true)
    end
end