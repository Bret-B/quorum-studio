package Libraries.Development.Environment.Studio

use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Views.ImageControlView
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Controls.Button
use Libraries.Interface.Views.LabelBoxView
use Libraries.Development.Environment.Resources
use Libraries.Game.Graphics.Gradient
use Libraries.Game.Graphics.Color
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Game.Graphics.Texture
use Libraries.Interface.Item2D
use Libraries.Interface.Events.FocusEvent
use Libraries.Containers.Array
use Libraries.Interface.Layouts.ToolbarLayout
use Libraries.Interface.Controls.Control
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Controls.ToolBar
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.Glyph
use Libraries.Game.Graphics.TextureRegion
use Libraries.Game.Graphics.Drawable
use Libraries.System.File
use Libraries.Development.Environment.Studio.Interface.KeyMap
use Libraries.Containers.HashTable

class Toolbar is ToolBar
    Layout layout = undefined
    Resources resources = undefined
    //Gradient gradient
    Color color
    number height = 0
    Behaviors behaviors = undefined
    KeyMap keymap = undefined
    Array<Button> buttons
    HashTable<text, Button> buttonMap

    on create
        SetFocusable(true)
    end

    action GainedFocus(FocusEvent event)
        if buttons:GetSize() > 0
            Button first = buttons:Get(0)
            first:Focus()
        end
    end

    action SetBehaviors(Behaviors behaviors)
        me:behaviors = behaviors
    end

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
        me:keymap = resources:GetKeyMap()
    end

    action GetPreviousFocus returns Item
        return parent:Item:GetPreviousFocus()
    end

    action GetNextFocus returns Item
        return parent:Item:GetNextFocus()
    end

    action Setup
        SetPixelHeight(34)
        AddButtonToToolbar(resources:GetNewProject(), behaviors:GetOpenNewProjectBehavior(), "New Project", keymap:GetNewProjectKey():GetDisplayName())
        AddButtonToToolbar(resources:GetNewFile(), behaviors:GetOpenNewFileBehavior(), "New File", keymap:GetNewFileKey():GetDisplayName())
        AddButtonToToolbar(resources:GetOpenProject(), behaviors:GetOpenProjectBehavior(), "Open Project", keymap:GetOpenProjectKey():GetDisplayName())
        AddButtonToToolbar(resources:GetSaveAll(), behaviors:GetSaveAllBehavior(), "Save All", keymap:GetSaveAllKey():GetDisplayName())
        AddButtonToToolbar(resources:GetUndo(), behaviors:GetUndoBehavior(), "Undo", keymap:GetUndoKey():GetDisplayName())
        AddButtonToToolbar(resources:GetRedo(), behaviors:GetRedoBehavior(), "Redo", keymap:GetRedoKey():GetDisplayName())
        AddButtonToToolbar(resources:GetBuild(), behaviors:GetThreadedBuildProjectBehavior(), "Build", keymap:GetBuildKey():GetDisplayName())
        AddButtonToToolbar(resources:GetCleanAndBuild(), behaviors:GetThreadedCleanBuildProjectBehavior(), "Clean and Build", keymap:GetCleanBuildKey():GetDisplayName())
        AddButtonToToolbar(resources:GetRun(), behaviors:GetThreadedRunProjectBehavior(), "Run", keymap:GetRunKey():GetDisplayName())
        AddButtonToToolbar(resources:GetDebug(), behaviors:GetThreadedDebugProjectBehavior(), "Debug", keymap:GetDebugKey():GetDisplayName())
        AddButtonToToolbar(resources:GetStop(), behaviors:GetThreadedStopBehavior(), "Stop", keymap:GetStopKey():GetDisplayName())
        AddButtonToToolbar(resources:GetPause(), behaviors:GetThreadedPauseBehavior(), "Pause", keymap:GetPauseKey():GetDisplayName())
        AddButtonToToolbar(resources:GetContinue(), behaviors:GetThreadedContinueBehavior(), "Continue", keymap:GetContinueKey():GetDisplayName())
        AddButtonToToolbar(resources:GetStepOver(), behaviors:GetThreadedStepOverBehavior(), "Step Over", keymap:GetStepOverKey():GetDisplayName())
        AddButtonToToolbar(resources:GetStepInto(), behaviors:GetThreadedStepIntoBehavior(), "Step Into", keymap:GetStepIntoKey():GetDisplayName())
        AddButtonToToolbar(resources:GetStepOut(), behaviors:GetThreadedStepOutBehavior(), "Step Out", keymap:GetStepOutKey():GetDisplayName())
        SetupButtonFocus()
    end

    action SetupButtonFocus
        i = 0
        repeat while i < buttons:GetSize()
            Button button = buttons:Get(i)
            if i + 1 < buttons:GetSize()
                Button next = buttons:Get(i+1)
                button:SetNextFocus(next)
                next:SetPreviousFocus(button)
            end
            i = i + 1
        end

        Button first = buttons:Get(0)
        Button last = buttons:Get(buttons:GetSize() - 1)
        first:SetPreviousFocus(last)
        last:SetNextFocus(first)

        SetNextFocus(first)
        SetPreviousFocus(last)
    end

    private action AddButtonToToolbar(Icon image, Behavior behavior, text name, text description)
        Button button
        button:SetFont(undefined)
        
        button:SetIcon(image)
        button:SetTopPadding(3)
        button:SetBottomPadding(3)
        button:SetLeftPadding(5)
        button:SetName(name)
        button:SetDescription(description)

        if behavior not= undefined
            button:SetBehavior(behavior)
        end
        Add(button)
        buttons:Add(button)

        buttonMap:Add(name, button)
    end

    action GetButton(text name) returns Button
        if buttonMap:HasKey(name)
            return buttonMap:GetValue(name)
        else
            return undefined
        end
    end
end
