package Libraries.Development.Environment.Studio

use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Views.ImageControlView
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Controls.Button
use Libraries.Interface.Views.LabelBoxView
use Libraries.Development.Environmen.Resources
use Libraries.Game.Graphics.Gradient
use Libraries.Game.Graphics.Color
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Game.Graphics.Texture
use Libraries.Interface.Item2D
use Libraries.Interface.Events.FocusEvent
use Libraries.Containers.Array
use Libraries.Interface.Layouts.ToolbarLayout

class Toolbar is Item2D
    Layout layout = undefined
    Resources resources = undefined
    Gradient gradient
    Color color
    number height = 0
    Behaviors behaviors = undefined
    Array<Button> buttons

    on create
        gradient:Set(color:LightGray(), color:LightGray(), color:White(), color:White())
        SetFocusable(true)
        SetName("Toolbar")
        SetAccessibilityCode(parent:Item:TOOLBAR)
    end

    action SetBehaviors(Behaviors behaviors)
        me:behaviors = behaviors
    end

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action SetDisplayOffset(number value)
        height = value
    end

    action GetDisplayOffset returns number
        return height
    end

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
    end

    action GetLayout returns Layout
        return layout
    end

    action SetLayout(Layout layout)
        me:layout = layout
    end

    action Layout
        if layout not= undefined
            layout:Apply(me, me:GetWidth(), me:GetHeight())
        end
    end

    action GainedFocus(FocusEvent event)
        Button button = buttons:Get(0)
        button:Focus()
    end

    action Setup
        LabelBoxView content
        content:Initialize(gradient, color:Black(), "")
        content:SetBorderThickness(0)
        SetView2D(content)
        

        ToolbarLayout toolbarLayout
        toolbarLayout:SetWrapping(true)
        // Width is the full width of the screen.
        toolbarLayout:SetPercentageWidth(1)
        // Height is fixed to 100 pixels.
        toolbarLayout:SetPercentageHeight(0)
        toolbarLayout:SetHeightOffset(24)
        toolbarLayout:SetLeftPadding(6)
        toolbarLayout:SetTopPadding(6)
        toolbarLayout:SetBottomPadding(6)

        // Toolbar is placed at top of screen, minus 100 pixels.
        toolbarLayout:SetPercentageY(1)
        toolbarLayout:SetPercentageOriginY(1)
        toolbarLayout:SetOffsetY(height)
        AddLayout(toolbarLayout)

        AddButtonToToolbar(resources:GetNewProject(), undefined, "New Project")
        AddButtonToToolbar(resources:GetNewFile(), undefined, "New File")
        AddButtonToToolbar(resources:GetOpenProject(), behaviors:GetOpenProjectBehavior(), "Open Project")
        AddButtonToToolbar(resources:GetSaveAll(), undefined, "Save All")
        AddButtonToToolbar(resources:GetUndo(), undefined, "Undo")
        AddButtonToToolbar(resources:GetRedo(), undefined, "Redo")
        AddButtonToToolbar(resources:GetBuild(), behaviors:GetThreadedBuildProjectBehavior(), "Build")
        AddButtonToToolbar(resources:GetCleanAndBuild(), undefined, "Clean and Build")
        AddButtonToToolbar(resources:GetRun(), behaviors:GetThreadedRunProjectBehavior(), "Run")
        AddButtonToToolbar(resources:GetDebug(), behaviors:GetThreadedDebugProjectBehavior(), "Debug")
        AddButtonToToolbar(resources:GetStop(), behaviors:GetThreadedStopBehavior(), "Stop")
        AddButtonToToolbar(resources:GetPause(), undefined, "Pause")
        AddButtonToToolbar(resources:GetContinue(), undefined, "Continue")
        AddButtonToToolbar(resources:GetStepOver(), behaviors:GetThreadedStepOverBehavior(), "Step Over")
        AddButtonToToolbar(resources:GetStepInto(), behaviors:GetThreadedStepIntoBehavior(), "Step Into")
        AddButtonToToolbar(resources:GetStepOut(), behaviors:GetThreadedStepOutBehavior(), "Step Out")
        SetupButtonFocus()
        
        Resize()
    end

    action SetupButtonFocus
        i = 0
        repeat while i < buttons:GetSize()
            Button button = buttons:Get(i)
            if i + 1 < buttons:GetSize()
                Button next = buttons:Get(i+1)
                button:SetNextFocus(next)
                next:SetPreviousFocus(button)
            end
            i = i + 1
        end

        Button first = buttons:Get(0)
        Button last = buttons:Get(buttons:GetSize() - 1)
        first:SetPreviousFocus(last)
        last:SetNextFocus(first)
    end

    private action AddButtonToToolbar(Texture image, Behavior behavior, text name)
        ImageControlView buttonContent
        buttonContent:Load(image)
        
        Button button
        button:SetView2D(buttonContent)
        button:SetWidth(24)
        button:SetHeight(24)
        button:SetName(name)

        if behavior not= undefined
            button:SetBehavior(behavior)
        end
        Add(button)
        buttons:Add(button)
    end
end
