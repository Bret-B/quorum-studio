package Libraries.Development.Environment.Studio

use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Views.ImageControlView
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Controls.Button
use Libraries.Interface.Views.LabelBoxView
use Libraries.Development.Environment.Resources
use Libraries.Game.Graphics.Gradient
use Libraries.Game.Graphics.Color
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Game.Graphics.Texture
use Libraries.Interface.Item2D
use Libraries.Interface.Events.FocusEvent
use Libraries.Containers.Array
use Libraries.Interface.Layouts.ToolbarLayout
use Libraries.Interface.Controls.Control
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Controls.ToolBar
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.Glyph
use Libraries.Game.Graphics.TextureRegion
use Libraries.Game.Graphics.Drawable
use Libraries.System.File
use Libraries.Development.Environment.Studio.Interface.KeyMap
use Libraries.Containers.HashTable
use Libraries.Development.Environment.Configuration.OptionsConstants
use Libraries.Game.GameStateManager
use Libraries.Interface.Options.InterfaceOptions
use Libraries.Game.Graphics.ColorGroup

class Toolbar is ToolBar
    Layout layout = undefined
    Resources resources = undefined
    Color color
    number height = 0
    Behaviors behaviors = undefined
    KeyMap keymap = undefined
    Array<Button> buttons
    HashTable<text, Button> buttonMap
    Font fontAwesome
    OptionsConstants const
    Color transparent
        
    on create
        SetFocusable(true)
        File file
        file:SetPath("Resources/Fonts/Font Awesome 6 Free-Solid-900.otf")
        fontAwesome:LoadFont(file)

        SetFont(fontAwesome)
        SetFontSize(30)
        LayoutProperties properties = GetDefaultLayoutProperties()
        SetVerticalLayoutMode(properties:FIT_CONTENTS)
        transparent:SetAlpha(0.0)
    end

    action SetFontSize(integer size)
        parent:Control:SetFontSize(size)
        integer counter = 0
        repeat while counter < buttons:GetSize()
            buttons:Get(counter):SetFontSize(size)
            counter = counter + 1
        end
    end

    action GainedFocus(FocusEvent event)
        if buttons:GetSize() > 0
            Button first = buttons:Get(0)
            first:Focus()
        end
    end

    action SetBehaviors(Behaviors behaviors)
        me:behaviors = behaviors
    end

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
        me:keymap = resources:GetKeyMap()
    end

    action GetPreviousFocus returns Item
        return parent:Item:GetPreviousFocus()
    end

    action GetNextFocus returns Item
        return parent:Item:GetNextFocus()
    end

    action Setup
        AddButtonToToolbar(61892, const:TOOLBAR_FOLDER_BUTTONS_COLOR, behaviors:GetOpenNewProjectBehavior(), "New Project", keymap:GetNewProjectKey():GetDisplayName())
        AddButtonToToolbar(61897, const:TOOLBAR_FOLDER_BUTTONS_COLOR, behaviors:GetOpenNewFileBehavior(), "New File", keymap:GetNewFileKey():GetDisplayName())
        AddButtonToToolbar(61564, const:TOOLBAR_FOLDER_BUTTONS_COLOR, behaviors:GetOpenProjectBehavior(), "Open Project", keymap:GetOpenProjectKey():GetDisplayName())
        AddButtonToToolbar(61639, const:TOOLBAR_FOLDER_BUTTONS_COLOR, behaviors:GetSaveAllBehavior(), "Save All", keymap:GetSaveAllKey():GetDisplayName())
        AddButtonToToolbar(61666, const:TOOLBAR_FOLDER_BUTTONS_COLOR, behaviors:GetUndoBehavior(), "Undo", keymap:GetUndoKey():GetDisplayName())
        AddButtonToToolbar(61470, const:TOOLBAR_FOLDER_BUTTONS_COLOR, behaviors:GetRedoBehavior(), "Redo", keymap:GetRedoKey():GetDisplayName())
        AddButtonToToolbar(63203, const:TOOLBAR_PLAY_BUTTON_COLOR, behaviors:GetThreadedBuildProjectBehavior(), "Build", keymap:GetBuildKey():GetDisplayName())
        AddButtonToToolbar(57450, const:TOOLBAR_PLAY_BUTTON_COLOR, behaviors:GetThreadedCleanBuildProjectBehavior(), "Clean and Build", keymap:GetCleanBuildKey():GetDisplayName())
        AddButtonToToolbar(61515, const:TOOLBAR_PLAY_BUTTON_COLOR, behaviors:GetThreadedRunProjectBehavior(), "Run", keymap:GetRunKey():GetDisplayName())
        AddButtonToToolbar(61832, const:TOOLBAR_PLAY_BUTTON_COLOR, behaviors:GetThreadedDebugProjectBehavior(), "Debug", keymap:GetDebugKey():GetDisplayName())
        AddButtonToToolbar(61517, const:TOOLBAR_STOP_BUTTON_COLOR, behaviors:GetThreadedStopBehavior(), "Stop", keymap:GetStopKey():GetDisplayName())
        AddButtonToToolbar(61516, const:TOOLBAR_DEBUGGER_BUTTONS_COLOR, behaviors:GetThreadedPauseBehavior(), "Pause", keymap:GetPauseKey():GetDisplayName())
        AddButtonToToolbar(61520, const:TOOLBAR_DEBUGGER_BUTTONS_COLOR, behaviors:GetThreadedContinueBehavior(), "Continue", keymap:GetContinueKey():GetDisplayName())
        AddButtonToToolbar(61537, const:TOOLBAR_DEBUGGER_BUTTONS_COLOR, behaviors:GetThreadedStepOverBehavior(), "Step Over", keymap:GetStepOverKey():GetDisplayName())
        AddButtonToToolbar(61769, const:TOOLBAR_DEBUGGER_BUTTONS_COLOR, behaviors:GetThreadedStepIntoBehavior(), "Step Into", keymap:GetStepIntoKey():GetDisplayName())
        AddButtonToToolbar(61768, const:TOOLBAR_DEBUGGER_BUTTONS_COLOR, behaviors:GetThreadedStepOutBehavior(), "Step Out", keymap:GetStepOutKey():GetDisplayName())
        SetupButtonFocus()
    end

    action SetupButtonFocus
        i = 0
        repeat while i < buttons:GetSize()
            Button button = buttons:Get(i)
            if i + 1 < buttons:GetSize()
                Button next = buttons:Get(i+1)
                button:SetNextFocus(next)
                next:SetPreviousFocus(button)
            end
            i = i + 1
        end

        Button first = buttons:Get(0)
        Button last = buttons:Get(buttons:GetSize() - 1)
        first:SetPreviousFocus(last)
        last:SetNextFocus(first)

        SetNextFocus(first)
        SetPreviousFocus(last)
    end

    private action AddButtonToToolbar(integer unicode, text optionsKey, Behavior behavior, text name, text description)
        Button button
        button:SetFont(fontAwesome)
        button:SetFontSize(GetFontSize())
        text value = ""
        value = value:GetUnicodeValue(unicode)
        GameStateManager manager
        InterfaceOptions options = manager:GetInterfaceOptions()
        ColorGroup co = options:GetColorProperty(optionsKey)
        if co not= undefined
            Color co2 = cast(Color, co)
            button:SetFontColor(co2)
        end

        ColorGroup bg = options:GetColorProperty(const:TOOLBAR_BUTTON_BACKGROUND_COLOR)
        if bg not= undefined
            
            
            //button:SetBackgroundColor(color:Black())
        end
        
        button:SetBackgroundColor(transparent)
        button:SetBorderThickness(0)
        button:SetTopPadding(3)
        button:SetBottomPadding(1)
        button:SetLeftPadding(5)
        button:SetName(name)
        button:SetLabelText(value)
        button:SetDescription(description)

        if behavior not= undefined
            button:SetBehavior(behavior)
        end
        Add(button)
        buttons:Add(button)

        buttonMap:Add(name, button)
    end

    action GetButton(text name) returns Button
        if buttonMap:HasKey(name)
            return buttonMap:GetValue(name)
        else
            return undefined
        end
    end
end
