package Libraries.Development.Environment.Studio
use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Layouts.FillLayout
use Libraries.Containers.Array
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Layouts.Layout
use Libraries.System.File
use Libraries.Game.Graphics.Texture
use Libraries.Language.Errors.FileNotFoundError
use Libraries.Language.Errors.InputOutputError
use Libraries.Game.Graphics.Color
use Libraries.Containers.HashTable
use Libraries.Development.Environmen.Resources
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.TextureWrap
use Libraries.Game.Graphics.TextureRegion
use Libraries.Containers.Iterator

class OutputEditorTabPane is TabPane
    Color color
    HashTable<text, File> openFiles
    HashTable<text, Tab> openTabs
    Resources resources = undefined

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
    end

    action GetOpenFileIterator returns Iterator<File>
        return openFiles:GetValueIterator()
    end

    action Setup
        FillLayout fill
        fill:SetFillSide(fill:RIGHT)
        fill:SetPercentageY(1)
        fill:SetPercentageOriginY(1)
        fill:SetPercentageHeight(0.7)
        fill:SetOffsetX(5)

        SetName("Code Tabs")
        Array<Tab> array
        
        Initialize(200, 200, array)
        AddLayout(fill)
    end

    action OpenNewTab(File file)
        text contents = ""

        check
            contents = file:Read()
        detect error is FileNotFoundError
            // File wasn't found, handle the error gracefully.
            // Output is for testing purposes only.
            output "Couldn't find file: " + file:GetAbsolutePath()
            return now
        detect error is InputOutputError
            // File couldn't be read for some reason.
            // Output is for testing purposes only.
            output "Couldn't read file: " + file:GetAbsolutePath()
            error:OutputStackTrace()
            return now
        end

        Layout textBoxLayout
        textBoxLayout:SetPercentageX(0)
        textBoxLayout:SetPercentageY(0)
        textBoxLayout:SetPercentageWidth(1)
        textBoxLayout:SetPercentageHeight(1)

  

        // For testing purposes, just put in a placeholder icon.
        // Long-term, this should instead check the file extension and load an
        // appropriate image (e.g., Quorum icon for a .quorum file).
        Texture icon = resources:GetQuorumFile()

        Tab newTab
        newTab:Initialize(file:GetPath(), undefined, icon, true)
        boolean value = openFiles:HasKey(file:GetAbsolutePath())
        if value //it's already open
            Tab tab = openTabs:GetValue(file:GetAbsolutePath())
            //now we have to give that tab the focus
            tab:ReleasedMouse()
        else 
            openFiles:Add(file:GetAbsolutePath(), file)
            openTabs:Add(file:GetAbsolutePath(), newTab)
            Add(newTab)
            newTab:ReleasedMouse()
        end

        Resize()
    end
end