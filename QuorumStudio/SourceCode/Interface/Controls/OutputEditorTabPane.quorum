package Libraries.Development.Environment.Studio

use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Containers.Array
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Layouts.Layout
use Libraries.System.File
use Libraries.Game.Graphics.Texture
use Libraries.Language.Errors.FileNotFoundError
use Libraries.Language.Errors.InputOutputError
use Libraries.Game.Graphics.Color
use Libraries.Containers.HashTable
use Libraries.Development.Environment.Resources
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.TextureWrap
use Libraries.Game.Graphics.TextureRegion
use Libraries.Containers.Iterator
use Libraries.Development.Environment.Studio.Interface.ErrorTab
use Libraries.Development.Environment.Studio.Interface.OutputTab
use Libraries.Development.Environment.Studio.Interface.VariablesTab
use Libraries.Development.Environment.Studio.Interface.BreakpointsTab
use Libraries.Development.Environment.Studio.Interface.CallStackTab
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Interface.Controls.Button
use Libraries.Development.Environment.Studio.Behaviors.InputBehavior
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Events.FocusEvent
use Libraries.Interface.Selections.TabPaneSelection

class OutputEditorTabPane is TabPane
    Color color
    OutputTab outputTab
    VariablesTab variablesTab
    ErrorTab errorTab
    BreakpointsTab breakpointsTab
    CallStackTab callStackTab

    
    Resources resources = undefined
    Behaviors behaviors = undefined

    action GainedFocus(FocusEvent event)
        TabPaneSelection selection = GetSelection()
        Tab tab = selection:GetTab()
        if tab not= undefined
            Item2D value = tab:GetFocusTarget()
            if value not= undefined
                value:Focus()
            end
        end
    end

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action SetBehaviors(Behaviors behaviors)
        me:behaviors = behaviors
        breakpointsTab:SetBehaviors(behaviors)
        variablesTab:SetBehaviors(behaviors)
        errorTab:SetBehaviors(behaviors)
        callStackTab:SetBehaviors(behaviors)
    end

    on create
        FlowLayout layout
        SetViewAreaLayout(layout)

        SetName("Output Editor TabPane")
    end

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
        breakpointsTab:SetResources(resources)
        variablesTab:SetResources(resources)
    end

    action GetOutputTab returns OutputTab
        return outputTab
    end

    action GetVariablesTab returns VariablesTab
        return variablesTab
    end

    action GetErrorTab returns ErrorTab
        return errorTab
    end

    action GetBreakpointsTab returns BreakpointsTab
        return breakpointsTab
    end

    action GetCallStackTab returns CallStackTab
        return callStackTab
    end

    action Setup
        LayoutProperties properties = GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:STANDARD)
        properties:SetVerticalLayoutMode(properties:FILL)
        properties:SetPercentageWidth(1.0)
        properties:SetLeftPadding(3)
        properties:SetRightPadding(3)
        properties:SetTopPadding(4)

        breakpointsTab:SetResources(resources)
        variablesTab:SetResources(resources)
        

        SetName("Output Tabs")
        Array<Tab> array
        SetupTabs()
        array:Add(outputTab)
        array:Add(errorTab)
        array:Add(variablesTab)
        array:Add(breakpointsTab)
        array:Add(callStackTab)
        Add(array)

        Select(0)
    end

    action SetupTabs
        Control outputPanel
        LayoutProperties outputPanelLayout = outputPanel:GetDefaultLayoutProperties()
        outputPanelLayout:SetPercentageX(0)
        outputPanelLayout:SetPercentageY(0)
        outputPanelLayout:SetPercentageWidth(1)
        outputPanelLayout:SetPercentageHeight(1)

        FlowLayout flow
        outputPanel:SetLayout(flow)

        TextBox textBoxForConsoleOutput  
        textBoxForConsoleOutput:SetBackgroundColor(color:White())
        textBoxForConsoleOutput:SetText("Welcome to Quorum Studio!")
        textBoxForConsoleOutput:SetMoveFocusOnTab(true)
        textBoxForConsoleOutput:SetName("Console output")

        LayoutProperties textBoxPanelLayout = textBoxForConsoleOutput:GetDefaultLayoutProperties()
        textBoxPanelLayout:SetPercentageX(0)
        textBoxPanelLayout:SetPercentageY(0.2)
        textBoxPanelLayout:SetPercentageWidth(1)
        textBoxPanelLayout:SetPercentageHeight(0.8)

        TextBox inputTextBoxForOutputPane
        inputTextBoxForOutputPane:SetName("Input Text Box")

        LayoutProperties textBoxPanelLayoutForInput = inputTextBoxForOutputPane:GetDefaultLayoutProperties()
        textBoxPanelLayoutForInput:SetPercentageX(0)
        textBoxPanelLayoutForInput:SetPercentageY(0)
        textBoxPanelLayoutForInput:SetPercentageWidth(0.8)
        textBoxPanelLayoutForInput:SetPercentageHeight(0.2)
        textBoxPanelLayoutForInput:SetTopPadding(3)

        inputTextBoxForOutputPane:SetBackgroundColor(color:White())
        inputTextBoxForOutputPane:SetMoveFocusOnTab(true)
        InputBehavior behavior = behaviors:GetInputBehavior()
        
        Button sendButtonOutputPane
        LayoutProperties buttonProperties = sendButtonOutputPane:GetDefaultLayoutProperties()
        buttonProperties:SetHorizontalLayoutMode(buttonProperties:FILL)
        buttonProperties:SetVerticalLayoutMode(buttonProperties:FILL)
        buttonProperties:SetTopPadding(2)
        sendButtonOutputPane:SetName("Send")

        //Set the focus system up for these controls
        sendButtonOutputPane:SetNextFocus(textBoxForConsoleOutput)
        sendButtonOutputPane:SetPreviousFocus(inputTextBoxForOutputPane)
        inputTextBoxForOutputPane:SetNextFocus(sendButtonOutputPane)
        inputTextBoxForOutputPane:SetPreviousFocus(textBoxForConsoleOutput)
        textBoxForConsoleOutput:SetNextFocus(inputTextBoxForOutputPane)
        textBoxForConsoleOutput:SetPreviousFocus(sendButtonOutputPane)

        //setup some behaviors
        behavior:SetInputTextBox(inputTextBoxForOutputPane)
        sendButtonOutputPane:SetBehavior(behavior)

        outputPanel:Add(textBoxForConsoleOutput)
        outputPanel:Add(inputTextBoxForOutputPane)
        outputPanel:Add(sendButtonOutputPane)
        
        outputTab:SetTextBox(textBoxForConsoleOutput)
        outputTab:SetFocusTarget(textBoxForConsoleOutput)
        outputTab:SetName("Console")
        outputTab:SetRelatedItem(outputPanel)
        outputTab:DisplayCloseButton(true)
        
        errorTab:Setup()

        callStackTab:SetName("Call Stack")
        callStackTab:SetRelatedItem(undefined)
        callStackTab:DisplayCloseButton(true)
        callStackTab:Setup()

        breakpointsTab:Setup()
        variablesTab:Setup()
    end
end