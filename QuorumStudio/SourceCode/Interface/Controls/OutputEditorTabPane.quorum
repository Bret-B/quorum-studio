package Libraries.Development.Environment.Studio

use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Containers.Array
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Layouts.Layout
use Libraries.System.File
use Libraries.Game.Graphics.Texture
use Libraries.Language.Errors.FileNotFoundError
use Libraries.Language.Errors.InputOutputError
use Libraries.Game.Graphics.Color
use Libraries.Containers.HashTable
use Libraries.Development.Environment.Resources
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.TextureWrap
use Libraries.Game.Graphics.TextureRegion
use Libraries.Containers.Iterator
use Libraries.Development.Environment.Studio.Interface.ErrorTab
use Libraries.Development.Environment.Studio.Interface.OutputTab
use Libraries.Development.Environment.Studio.Interface.VariablesTab
use Libraries.Development.Environment.Studio.Interface.BreakpointsTab
use Libraries.Development.Environment.Studio.Interface.CallStackTab
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Interface.Controls.Button
use Libraries.Development.Environment.Studio.Behaviors.InputBehavior
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Events.FocusEvent
use Libraries.Interface.Selections.TabPaneSelection
use Libraries.Development.Environment.Studio.Interface.SearchResultsTab

class OutputEditorTabPane is TabPane
    Color color
    OutputTab outputTab
    VariablesTab variablesTab
    ErrorTab errorTab
    BreakpointsTab breakpointsTab
    CallStackTab callStackTab
    SearchResultsTab searchResultsTab
    Array<Tab> openTabs
    
    Resources resources = undefined
    Behaviors behaviors = undefined

    action GainedFocus(FocusEvent event)
        TabPaneSelection selection = GetSelection()
        Tab tab = selection:GetTab()
        if tab not= undefined
            Item2D value = tab:GetFocusTarget()
            if value not= undefined
                value:Focus()
            end
        end
    end

    action Remove(Tab tab)
        openTabs:Remove(tab)
        parent:TabPane:Remove(tab)
    end

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action SetBehaviors(Behaviors behaviors)
        me:behaviors = behaviors
        outputTab:SetBehaviors(behaviors)
        breakpointsTab:SetBehaviors(behaviors)
        variablesTab:SetBehaviors(behaviors)
        errorTab:SetBehaviors(behaviors)
        callStackTab:SetBehaviors(behaviors)
        searchResultsTab:SetBehaviors(behaviors)
    end

    on create
        FlowLayout layout
        SetViewAreaLayout(layout)

        SetName("Output Editor TabPane")
    end

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
        breakpointsTab:SetResources(resources)
        variablesTab:SetResources(resources)
    end

    action GetOutputTab returns OutputTab
        if not openTabs:Has(outputTab)
            Add(cast(Tab,outputTab))
            openTabs:Add(outputTab)
        end
        return outputTab
    end

    action GetVariablesTab returns VariablesTab
        if not openTabs:Has(variablesTab)
            Add(cast(Tab,variablesTab))
            openTabs:Add(variablesTab)
        end
        return variablesTab
    end

    action GetErrorTab returns ErrorTab
        if not openTabs:Has(errorTab)
            Add(cast(Tab,errorTab))
            openTabs:Add(errorTab)
        end
        return errorTab
    end

    action GetBreakpointsTab returns BreakpointsTab
        if not openTabs:Has(breakpointsTab)
            Add(cast(Tab,breakpointsTab))
            openTabs:Add(breakpointsTab)
        end
        return breakpointsTab
    end

    action GetCallStackTab returns CallStackTab
        if not openTabs:Has(callStackTab)
            Add(cast(Tab,callStackTab))
            openTabs:Add(callStackTab)
        end
        return callStackTab
    end

    action GetSearchResultsTab returns SearchResultsTab
        if not openTabs:Has(searchResultsTab)
            Add(cast(Tab,searchResultsTab))
            openTabs:Add(searchResultsTab)
        end
        return searchResultsTab  
    end
    
    action Setup
        LayoutProperties properties = GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:STANDARD)
        properties:SetVerticalLayoutMode(properties:FILL)
        properties:SetPercentageWidth(1.0)
        properties:SetLeftPadding(3)
        properties:SetRightPadding(3)
        properties:SetTopPadding(4)

        breakpointsTab:SetResources(resources)
        variablesTab:SetResources(resources)

        SetName("Output Tabs")
        Array<Tab> array
        SetupTabs()
        array:Add(outputTab)
        array:Add(errorTab)
        array:Add(variablesTab)
        array:Add(breakpointsTab)
        array:Add(callStackTab)
        array:Add(searchResultsTab)
        openTabs = array
        Add(array)

        Display(0)
    end

    action SetupTabs
        outputTab:Setup()
        errorTab:Setup()

        callStackTab:SetName("Call Stack")
        callStackTab:SetRelatedItem(undefined)
        callStackTab:DisplayCloseButton(true)
        callStackTab:Setup()

        breakpointsTab:Setup()
        variablesTab:Setup()
        searchResultsTab:Setup()
    end
end