package Libraries.Development.Environment.Studio.Interface

use Libraries.Interface.Controls.Tab
use Libraries.Development.Environment.Studio.Build.BuildManager
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.Tree
use Libraries.Development.Environment.Resources
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Language.Debug.Debugger
use Libraries.Language.Debug.VariablesModel
use Libraries.Containers.Array
use Libraries.Language.Debug.Variable
use Libraries.Interface.Controls.TreeItem
use Libraries.Interface.Events.TreeChangeListener
use Libraries.Interface.Events.TreeChangeEvent
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Controls.TreeTable
use Libraries.Language.Debug.VariableTypeColumn
use Libraries.Language.Debug.VariableNameColumn
use Libraries.Language.Debug.VariableValueColumn
use Libraries.Interface.Controls.TreeTableColumn
use Libraries.Language.Debug.VariableRow
use Libraries.Interface.Events.FocusEvent
use Libraries.Interface.Selections.TreeTableSelection
use Libraries.Interface.Controls.TreeTableCell

class VariablesTab is Tab, TreeChangeListener
    BuildManager manager = undefined
    Resources resources = undefined
    Behaviors behaviors = undefined
    Debugger debugger = undefined
    BreakpointScrollPane pane
    TreeTable variablesTree
    boolean updateDebuggerInformation = false
    VariableNameColumn nameColumn
    VariableTypeColumn typeColumn
    VariableValueColumn valueColumn
    VariableRow titleRow

    on create
        nameColumn:SetPixelWidth(0)
        nameColumn:SetPercentageWidth(0.333)

        typeColumn:SetPixelWidth(0)
        typeColumn:SetPercentageWidth(0.333)

        valueColumn:SetPixelWidth(0)
        valueColumn:SetPercentageWidth(0.334)

        variablesTree:Add(cast(TreeTableColumn, nameColumn))
        variablesTree:Add(cast(TreeTableColumn, typeColumn))
        variablesTree:Add(cast(TreeTableColumn, valueColumn))

        titleRow:SetName("Name")
        titleRow:SetType("Type")
        titleRow:SetValue("Value")
        variablesTree:Add(titleRow)
    end

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action GetDebuggerTreeTable returns TreeTable
        return variablesTree
    end

    action SetBehaviors(Behaviors behaviors)
        me:behaviors = behaviors
        manager = behaviors:GetBuildManager()
        debugger = manager:GetDebugger()
    end

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
    end

    action GetBuildManager returns BuildManager
        return manager
    end

    action SetBuildManager(BuildManager build)
        me:manager = build
    end


    action GainedFocus(FocusEvent event)
        TreeTableSelection selection = variablesTree:GetSelection()
        if selection:IsEmpty() and variablesTree:GetColumnsSize() > 0 
            and variablesTree:GetRows() not= undefined and variablesTree:GetRows():GetSize() > 0
        
            TreeTableCell cell = variablesTree:GetCell(0,0)
            variablesTree:Select(cell)
            cell:Focus()
        end
    end

    action Setup
        LayoutProperties properties = variablesTree:GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:STANDARD)
        properties:SetPercentageWidth(1)
        pane:SetBackgroundColor(resources:GetBackgroundColor())
        pane:Add(variablesTree)
        pane:Resize()

        SetName("Variables")
        SetRelatedItem(GetPane())
        DisplayCloseButton(true)
        SetFocusTarget(variablesTree)
    end

    action GetPane returns BreakpointScrollPane
        return pane
    end
end