package Libraries.Development.Environment.Studio
use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Layouts.FillLayout
use Libraries.Containers.Array
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Layouts.Layout
use Libraries.System.File
use Libraries.Game.Graphics.Texture
use Libraries.Language.Errors.FileNotFoundError
use Libraries.Language.Errors.InputOutputError
use Libraries.Game.Graphics.Color
use Libraries.Containers.HashTable
use Libraries.Development.Environmen.Resources

class CodeEditorTabPane is TabPane
    Color color
    HashTable<text, File> openFiles
    Resources resources = undefined

    action GetResources returns Resources
        return resources
    end

    action SetResources(Resources resources)
        me:resources = resources
    end

    action Setup
        FillLayout fill
        fill:SetFillSide(fill:RIGHT)
        fill:SetPercentageY(1)
        fill:SetPercentageOriginY(1)
        fill:SetPercentageHeight(0.7)
        fill:SetOffsetX(5)

        SetName("Code Tabs")
        Array<Tab> array
        TextBox testTextBox
        testTextBox:Initialize(100, 100, resources:GetBackgroundColor())
        Layout testTextBoxLayout
        testTextBoxLayout:SetPercentageX(0)
        testTextBoxLayout:SetPercentageY(0)
        testTextBoxLayout:SetPercentageWidth(1)
        testTextBoxLayout:SetPercentageHeight(1)
        testTextBox:AddLayout(testTextBoxLayout)
        
        Initialize(200, 200, array)
        AddLayout(fill)
    end

    action OpenNewTab(File file)
        text contents = ""

        check
            contents = file:Read()
        detect error is FileNotFoundError
            // File wasn't found, handle the error gracefully.
            // Output is for testing purposes only.
            output "Couldn't find file: " + file:GetAbsolutePath()
            return now
        detect error is InputOutputError
            // File couldn't be read for some reason.
            // Output is for testing purposes only.
            output "Couldn't read file: " + file:GetAbsolutePath()
            error:OutputStackTrace()
            return now
        end

        Layout textBoxLayout
        textBoxLayout:SetPercentageX(0)
        textBoxLayout:SetPercentageY(0)
        textBoxLayout:SetPercentageWidth(1)
        textBoxLayout:SetPercentageHeight(1)

        TextBox textBox
        
        textBox:Initialize(400, 400, color:CustomColor(1, 1, 1, 1), contents)
        textBox:AddLayout(textBoxLayout)    

        // For testing purposes, just put in a placeholder icon.
        // Long-term, this should instead check the file extension and load an
        // appropriate image (e.g., Quorum icon for a .quorum file).
        Texture icon = resources:GetQuorumFile()

        Tab newTab
        newTab:Initialize(file:GetPath(), textBox, icon, true)
        Add(newTab)
        
        Resize()
    end
end