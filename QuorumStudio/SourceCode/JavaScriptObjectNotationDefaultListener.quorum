package Libraries.Data.Formats
use Libraries.Containers.Stack

class JavaScriptObjectNotationDefaultListener is JavaScriptObjectNotationListener
    JavaScriptObjectNotationObject object = undefined
    Stack<JavaScriptObjectNotationObject> objects
    Stack<integer> opStack
    constant integer inObject = 0
    constant integer inArray = 1
    constant integer inPair = 2
    constant integer inValue = 3

    action GetObject returns JavaScriptObjectNotationObject
        return object
    end

    action EnterStart
    end

    action ExitStart
    end

    action EnterObject
        JavaScriptObjectNotationObject newObject
        
        objects:Push(newObject)
        opStack:Push(inObject)
        if object = undefined
            object = newObject
        else 
            object:Add(newObject)
        end
    end

    action ExitObject
        objects:Pop()
        opStack:Pop()
    end

    action EnterArray
        opStack:Push(inArray)
    end

    action ExitArray
        opStack:Pop()
    end

    action EnterPair
        JavaScriptObjectNotationObject newObject
        opStack:Push(inPair)
        objects:Push(newObject)
        object:Add(newObject)
    end

    action ExitPair
        opStack:Pop()
        objects:Pop()
    end

    action EnterValue
        opStack:Push(inValue)
    end

    action ExitValue
        opStack:Pop()
    end

    action VisitString(JavaScriptObjectNotationTerminal terminal)
        integer op = opStack:Peek()
        JavaScriptObjectNotationObject obj = objects:Peek()
        JavaScriptObjectNotationToken token = terminal:GetToken()
        if op = inPair
            obj:SetKey(token:value)
        elseif op = inValue
            obj:SetText(token:value)
        end
    end

    action VisitNumber(JavaScriptObjectNotationTerminal terminal)
        integer op = opStack:Peek()
        JavaScriptObjectNotationObject obj = objects:Peek()
        JavaScriptObjectNotationToken token = terminal:GetToken()
        if op = inPair
            obj:SetKey(token:value)
        elseif op = inValue
            number val = cast(number, token:value)
            obj:SetNumber(val)
        end
    end

    action VisitTrue(JavaScriptObjectNotationTerminal terminal)
        integer op = opStack:Peek()
        JavaScriptObjectNotationObject obj = objects:Peek()
        JavaScriptObjectNotationToken token = terminal:GetToken()
        if op = inPair
            obj:SetKey(token:value)
        elseif op = inValue
            obj:SetBoolean(true)
        end
    end

    action VisitFalse(JavaScriptObjectNotationTerminal terminal)
        integer op = opStack:Peek()
        JavaScriptObjectNotationObject obj = objects:Peek()
        JavaScriptObjectNotationToken token = terminal:GetToken()
        if op = inPair
            obj:SetKey(token:value)
        elseif op = inValue
            obj:SetBoolean(false)
        end
    end

    action VisitNull(JavaScriptObjectNotationTerminal terminal)
        integer op = opStack:Peek()
        JavaScriptObjectNotationObject obj = objects:Peek()
        JavaScriptObjectNotationToken token = terminal:GetToken()
        if op = inPair
            obj:SetKey(token:value)
        elseif op = inValue
            obj:SetNull()
        end
    end

    action VisitError(JavaScriptObjectNotationError error)
    end
end