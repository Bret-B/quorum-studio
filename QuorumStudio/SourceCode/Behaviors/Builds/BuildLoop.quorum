package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.Build.BuildManager
use Libraries.System.SystemHelper
use Libraries.System.DateTime
use Libraries.System.Logging.SharedLogger

class BuildLoop is Behavior
    SharedLogger logger
    on create
        SetRunOnUpdate(true)
    end

    BuildManager manager = undefined
    boolean finished = false
    DateTime time

    action SetBuildManager(BuildManager bm)
        manager = bm
    end

    action GetBuildManager returns BuildManager
        return manager
    end

    action IsFinished returns boolean
        return finished
    end

    action SetFinished(boolean finished)
        me:finished = finished
    end

    action Run(BehaviorEvent event)
        
        SystemHelper help
        if manager = undefined
            return now
        end

        repeat until finished
            check
                boolean indexed = manager:Index()
                number value = manager:GetParseDelayInSeconds()
                integer delay = cast(integer, value * 100)
                help:Sleep(delay)
            detect e
                text value = e:GetStackTraceMessage()
                logger:Log(value)
            end
        end
    end
end