package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.Build.BuildManager
use Libraries.System.SystemHelper
use Libraries.System.DateTime
use Libraries.Development.Environment.Studio.Interface.ErrorTab
use Libraries.Development.Environment.Studio.OutputEditorTabPane
use Libraries.Language.Compile.CompilerErrorManager
use Libraries.Containers.Iterator
use Libraries.Language.Compile.CompilerError
use Libraries.Development.Environment.Studio.Interface.OutputTab
use Libraries.Interface.Accessibility
use Libraries.Game.GameStateManager
use Libraries.System.Properties

class SetErrorsBehavior is Behavior

    on create
        SetRunOnUpdate(true)
    end

    ErrorTab errorTab = undefined
    OutputEditorTabPane outputTabs = undefined
    CompilerErrorManager errors = undefined
    OutputTab out = undefined
    text message = ""
    boolean finished = false
    GameStateManager manager

    action SetMessage(text bm)
        message = bm
    end

    action GetMessage returns text
        return message
    end

    action SetOutputTab(OutputTab bm)
        out = bm
    end

    action GetOutputTab returns OutputTab
        return out
    end

    action SetErrorTab(ErrorTab bm)
        errorTab = bm
    end

    action GetErrorTab returns ErrorTab
        return errorTab
    end

    action SetOutputEditorTabPane(OutputEditorTabPane bm)
        outputTabs = bm
    end

    action GetOutputEditorTabPane returns OutputEditorTabPane
        return outputTabs
    end

    action SetCompilerErrorManager(CompilerErrorManager bm)
        errors = bm
    end

    action GetCompilerErrorManager returns CompilerErrorManager
        return errors
    end

    action Update(number seconds)
        if not finished

            Accessibility accessibility = manager:GetAccessibility()

            if errors:IsCompilationErrorFree()
                out:Append(message)
                errorTab:Empty()
                errorTab:Resize()
                outputTabs:Select(out, false)

                if accessibility not= undefined
                    accessibility:Notify(manager:GetFocus(), message)
                end
            else
                errorTab:Empty()
                integer amount = errors:GetErrorAmount()
                text errorMessage = ""
                if amount = 1
                    errorMessage = "I found an error."
                else
                    errorMessage = "I found " + errors:GetErrorAmount() + 
                        " errors."
                end

                boolean first = true
                Iterator<CompilerError> iterator = errors:GetIterator()
                repeat while iterator:HasNext()
                    CompilerError error = iterator:Next()
                    if first and error not= undefined
                        integer line = error:GetLineNumber()
                        text message = error:GetErrorMessage()
                        errorMessage = errorMessage + " Line " + line + ", " + message
                        first = false
                    end
                    errorTab:AddError(error)
                end

                Properties prop
                if prop:IsWindows()
                    errorMessage = errorMessage + " Press control 4 to go to the errors tab." +
                    " or control G to go to the line in the editor."
                else
                    errorMessage = errorMessage + " Press command 4 to go to the errors tab " +
                    " or command G to go to the line in the editor."
                end

                out:Append(errorMessage)
                errorTab:Resize()
                outputTabs:Select(errorTab, false)

                if accessibility not= undefined
                    accessibility:Notify(manager:GetFocus(), errorMessage)
                end
            end
            finished = true
        end
    end
    
    action IsFinished returns boolean
        return finished
    end
end