package Libraries.Development.Environment.Studio.Behaviors
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Events.DragAndDropEvent
use Libraries.Interface.Item
use Libraries.Interface.Item2D
use Libraries.Development.Environment.Studio.Interface.VisualEditor.VisualSceneController
use Libraries.Development.Environment.Studio.Interface.VisualEditor.EditorLayer
use Libraries.Interface.Item3D
use Libraries.Development.Environment.Studio.Interface.VisualEditor.EditorLayer2D
use Libraries.Development.Environment.Studio.Interface.VisualEditor.ScenePaletteItem
use Libraries.Development.Environment.Studio.Interface.VisualEditor.ItemBlueprint
use Libraries.Development.Environment.Studio.Interface.VisualEditor.DrawableBlueprint
use Libraries.Development.Environment.Studio.Interface.VisualEditor.NewItemPreview2D
use Libraries.Game.Graphics.Drawable
use Libraries.Containers.Array
use Libraries.Compute.Vector3

class PaletteItemDropBehavior is QuorumStudioBehavior

    action Run(BehaviorEvent event)
        DragAndDropEvent dragEvent = event:GetDragAndDropEvent()

        VisualSceneController controller = undefined
        Item destination = dragEvent:GetDestination()
        if destination is VisualSceneController
            controller = cast(VisualSceneController, destination)
        elseif destination not= undefined
            EditorLayer layer = undefined
            if destination is Item2D
                Item2D item = cast(Item2D, destination)
                if item:GetLayer() is EditorLayer
                    layer = cast(EditorLayer2D, item:GetLayer())
                end
            elseif destination is Item3D
                // TO-DO
            end

            if layer not= undefined
                controller = layer:GetSceneController()
            end
        end

        if controller = undefined
            return now
        end

        Item2D item = undefined
        if dragEvent:GetSource() is Item2D
            item = cast(Item2D, dragEvent:GetSource())
            repeat until item = undefined or item is ScenePaletteItem
                item = item:GetParent()
            end
        end

        if item = undefined
            return now
        end

        ScenePaletteItem paletteItem = cast(ScenePaletteItem, item)
        ItemBlueprint schematic = paletteItem:GetItemBlueprint()
        if schematic is DrawableBlueprint
            EditorLayer layer = controller:GetSelectedLayer()
            if layer is EditorLayer2D = false
                Array<EditorLayer> array = controller:GetSceneLayers()
                integer i = array:GetSize()
                boolean found = false
                repeat while i > 0 and found = false
                    i = i - 1
                    if array:Get(i) is EditorLayer2D
                        layer = array:Get(i)
                        found = true
                    end
                end

                if not found
                    return now
                end
            end

            NewItemPreview2D preview
            preview:Load(controller, cast(DrawableBlueprint, schematic))
            Drawable drawable = preview:ConvertToDrawable()

            Vector3 worldCoordinates = layer:GetCamera():ScreenToWorldCoordinates(dragEvent:GetX() - layer:GetViewportX(), dragEvent:GetY() - layer:GetViewportY())
            number x = layer:GetPreviousGridCoordinate(worldCoordinates:GetX())
            number y = layer:GetPreviousGridCoordinate(worldCoordinates:GetY())
            drawable:SetPosition(x, y)

            controller:Add(layer, drawable)
            controller:IncrementNameCount(schematic:GetName())
        else
            // TO-DO
        end
    end

end