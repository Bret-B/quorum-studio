package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Events.DragAndDropEvent
use Libraries.Interface.Item2D
use Libraries.Development.Environment.Studio.Interface.VisualEditor.ScenePaletteItem
use Libraries.Development.Environment.Studio.Interface.VisualEditor.NewItemPreview2D
use Libraries.Game.Graphics.Drawable
use Libraries.Compute.Vector3
use Libraries.Game.Layer2D
use Libraries.Development.Environment.Studio.Interface.VisualEditor.DrawableBlueprint
use Libraries.Development.Environment.Studio.Interface.VisualEditor.ScenePalette
use Libraries.Interface.Controls.Scenes.Blueprints.ItemBlueprint

class PaletteItemStartDragBehavior is QuorumStudioBehavior

    action Run(BehaviorEvent event)
        DragAndDropEvent dragEvent = event:GetDragAndDropEvent()

        Item2D item = undefined
        if dragEvent:GetSource() is Item2D
            item = cast(Item2D, dragEvent:GetSource())
            repeat until item = undefined or item is ScenePaletteItem
                item = item:GetParent()
            end
        end

        if item = undefined
            return now
        end
        
        ScenePaletteItem paletteItem = cast(ScenePaletteItem, item)
        ScenePalette palette = paletteItem:GetPalette()
        ItemBlueprint schematic = paletteItem:GetItemBlueprint()
        if schematic is DrawableBlueprint
            NewItemPreview2D preview
            preview:Load(palette:GetSceneController(), cast(DrawableBlueprint, schematic))
            Drawable drawable = preview:ConvertToDrawable()

            Layer2D layer = item:GetLayer()
            Vector3 worldCoordinates = layer:GetCamera():ScreenToWorldCoordinates(dragEvent:GetX() - layer:GetViewportX(), dragEvent:GetY() - layer:GetViewportY())

            drawable:SetPosition(worldCoordinates:GetX() - drawable:GetWidth() / 2, worldCoordinates:GetY() - drawable:GetHeight() / 2)
            palette:SetDraggedItem(drawable)
        else
            // TO-DO
        end
    end

end