package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.Interface.VisualEditor.InteractableItem
use Libraries.Compute.Vector3
use Libraries.Development.Environment.Studio.Interface.VisualEditor.InteractableItem3D
use Libraries.Development.Environment.Studio.Interface.VisualEditor.InteractableItem2D

class SelectionScaleBehavior is QuorumStudioBehavior

    Vector3 scale

    action SetScale(number x, number y, number z)
        scale:Set(x, y, z)
    end

    action SetScale(Vector3 value)
        scale:Set(value)
    end

    action GetScale returns Vector3
        return scale
    end

    action Run(BehaviorEvent event)
        if event:GetItem() is InteractableItem3D
            InteractableItem3D item = cast(InteractableItem3D, event:GetItem())
            item:Scale(scale)

            // Temporary output while developing.
            if (scale:GetX() > 1 and scale:GetY() > 1 and scale:GetZ() > 1)
                item:NotifyAccessibility("Scaled up")
            elseif (scale:GetX() < 1 and scale:GetY() < 1 and scale:GetZ() < 1)
                item:NotifyAccessibility("Scaled down")
            elseif scale:GetX() > 1
                item:NotifyAccessibility("Scaled X up")
            elseif scale:GetX() < 1
                item:NotifyAccessibility("Scaled X down")
            elseif scale:GetY() > 1
                item:NotifyAccessibility("Scaled Y up")
            elseif scale:GetY() < 1
                item:NotifyAccessibility("Scaled Y down")
            elseif scale:GetZ() > 1
                item:NotifyAccessibility("Scaled Z up")
            elseif scale:GetZ() < 1
                item:NotifyAccessibility("Scaled Z down")
            end
        elseif event:GetItem() is InteractableItem2D
            InteractableItem2D item = cast(InteractableItem2D, event:GetItem())
            item:Scale(scale:GetX(), scale:GetY())

            // Temporary output while developing.
            if (scale:GetX() > 1 and scale:GetY() > 1)
                item:NotifyAccessibility("Scaled up")
            elseif (scale:GetX() < 1 and scale:GetY() < 1)
                item:NotifyAccessibility("Scaled down")
            elseif scale:GetX() > 1
                item:NotifyAccessibility("Scaled X up")
            elseif scale:GetX() < 1
                item:NotifyAccessibility("Scaled X down")
            elseif scale:GetY() > 1
                item:NotifyAccessibility("Scaled Y up")
            elseif scale:GetY() < 1
                item:NotifyAccessibility("Scaled Y down")
            end
        end
    end
end