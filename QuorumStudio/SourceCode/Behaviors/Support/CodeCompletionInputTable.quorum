package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Game.InputTable
use Libraries.Game.InputSet
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.Interface.CodeCompletionMenu
use Libraries.Development.Environment.Studio.Interface.CodeTextBox
use Libraries.Interface.Controls.Blocks.CodeEditor

class CodeCompletionInputTable is InputTable

    on create
        SetIdentifier("CodeCompletionMenuRoot")
    end

    action HasKey(InputSet set) returns boolean
        return parent:InputTable:HasKey(set)
    end

    action ProcessInput(InputSet set, BehaviorEvent event) returns boolean
        boolean success = false
        if parent:InputTable:ProcessInput(set, event) = true
            return true
        elseif event:GetItem() is CodeCompletionMenu
            CodeCompletionMenu menu = cast(CodeCompletionMenu, event:GetItem())
            CodeTextBox textBox = menu:GetTextBox()
            CodeEditor editor = menu:GetCodeEditor()
            InputTable table = undefined
            if textBox not= undefined
                table = textBox:GetInputTable()
                event:SetItem(textBox)
            elseif editor not= undefined
                table = editor:GetInputTable()
                event:SetItem(editor)
            else
                return false
            end

            success = table:ProcessInput(set, event)

            if success
                // Do extra work here to see if we need to update the code completion filter or close the menu
                menu:Filter(event)
                if menu:IsCompletionItemsEmpty() and not menu:IsParsing()
                    menu:Close()
                    if textBox not= undefined
                        textBox:Focus()
                    elseif editor not= undefined
                        editor:Focus()
                    end
                end
            end

            // Reset the BehaviorEvent to use the original item again.
            event:SetItem(menu)
        end
        
        return success
    end
end