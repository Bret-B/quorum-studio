package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.Interface.ProjectPropertiesDialog
use Libraries.Interface.Controls.List
use Libraries.Interface.Controls.FileChooser
use Libraries.Interface.Controls.FileFilter
use Libraries.Development.Environment.Projects.Quorum.QuorumProject
use Libraries.System.File
use Libraries.Containers.Array
use Libraries.Development.Environment.Projects.Quorum.ImageSheet
use Libraries.Interface.Selections.ListSelection
use Libraries.Interface.Controls.ListItem

class ProjectPropertiesAddImageFolderBehavior is QuorumStudioBehavior   
    ProjectPropertiesDialog dialog = undefined
    List imageList = undefined
    List imageSheetsList = undefined

    action SetDialog(ProjectPropertiesDialog dialog)
        me:dialog = dialog
    end

    action GetDialog returns ProjectPropertiesDialog
        return dialog
    end

    action SetImageSheetsList(List list)
        me:imageSheetsList = list
    end
    
    action GetImageSheetsList returns List
        return me:imageSheetsList
    end

    action Run(BehaviorEvent event)    
        ListSelection sheetSelection = imageSheetsList:GetSelection() 
        integer sheetIndex = sheetSelection:GetIndex()

        imageList = me:dialog:GetCurrentImageList(sheetIndex)            
        FileChooser chooser

        QuorumProject project = dialog:GetProject()
        File imageFolder = chooser:ChooseFolderDialog(project:GetLocation())

        if imageFolder:IsDirectory()
            Array<File> images = imageFolder:GetDirectoryListing()
            integer i = 0
            repeat while i < images:GetSize()
                imageList:Add(images:Get(i):GetAbsolutePath())
                i = i + 1
            end
        end

        me:dialog:AddToImagesListArray(sheetIndex, imageList)
        me:dialog:SetCurrentImageList(sheetIndex)        
    end
end