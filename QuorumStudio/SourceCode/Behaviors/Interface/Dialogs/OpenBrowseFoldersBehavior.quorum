package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.Interface.NewFileDialog
use Libraries.Development.Environment.Projects.Quorum.QuorumProject
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Development.Environment.Projects.ProjectTree
use Libraries.Interface.Selections.TreeSelection
use Libraries.Containers.Array
use Libraries.Development.Environment.Projects.ProjectTreeItem
use Libraries.Interface.Controls.TreeItem
use Libraries.Interface.Controls.FileChooser
use Libraries.Interface.Controls.FileFilter
use Libraries.System.File
use Libraries.Development.Environment.Projects.ProjectView
use Libraries.Game.GameStateManager


class OpenBrowseFoldersBehavior is QuorumStudioBehavior 
    GameStateManager manager 
    text os = manager:GetOperatingSystem()
    NewFileDialog dialog = undefined
    text folderName = undefined

    action SetDialog(NewFileDialog dialog)
        me:dialog = dialog
    end

    action GetDialog returns NewFileDialog
        return dialog
    end

    action SetFolderName (text folderName)
        me:folderName = folderName
    end

    action GetFolderName returns text
        return me:folderName
    end

    action Run(BehaviorEvent event)
        FileChooser chooser

        QuorumProject project = dialog:GetProject()
        ProjectView projectView = project:GetProjectView()
        TreeItem treeItem = projectView:GetLogicalTree()

        File location = project:GetLocation()     
        File folder = chooser:ChooseFolderDialog(location)

        //only update the folder name if the path is within the path of the project
        if folder not= undefined and folder:GetAbsolutePath():Contains(location:GetAbsolutePath())
            if os:Contains("OS X")
                if folder:GetAbsolutePath():Equals(location:GetAbsolutePath())
                    me:folderName = project:PROJECT_SOURCES + "/"
                else
                    me:folderName = folder:GetAbsolutePath():Replace(location:GetAbsolutePath() + "/", "") + "/"
                end
            else
                if folder:GetAbsolutePath():Equals(location:GetAbsolutePath())
                    me:folderName = project:PROJECT_SOURCES + "\"
                else
                    me:folderName = folder:GetAbsolutePath():Replace(location:GetAbsolutePath() + "\", "") + "\"
                end                
            end
            dialog:UpdateFolderName(folderName)
        end

        
    end
end
