package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Development.Environment.Studio.Behaviors.QuorumStudioBehavior
use Libraries.Development.Environment.Studio.Interface.FindDialog
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Game.GameStateManager
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Development.Environment.Studio.CodeEditorTabPane
use Libraries.Interface.Item2D
use Libraries.Development.Environment.Studio.Interface.CodeTextBox
use Libraries.Interface.Controls.Blocks.CodeEditor

class OpenFindDialogBehavior is QuorumStudioBehavior 
    FindDialog dialog = undefined
    GameStateManager manager

    action Run(BehaviorEvent event)
        if dialog = undefined
            FindDialog find
            dialog = find
            dialog:Initialize()
        end

        QuorumStudio studio = cast(QuorumStudio, manager:GetGame())
        CodeEditorTabPane tabPane = studio:GetTabPane()
        Item2D tabbedItem = tabPane:GetCurrentItem()

        if tabbedItem is CodeTextBox
            CodeTextBox textBox = cast(CodeTextBox, tabbedItem)
            if not textBox:GetIsDialogOpen()
                dialog:SetTextBox(textBox)
                dialog:Show()
            else
                dialog:ResetFindFocus()
            end
        elseif tabbedItem is CodeEditor
            CodeEditor editor = cast(CodeEditor, tabbedItem)
            dialog:SetCodeEditor(editor)
            dialog:Show()
        end
    end
end