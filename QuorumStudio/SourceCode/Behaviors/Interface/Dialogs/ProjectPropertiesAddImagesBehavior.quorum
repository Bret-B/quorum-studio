package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.Interface.ProjectPropertiesDialog
use Libraries.Interface.Controls.List
use Libraries.Interface.Controls.FileChooser
use Libraries.Interface.Controls.FileFilter
use Libraries.Development.Environment.Projects.Quorum.QuorumProject
use Libraries.System.File
use Libraries.Containers.Array
use Libraries.Development.Environment.Projects.Quorum.ImageSheet
use Libraries.Interface.Selections.ListSelection
use Libraries.Interface.Controls.ListItem

class ProjectPropertiesAddImagesBehavior is QuorumStudioBehavior   
    ProjectPropertiesDialog dialog = undefined
    List imageList = undefined
    List imageSheetsList = undefined

    action SetDialog(ProjectPropertiesDialog dialog)
        me:dialog = dialog
    end

    action GetDialog returns ProjectPropertiesDialog
        return dialog
    end

    action SetImageSheetsList(List list)
        me:imageSheetsList = list
    end
    
    action GetImageSheetsList returns List
        return me:imageSheetsList
    end

    
    action Run(BehaviorEvent event)  
        /* if there have been no image sheets created to store the images to, then 
        a default assets image sheet is created for the user. This could be replaced
        to not allow users to click add image button unless an image sheet exists and
        is selected. */
        if imageSheetsList:IsEmpty()
            me:dialog:AddNewImageSheet("assets")
            me:dialog:ToggleEnableImageSheets()
            imageSheetsList:Select(0)
        end
        
        ListSelection sheetSelection = imageSheetsList:GetSelection()
        /* adds the images to the currently selected imageSheet, if no imageSheet is
        selected then the image is added to the first imageSheet in the list by default 
        this could be changed to not allow users to click add image button unless if 
        an image sheet has been selected */
        integer sheetIndex = 0
        if sheetSelection not= undefined
            sheetIndex = sheetSelection:GetIndex()
            if sheetIndex < 0
                sheetIndex = 0
            end
        end

        imageList = me:dialog:GetCurrentImageList(sheetIndex)            
        FileChooser chooser
        FileFilter filter
        filter:Empty()
        filter:Add("png")
        filter:Add("jpg")
        filter:Add("jpeg")        
        filter:SetAcceptAll(false)

        QuorumProject project = dialog:GetProject()
        File image = chooser:OpenFileDialog(project:GetLocation(), filter)

        if image not= undefined
            imageList:Add(image:GetAbsolutePath())                      
        end

        me:dialog:AddToImagesListArray(sheetIndex, imageList)
        me:dialog:SetCurrentImageList(sheetIndex)        
    end
end