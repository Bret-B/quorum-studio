package Libraries.Development.Environment.Studio.Behaviors
use Libraries.Development.Environment.Studio.Interface.NewProjectDialog
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Controls.Button
use Libraries.Development.Environment.Projects.Quorum.QuorumProject
use Libraries.System.File
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Development.Environment.Projects.ProjectManager
use Libraries.Development.Environment.Studio.Interface.ErrorDialog


class NewProjectOkBehavior is QuorumStudioBehavior

    QuorumProject project
    NewProjectDialog dialog = undefined

    action SetDialog(NewProjectDialog dialog)
        me:dialog = dialog
    end

    action GetDialog returns NewProjectDialog
        return me:dialog
    end

    action Run(BehaviorEvent event)
        File file
        
        if me:dialog:GetName() = ""
            me:dialog:ActivateCancelButton()
        elseif me:dialog:GetLocation() = ""
            me:dialog:ActivateCancelButton()
        else

            me:project = me:dialog:CreateNewProject()
            if me:project = undefined
                //This means there was an error
                ErrorDialog eDialog
                dialog:ActivateCancelButton()
                eDialog:Show()
                return now
            end
            file = me:project:GetPropertiesLocation()
            me:project:LoadProperties(file)
            me:project:SetName(me:dialog:GetProjectName())
            project:SaveProperties(file)
            
            
            QuorumStudio studio = GetMainApplication()
            ProjectManager manager = studio:GetProjectManager()
            manager:Open(file)

            me:dialog:Hide()
        end
    end
end