package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Development.Environment.Projects.ProjectManager
use Libraries.Development.Environment.Projects.Project
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Controls.TextBox
use Libraries.Development.Environment.Studio.OutputEditorTabPane
use Libraries.Development.Environment.Projects.Quorum.QuorumProject
use Libraries.Development.Environment.Studio.Interface.OutputTab
use Libraries.Development.Environment.Studio.Build.BuildManager
use Libraries.Development.Environment.Projects.ProjectTree
use Libraries.Interface.Selections.TreeSelection
use Libraries.Containers.Array
use Libraries.Interface.Controls.TreeItem
use Libraries.Development.Environment.Projects.ProjectTreeItem
use Libraries.Development.Environment.Studio.Build.BuildInformation
use Libraries.Game.GameStateManager
use Libraries.Game.Game

class InputBehavior is QuorumStudioBehavior
    BuildManager builder = undefined
    TextBox textbox = undefined
    GameStateManager manager
    action GetInputTextBox returns TextBox
        return textbox
    end

    action SetInputTextBox(TextBox textbox)
        me:textbox = textbox
    end

    action GetBuildManager returns BuildManager
        return builder
    end

    action SetBuildManager(BuildManager builder)
        me:builder = builder
    end

    action Run(BehaviorEvent event)
        Game game = manager:GetGame()
        QuorumStudio studio = cast(QuorumStudio, game)
        OutputEditorTabPane outPane = studio:GetOutputPane()
        OutputTab out = outPane:GetOutputTab()

        ProjectTree tree = studio:GetProjectTree()
        TreeSelection selection = tree:GetSelection()
        if not selection:IsEmpty() and textbox not= undefined
            Array<TreeItem> items = selection:GetPath()
            ProjectTreeItem value = cast(ProjectTreeItem, items:Get(0))
            QuorumProject project = value:GetProject()

            BuildInformation info
            info:outputTab = out
            info:project = project
            
            builder:AddInputToProcess(info, textbox:GetText())
        end
    end
end