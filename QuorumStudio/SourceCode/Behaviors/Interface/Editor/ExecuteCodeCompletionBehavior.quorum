package Libraries.Development.Environment.Studio.Behaviors
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Language.Compile.Hints.Hint
use Libraries.Development.Environment.Studio.Interface.CodeTextBox
use Libraries.Language.Compile.CodeCompletionItem
use Libraries.Interface.Controls.TextBox
use Libraries.Language.Compile.CodeCompletionResult
use Libraries.Development.Environment.Studio.Interface.CodeCompletionMenu
use Libraries.Development.Environment.Studio.Build.BuildManager
use Libraries.Interface.Controls.Blocks.CodeEditor
use Libraries.Interface.Selections.CodeEditorSelection
use Libraries.Interface.Controls.Blocks.BlockItem
use Libraries.Interface.Controls.Blocks.EditBox
use Libraries.Interface.Controls.Blocks.EditField
use Libraries.Interface.Controls.Blocks.Block

class ExecuteCodeCompletionBehavior is Behavior

    on create
        SetRunOnUpdate(true)
    end

    CodeCompletionItem completion = undefined
    TextBox box = undefined
    CodeEditor editor = undefined
    CodeCompletionResult result = undefined
    CodeCompletionMenu menu = undefined

    action GetTextBox returns TextBox
        return box
    end

    action SetTextBox(TextBox box)
        me:box = box
    end

    action GetCodeCompletionResult returns CodeCompletionResult
        return result
    end

    action SetCodeCompletionResult(CodeCompletionResult result)
        me:result = result
    end

    action SetCodeCompletionItem(CodeCompletionItem completion)
        me:completion = completion
    end

    action GetCodeCompletionItem returns CodeCompletionItem
        return completion
    end

    action Run(BehaviorEvent event)
        if completion not= undefined and result not= undefined and
            (box not= undefined or editor not= undefined)
            and completion:completionText not= undefined

            text value = completion:completionText
            text filter = result:filter
            integer size = 0
            if filter not= undefined
                size = filter:GetSize()
            end
            if value not= undefined and value:GetSize() > size
                value = value:GetSubtext(size)
            elseif (value not= undefined and value:GetSize() = size)
                or (value not= undefined and (value + "(") = filter)
                value = "" //the user already typed it, so just add parens if blank
            end
            integer position = -1



            if completion:isActionWithoutParameters
                value = value + "()"
            end
            if box not= undefined
                position = box:GetCaretPosition()
                box:Insert(position, value)
            elseif editor not= undefined
                position = editor:GetCaretPosition()
                InsertIntoCodeEditor(position, value)
            end
            //add another dot and popup code completion again.
            if result:IsUsage()
                if completion:GetType() = completion:PACKAGE and
                   value not= "all"
                    if box not= undefined and box is CodeTextBox
                        box:Insert(box:GetCaretPosition(), ".")
                        DoCodeCompletion(cast(CodeTextBox, box))
                    end
                end
            end
        end
    end

    private action InsertIntoCodeEditor(integer position, text value)
        CodeEditorSelection selection = editor:GetSelection()
        Block block = selection:GetBlock()
        BlockItem blockItem = selection:GetBlockItem()
        
        //first we calculate the local position compared to the block
        //we later need to subtract the position of the block relative to this
        integer localPosition = position
        localPosition = localPosition - block:GetStartIndex()
        if blockItem is EditBox
            EditBox edit = cast(EditBox, blockItem)
            localPosition = localPosition - edit:GetStartIndex()
            edit:Insert(localPosition, value)
        elseif blockItem is EditField
            EditField edit = cast(EditField, blockItem)
            localPosition = localPosition - edit:GetStartIndex()
            edit:Insert(localPosition, value)
        end
    end

    private action DoCodeCompletion(CodeTextBox box)
        BuildManager builder = menu:GetBuilder()

        menu:SetTextBox(box)
        menu:Generate(builder)

        box:ShowCaret()
    end

    action GetMenu returns CodeCompletionMenu
        return menu
    end

    action SetMenu(CodeCompletionMenu menu)
        me:menu = menu
    end

    action GetCodeEditor returns CodeEditor
        return editor
    end

    action SetCodeEditor(CodeEditor editor)
        me:editor = editor
    end

end