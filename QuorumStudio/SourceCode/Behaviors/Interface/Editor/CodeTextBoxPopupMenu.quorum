package Libraries.Development.Environment.Studio.Interface
use Libraries.Interface.Controls.PopupMenu
use Libraries.Interface.Controls.MenuItem
use Libraries.Containers.Iterator
use Libraries.Language.Compile.Hints.Hint
use Libraries.Interface.Behaviors.Controls.TextBoxCopyToClipboardBehavior
use Libraries.Interface.Behaviors.Controls.TextBoxPasteFromClipboardBehavior
use Libraries.Interface.Behaviors.Controls.TextBoxCutToClipboardBehavior
use Libraries.Development.Environment.Studio.Behaviors.ExecuteEditorHintBehavior
use Libraries.Game.GameStateManager
use Libraries.Development.Environment.Studio.QuorumStudio

class CodeTextBoxPopupMenu is PopupMenu
    CodeTextBox box = undefined
    MenuItem cut
    MenuItem copy    
    MenuItem paste

    on create
        GameStateManager manager
        QuorumStudio qs = cast(QuorumStudio, manager:GetGame())

        cut:SetName("Cut")
        cut:SetBehavior(qs:GetBehaviors():GetCutBehavior())
        //build:SetBehavior(behaviors:GetThreadedBuildProjectBehavior())

        
        copy:SetName("Copy")
        copy:SetBehavior(qs:GetBehaviors():GetCopyBehavior())
        //build:SetBehavior(behaviors:GetThreadedBuildProjectBehavior())

        
        paste:SetName("Paste")
        paste:SetBehavior(qs:GetBehaviors():GetPasteBehavior())
        //build:SetBehavior(behaviors:GetThreadedBuildProjectBehavior())

        
    end

    action GetCodeTextBox returns CodeTextBox
        return box
    end

    action SetCodeTextBox(CodeTextBox box)
        me:box = box
    end

    private action GenerateContextSensitiveMenus
        if box:HasHints()
            MenuItem hints
            hints:SetName("More Hints")
            integer numberOfHintsOnTop = 4

            i = 0
            Iterator<Hint> iterator = box:GetHintsAtLine(box:GetCaretLine())
            repeat while iterator:HasNext()
                Hint hint = iterator:Next()
                MenuItem hintItem
                hintItem:SetName(hint:GetHintMessage())
                ExecuteEditorHintBehavior behave
                behave:SetHint(hint)
                behave:SetCodeTextBox(box)

                hintItem:SetBehavior(behave)
                if i <= numberOfHintsOnTop
                    Add(hintItem)
                else
                    hints:Add(hintItem)
                end
                
                i = i + 1
            end

            if i > numberOfHintsOnTop
                Add(hints)
            end
        end
    end

    private action GenerateMenu
        //first regenerate the menu based upon context
        parent:PopupMenu:panel:Empty()
        if box not= undefined
            GenerateContextSensitiveMenus()
        end

        Add(cut)
        Add(copy)
        Add(paste)
    end

    action OpenAtCursor
        GenerateMenu()
        parent:PopupMenu:OpenAtCursor()
    end

    action OpenAt(number x, number y)
        GenerateMenu()
        parent:PopupMenu:OpenAt(x, y)
    end
end