package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Network.NetworkConnection
use Libraries.Network.NetworkRequest
use Libraries.Network.NetworkResponseEvent
use Libraries.Network.NetworkRequestListener
use Libraries.Data.Formats.JavaScriptObjectNotation
use Libraries.Development.Environment.Projects.ProjectManager
use Libraries.Development.Environment.Studio.Interface.CheckForUpdatesDialog
use Libraries.Development.Environment.Configuration.Options
use Libraries.Game.GameStateManager
use Libraries.Game.Game
use Libraries.Development.Environment.Projects.Credentials
use Libraries.Concurrency.ThreadRunner
use Libraries.System.Properties
use Libraries.System.File
use Libraries.Development.Environment.Studio.OutputEditorTabPane

class CheckForUpdatesBehavior is QuorumStudioBehavior, NetworkRequestListener
    QuorumStudio studio = undefined
    ProjectManager manager = undefined
    Options options = undefined
    constant text RELEASE_VERSION = "https://quorumlanguage.com/studio/version.json"
    constant text BETA_VERSION = "https://quorumlanguage.com/studio/beta.json"

    action SetMainApplication(QuorumStudio studio)
        me:studio = studio
        manager = studio:GetProjectManager()
        options = studio:GetOptions()
    end

    action GetMainApplication returns QuorumStudio
        return studio
    end

    action Run(BehaviorEvent event)
        NetworkRequest request
        request:SetRequestTypeToGet()
        // In order to successfully access our website, the user agent header must be provided.
        // What is provided is seemingly arbitrary, but this is pulled from the headers generated by Firefox to access this page.
        request:AddHeader("User-Agent", "Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:96.0) Gecko/20100101 Firefox/96.0")

        if options = undefined
            GameStateManager gsm
            studio = cast(QuorumStudio, gsm:GetGame())
            options = studio:GetOptions()
        end
        boolean beta = options:GetSpecialBetaBuild()
        //if this is a special beta build, then we want to look for the internal version 
        //on the server to grab only the internal beta build.
        if not beta
            request:SetWebAddress(RELEASE_VERSION)
        else 
            request:SetWebAddress(BETA_VERSION)
        end
        NetworkConnection http
        http:AddListener(me)

        check
            http:SendRequest(request)
        detect e
            CheckForUpdatesDialog dialog
            dialog:SetNewMajorVersion(-10)
            dialog:Initialize()
            dialog:Show()
        end
    end

    action ResponseReceived(NetworkResponseEvent response)
        text value = response:GetResponseText()
        integer major = -10
        integer minor = -10
        integer build = -10
        text windows = undefined
        text mac = undefined
        text macARM = undefined
        text updateFile = undefined
        if value not= undefined
            JavaScriptObjectNotation notation
            notation:Read(value)
            text formatted = notation:ToText()
        
            check
                major = cast(integer, notation:GetValue("MajorVersion"))
                minor = cast(integer, notation:GetValue("MinorVersion"))
                build = cast(integer, notation:GetValue("BuildVersion"))
                windows = notation:GetValue("Windows")
                mac = notation:GetValue("Mac")
                macARM = notation:GetValue("MacARM")
            detect e //ignore this
                return now //this file is corrupt, so you know, don't update to that.
            end
        end

        //The new version of quorum studio requires flags specifying which version 
        //we are getting from the system. 
        if windows = undefined or mac = undefined
            return now
        end

        //we need some place safe to store the file.
        File propertiesLocation = options:GetPropertiesLocation()
        if propertiesLocation = undefined or not propertiesLocation:Exists()
            return now
        end

        File downloadLocation
        downloadLocation:SetWorkingDirectory(propertiesLocation:GetAbsolutePath())

        //figure out what OS I'm on and then use that version for the update request
        Properties properties
        if properties:IsWindows()
            updateFile = windows
        elseif properties:IsMac() //look for ARM if we're on that architecture
            text arch = properties:GetProperty("os.arch")
            if arch not= undefined and arch = "aarch64"
                updateFile = macARM
            else
                updateFile = mac
            end
        end

        text name = options:GetAutoUpdateName()
        //something else is wrong and this system isn't supported for auto-update
        if name = undefined
            return now
        end

        downloadLocation:SetPath(name)
        //only mac and windows are supported for the auto-update system.
        if updateFile = undefined
            return now
        end

        //in Quorum Studio 4, we no longer require usernames or passwords for downloading.
        if studio:IsNewer(major, minor, build)
            SetupFileDownloadBehavior(major, minor, build, updateFile, downloadLocation)
        else //there is no new version. Just post the dialog
             //saying that we are up to date.
            CheckForUpdatesDialog dialog
            dialog:SetUpdateChecked(true)
            dialog:Initialize()
            dialog:Show()
        end
    end

    /*
        This action sets up downloading inside of Quorum Studio for the new version.
    */
    private action SetupFileDownloadBehavior(integer major, integer minor, integer build, text updateFile, File downloadLocation)
        Behaviors behaviors = studio:GetBehaviors()
        ThreadRunner thread = behaviors:GetNetworkThreadRunner()
        QuorumLoginBehavior qlb
        FileDownloadBehavior flb
        flb:SetFileDownloadToRequest(updateFile)
        flb:SetDownloadLocation(downloadLocation)
        RunProcessBehavior runBehave
        runBehave:SetMainApplication(studio)
        runBehave:SetEndProcessAfterCallingRun(true)
        runBehave:SetFile(downloadLocation)
        flb:SetPostDownloadBehavior(runBehave)

        qlb:SetSuccessfulLoginBehavior(flb)
        ThreadedBehavior behave

        behave:SetNestedBehavior(qlb)
        behave:SetThreadRunner(thread)
        thread:Add(behave)

        AdjustCheckForUpdatesBehavior adjust
        adjust:SetNewMajorVersion(major)
        adjust:SetNewMinorVersion(minor)
        adjust:SetNewBuildVersion(build)
        OutputEditorTabPane pane = studio:GetOutputPane()
        pane:AddBehavior(adjust)

        flb:SetDialog(adjust:GetDialog())
    end
end