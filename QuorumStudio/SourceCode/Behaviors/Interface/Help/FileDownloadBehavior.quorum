package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Network.NetworkConnection
use Libraries.Network.NetworkRequest
use Libraries.Network.NetworkResponseEvent
use Libraries.Network.NetworkRequestListener
use Libraries.Data.Formats.JavaScriptObjectNotation
use Libraries.Development.Environment.Projects.ProjectManager
use Libraries.Development.Environment.Studio.Interface.CheckForUpdatesDialog
use Libraries.Development.Environment.Configuration.Options
use Libraries.Game.GameStateManager
use Libraries.Game.Game
use Libraries.Development.Environment.Projects.Credentials
use Libraries.Containers.HashTable
use Libraries.Containers.Array
use Libraries.System.File
use Libraries.Development.Environment.Studio.Interface.OutputTab
use Libraries.Development.Environment.Studio.OutputEditorTabPane
use Libraries.Interface.Accessibility
use Libraries.Compute.Math
use Libraries.System.Logger

class FileDownloadBehavior is QuorumStudioBehavior, NetworkRequestListener
    QuorumStudio studio = undefined
    Options options = undefined
    text fileDownloadToRequest = ""
    File downloadLocation = undefined
    Behavior postDownloadBehavior = undefined
    Accessibility access = undefined
    OutputEditorTabPane pane = undefined
    OutputTab out = undefined
    CheckForUpdatesDialog dialog = undefined
    Logger logger

    action SetMainApplication(QuorumStudio studio)
        me:studio = studio
        options = studio:GetOptions()
        access = studio:GetAccessibility()
        pane = studio:GetOutputPane()
        out = pane:GetOutputTab()
    end

    action GetMainApplication returns QuorumStudio
        return studio
    end

    action Run(BehaviorEvent event)
        if options = undefined
            GameStateManager gsm
            studio = cast(QuorumStudio, gsm:GetGame())
            options = studio:GetOptions()
            access = studio:GetAccessibility()
            pane = studio:GetOutputPane()
            out = pane:GetOutputTab()
        end

        text update = "
Update Found. Downloading " + fileDownloadToRequest
        
        //out:Append(update)
        if access not= undefined
            access:Notify(out, update)
        end

        text cookie = options:GetLoginCookie()
        if cookie = undefined
            return now
        end
        NetworkRequest request
        request:SetRequestTypeToGet()
        request:SetWebAddress("https://quorumlanguage.com/downloadQuorum.php?file="+fileDownloadToRequest+"&")
        request:SetDownloadFile(GetDownloadLocation())
        request:AddHeader("Cookie", "sessionID=" + cookie)
        

        NetworkConnection http
        http:AddListener(me)

        check
            http:SendRequest(request)
        detect e
            logger:Log(e:GetStackTraceMessage())
        end      
    end

    action ResponseReceived(NetworkResponseEvent response)
        text update = "
Download Complete. Starting Installer."
        //out:Append(update)
        if access not= undefined
            access:Notify(out, update)
        end

        if dialog not= undefined
            dialog:SetShutdown(true)
        end
        if postDownloadBehavior not= undefined
            postDownloadBehavior:Run(undefined)
        end
    end

    action DownloadProgress(integer length, integer read)
        number l = length
        number r = read
        number percent = 100 * (r / l)

        if dialog not= undefined
            dialog:SetPercent(percent)
        end
    end

    action GetFileDownloadToRequest returns text
        return fileDownloadToRequest
    end

    action SetFileDownloadToRequest(text fileDownloadToRequest)
        me:fileDownloadToRequest = fileDownloadToRequest
    end

    action GetDownloadLocation returns File
        return downloadLocation
    end

    action SetDownloadLocation(File downloadLocation)
        me:downloadLocation = downloadLocation
    end

    action GetPostDownloadBehavior returns Behavior
        return postDownloadBehavior
    end

    action SetPostDownloadBehavior(Behavior postDownloadBehavior)
        me:postDownloadBehavior = postDownloadBehavior
    end

    action GetDialog returns CheckForUpdatesDialog
        return dialog
    end

    action SetDialog(CheckForUpdatesDialog dialog)
        me:dialog = dialog
    end
end