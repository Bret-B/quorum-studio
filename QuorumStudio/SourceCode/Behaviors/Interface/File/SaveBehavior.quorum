package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Development.Environment.Projects.ProjectTreeItem
use Libraries.Development.Environment.Studio.CodeEditorTabPane
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Interface.Selections.TabPaneSelection
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Item2D
use Libraries.Interface.Controls.TextBox
use Libraries.Development.Environment.Studio.Interface.EditorTab
use Libraries.Development.Environment.Studio.Interface.VisualEditorTab

class SaveBehavior is QuorumStudioBehavior

    /*
         This action saves to disk if the file is dirty.
    */
    action Run(BehaviorEvent event)
        QuorumStudio studio = GetMainApplication()
        CodeEditorTabPane pane = studio:GetTabPane()
        TabPaneSelection selection = pane:GetSelection()
        if selection not= undefined
            Tab tab = selection:GetTab()
            if tab is EditorTab
                EditorTab et = cast(EditorTab, tab)
                et:Save()
            elseif tab is VisualEditorTab
                VisualEditorTab visualTab = cast(VisualEditorTab, tab)
                visualTab:Save()
            end
        end
    end
end