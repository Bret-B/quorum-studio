package Libraries.Development.Environment.Studio.Behaviors

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Development.Environment.Studio.QuorumStudio
use Libraries.Development.Environment.Studio.StudioFocusManager
use Libraries.Development.Environment.Projects.Project
use Libraries.Development.Environment.Configuration.Options
use Libraries.Development.Environment.Projects.Quorum.QuorumProject
use Libraries.System.File
use Libraries.Development.Environment.Studio.CodeEditorTabPane
use Libraries.Interface.Controls.Tab
use Libraries.Development.Environment.Studio.Interface.EditorTab
use Libraries.Development.Environment.Studio.Interface.VisualEditorTab
use Libraries.Development.Versioning.AddRequest
use Libraries.Containers.Array
use Libraries.Development.Versioning.Repository
use Libraries.Development.Versioning.Git
use Libraries.Development.Environment.Studio.Interface.OutputTab
use Libraries.Development.Environment.Studio.OutputEditorTabPane

class AddProjectRequestBehavior is QuorumStudioBehavior

    action Run(BehaviorEvent event)
        QuorumStudio studio = GetMainApplication()
        StudioFocusManager manager = studio:GetStudioFocusManager()
        OutputEditorTabPane outPane = studio:GetOutputPane()
        OutputTab out = outPane:GetOutputTab()
        Project project = manager:GetProjectFocus()
        Options options = studio:GetOptions()
        CodeEditorTabPane pane = studio:GetTabPane()
        

        //Basic error checking out of the way, do the addition to the project repository
        if project is QuorumProject
            QuorumProject quorumProject = cast(QuorumProject, project)
            File projectLocation = quorumProject:GetLocation()
            if quorumProject:HasGitSupport()
                Git git
                Repository repo = quorumProject:GetGitRepository()
                AddRequest request
                Array<File> files = request:GetFilesToAdd()

                Array<File> sources = quorumProject:GetSources()
                i = 0
                repeat while i < sources:GetSize()
                    files:Add(sources:Get(i))
                    i = i + 1
                end

                File projectFile = quorumProject:GetPropertiesLocation()
                files:Add(projectFile)

                git:Add(repo, request)
                out:Append("")
                out:Append("Adding all source code and project files to the repository on your computer. You can run Status from the Team menu to see what changed.")
                out:Append("Please commit any changes and then push to send it to the team.")
            end
        end
    end
end